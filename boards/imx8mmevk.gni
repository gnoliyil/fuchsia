# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//boards/common/arm64-common.gni")

# The targeted CPU affects many parts of the build process.
target_cpu = "arm64"

board_name = "imx8mmevk"

# Board-specific configuration based on your targeted architecture and CPU.
# //build/config/arm64/BUILD.gn
board_configs += [ "//build/config/arm64:cortex-a53" ]

# The Zircon Boot Image (ZBI) contains everything needed to boot.
max_fuchsia_zbi_size = 33554432  # 32 mebibytes (MiB)

# Zedboot is a minimal recovery image that's used to install and boot a full
# Fuchsia system.
# https://fuchsia.dev/fuchsia-src/glossary#zedboot
max_zedboot_zbi_size = 33554432  # 32 MiB

# You'll put kernel command line arguments and driver dependencies here later.
_common_bootfs_deps = [
  "//src/devices/bin/driver_host2",
  "//src/devices/block/drivers/imx8m-sdmmc",
  "//src/devices/board/drivers/imx8mmevk",
  "//src/devices/gpio/drivers/imx8m-gpio",
  "//src/devices/i2c/drivers/imx8m-i2c",
]

board_information_for_assembly = "//boards/imx8mmevk/board_information.json5"

board_bootfs_labels += _common_bootfs_deps
board_zedboot_bootfs_labels += _common_bootfs_deps
board_recovery_bootfs_labels += _common_bootfs_deps

#board_driver_package_labels += []
#board_package_labels += []

# Supports only physical devices.
board_is_emu = false
board_is_phys = true

# Shell wrapper script around the main image packaging logic.
# //zircon/scripts/package-image.sh
custom_signing_script =
    "//zircon/kernel/target/arm64/board/imx8mmevk/package-image.sh"

# //zircon/kernel/target/arm64/boot-shim/BUILD.gn contains a lot of the logic
# for compiling your boot shim.
# //zircon/kernel/target/arm64/boot-shim/BUILD.gn
custom_signing_script_deps =
    [ "//zircon/kernel/target/arm64/boot-shim:imx8mmevk" ]

custom_signing_script_tools = [
  # Image compression tool.
  # https://fuchsia.dev/fuchsia-src/concepts/process/userboot#bootfs
  "//zircon/tools/lz4",

  # Zircon Boot Image creation tool.
  # //zircon/tools/zbi/zbi.cc
  "//zircon/tools/zbi",
]

images_config_label = "//boards/images:imx8mmevk"
zedboot_images_config_label = "//boards/images:zedboot_imx8mmevk"
bringup_images_config_label = "//boards/images:bringup_imx8mmevk"
bringup_fastboot_images_config_label =
    "//boards/images:bringup_fastboot_imx8mmevk"
