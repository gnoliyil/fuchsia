# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/generated_partitions_config.gni")

_standard_names = {
  zbi_a = "zircon_a"
  zbi_b = "zircon_b"
  zbi_r = "zircon_r"
  vbmeta_a = "vbmeta_a"
  vbmeta_b = "vbmeta_b"
  vbmeta_r = "vbmeta_r"
  fvm = "fvm"
}

_vim3_boot = {
  bootloader_partitions = [
    {
      image = "//prebuilt/third_party/firmware/vim3/u-boot.bin.unsigned"
      type = ""
      name = "bootloader"
    },
  ]
}

_gigaboot = {
  testonly = true
  bootloader_partitions = [
    {
      image = "$root_build_dir/fuchsia.esp.blk"
      type = ""
      name = "fuchsia-esp"
    },
  ]
  deps = [ "//build/images/flash:esp" ]
}

generated_partitions_config("default") {
  hw_revision = ""
}

generated_partitions_config("x64") {
  forward_variables_from(_gigaboot, "*")
  hw_revision = "x64"

  # TODO: Investigate if we can change these to the standard names.
  zbi_a = "zircon-a"
  zbi_b = "zircon-b"
  zbi_r = "zircon-r"
  vbmeta_a = "vbmeta_a"
  vbmeta_b = "vbmeta_b"
  vbmeta_r = "vbmeta_r"
  fvm = "fvm"
}

generated_partitions_config("x64-fxfs") {
  forward_variables_from(_gigaboot, "*")
  hw_revision = "x64"

  forward_variables_from(_standard_names, "*", [ "fvm" ])
  fxfs = "fvm"
}

generated_partitions_config("arm64") {
  forward_variables_from(_gigaboot, "*")
  hw_revision = "arm64"
}

generated_partitions_config("riscv64") {
  hw_revision = "riscv64"
}

generated_partitions_config("qemu-x64") {
  hw_revision = "qemu-x64"
}

generated_partitions_config("as370") {
  hw_revision = "av370"
  zbi_a = "boot"
}

generated_partitions_config("av400") {
  forward_variables_from(_vim3_boot, "*")
  forward_variables_from(_standard_names, "*")
  hw_revision = "av400"
}

generated_partitions_config("buckeye") {
  forward_variables_from(_vim3_boot, "*")
  forward_variables_from(_standard_names, "*")
  hw_revision = "buckeye"
}

# NOTE: if you touch this target, please add this line to your commit
# description so that the vim3 tests are run as we do not run vim3 tests in CQ
# by default.
# Cq-Include-Trybots: luci.turquoise.global.try:bringup.vim3-debug,core.vim3-debug,core.vim3-vg-debug
generated_partitions_config("vim3") {
  forward_variables_from(_vim3_boot, "*")
  forward_variables_from(_standard_names, "*")
  hw_revision = "vim3"
}

generated_partitions_config("chromebook-x64") {
  forward_variables_from(_standard_names, "*")
  hw_revision = "chromebook-x64"
  bootloader_partitions = [
    {
      image = "//prebuilt/third_party/firmware/atlas/fuchsia_image.dev.bin"
      type = "ap"

      # TODO(fxbug.dev/83462): support flashing firmware over fastboot.
      #name = "bootloader"
    },
  ]
}

generated_partitions_config("clover") {
  forward_variables_from(_standard_names, "*")
  hw_revision = "clover"
}

group("partitions") {
  testonly = true
  public_deps = [
    ":arm64",
    ":as370",
    ":av400",
    ":buckeye",
    ":chromebook-x64",
    ":clover",
    ":qemu-x64",
    ":vim3",
    ":x64",
    ":x64-fxfs",
  ]
}
