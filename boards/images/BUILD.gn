# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/generated_images_config.gni")
import("//build/images/vboot/vboot.gni")

#################################
# Fuchsia images configurations #
#################################

_default_zbi = {
  zbi = {
    name = "fuchsia"
    compression = "zstd"
  }
}

_default_vbmeta = {
  vbmeta = {
    key = "//third_party/android/platform/external/avb/test/data/testkey_atx_psk.pem"
    key_metadata =
        "//third_party/android/platform/external/avb/test/data/atx_metadata.bin"
  }
}

_vim3_vbmeta = {
  vbmeta = {
    key = "//src/firmware/avb_keys/vim3/vim3-dev-key/vim3_devkey_atx_psk.pem"
    key_metadata =
        "//src/firmware/avb_keys/vim3/vim3-dev-key/vim3_dev_atx_metadata.bin"
  }
}

_default_fvm = {
  fvm = {
    slice_size = 8388608
    blobfs = {
      layout = "compact"
    }
    reserved = {
      slices = 0
    }
  }
}

_default_fxfs = {
  fxfs = {
  }
}

_default_fvm_with_size_limits = {
  fvm = {
    slice_size = 8388608
    blobfs = {
      layout = "compact"
      maximum_contents_size = 5216665600
    }
    reserved = {
      slices = 0
    }
  }
}

generated_images_config("default") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm, "*")
}

generated_images_config("arm64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")
}

generated_images_config("riscv64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm, "*")
}

generated_images_config("x64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  fvm.emmc = {
    compress = false
    truncate_to_length = 17179869184
  }
}

generated_images_config("x64-fxfs") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fxfs, "*")
}

generated_images_config("qemu-x64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm, "*")
}

generated_images_config("qemu-arm64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")
}

generated_images_config("av400") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  fvm.emmc = {
    compress = true
    truncate_to_length = 3221225472
  }
}

generated_images_config("buckeye") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  fvm.emmc = {
    compress = true
    truncate_to_length = 3221225472
  }
}

# NOTE: if you touch this target, please add this line to your commit
# description so that the vim3 tests are run as we do not run vim3 tests in CQ
# by default.
# Cq-Include-Trybots: luci.turquoise.global.try:bringup.vim3-debug,core.vim3-debug,core.vim3-vg-debug
generated_images_config("vim3") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  fvm.emmc = {
    compress = true
    truncate_to_length = 3439329280
  }
}

generated_images_config("clover") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  fvm.nand = {
    compress = false
    page_size = 4096
    oob_size = 128
    pages_per_block = 64
    block_count = 1604

    truncate_to_length = 3221225472
  }
}

generated_images_config("chromebook-x64") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  forward_variables_from(_default_fvm, "*")

  zbi.signing_script = vboot_action.script
  zbi.signing_args = vboot_action.args

  fvm.emmc = {
    compress = false
    truncate_to_length = 17179869184
  }
}

generated_images_config("imx8mmevk") {
  forward_variables_from(_default_zbi, "*")
  forward_variables_from(_default_fvm_with_size_limits, "*")

  zbi.signing_script =
      "//zircon/kernel/target/arm64/board/imx8mmevk/package-image.sh"
  deps = [ "//zircon/kernel/target/arm64/boot-shim:imx8mmevk" ]
}

#################################
# zedboot images configurations #
#################################

_default_zedboot_zbi = {
  zbi = {
    name = "zedboot"
    compression = "zstd"
  }
}

generated_images_config("zedboot_default") {
  forward_variables_from(_default_zedboot_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
}

generated_images_config("zedboot_vim3") {
  forward_variables_from(_default_zedboot_zbi, "*")
  forward_variables_from(_vim3_vbmeta, "*")
}

generated_images_config("zedboot_imx8mmevk") {
  forward_variables_from(_default_zedboot_zbi, "*")
  zbi.signing_script =
      "//zircon/kernel/target/arm64/board/imx8mmevk/package-image.sh"
  deps = [ "//zircon/kernel/target/arm64/boot-shim:imx8mmevk" ]
}

generated_images_config("zedboot_chromebook-x64") {
  forward_variables_from(_default_zedboot_zbi, "*")
  forward_variables_from(_default_vbmeta, "*")
  zbi.signing_script = vboot_action.script
  zbi.signing_args = vboot_action.args
}

generated_images_config("zedboot_qemu") {
  forward_variables_from(_default_zedboot_zbi, "*")
}

group("images") {
  public_deps = [
    # Fuchsia images configs
    ":arm64",
    ":av400",
    ":buckeye",
    ":chromebook-x64",
    ":clover",
    ":default",
    ":imx8mmevk",
    ":qemu-x64",
    ":vim3",
    ":x64",
    ":x64-fxfs",

    # zedboot images configs
    ":zedboot_chromebook-x64",
    ":zedboot_default",
    ":zedboot_imx8mmevk",
    ":zedboot_qemu",
    ":zedboot_vim3",
  ]
}
