# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/board_configuration.gni")
import("//build/assembly/board_input_bundle.gni")

assert(current_toolchain == default_toolchain,
       "The arm64 board can only be defined in the default toolchain.")
assert(current_cpu == "arm64",
       "The arm64 board can only be defined in an arm64 toolchain.")

board_configuration("arm64") {
  provided_features = [ "fuchsia::driver_framework_v2_support" ]

  input_bundles = [
    ":main_bundle",
    "//boards/emulation/virtio",
  ]

  filesystems = {
    vbmeta = {
      key = "//third_party/android/platform/external/avb/test/data/testkey_atx_psk.pem"
      key_metadata = "//third_party/android/platform/external/avb/test/data/atx_metadata.bin"
    }
    fvm = {
      blobfs = {
        size_checker_maximum_bytes = 5216665600
      }
      sparse_output = {
      }
    }
    fxfs = {
      size_checker_maximum_bytes = 5216665600
    }
  }
}

board_input_bundle("main_bundle") {
  configuration = {
    power_manager =
        "//src/power/power-manager/node_config/base_node_config.json5"
  }
  base_packages = [ "//src/graphics/bin/vulkan_loader" ]

  drivers = [
    {
      package_target = "//src/devices/block/drivers/nvme:package"
      package_set = "bootfs"
      driver_components = [ "meta/nvme.cm" ]
    },
    {
      package_target = "//src/devices/bus/drivers/pci:package"
      package_set = "bootfs"
      driver_components = [ "meta/bus-pci.cm" ]
    },
    {
      package_target = "//src/devices/usb/drivers/xhci:package"
      package_set = "bootfs"
      driver_components = [ "meta/xhci.cm" ]
    },
  ]

  # Additional drivers to support running as a virtualization guest.
  drivers += [
    {
      package_target = "//src/devices/block/drivers/ahci:package"
      package_set = "bootfs"
      driver_components = [ "meta/ahci.cm" ]
    },
    {
      package_target = "//src/devices/board/drivers/acpi-arm64:package"
      package_set = "bootfs"
      driver_components = [ "meta/acpi-arm64.cm" ]
    },
    {
      package_target = "//src/devices/board/drivers/qemu-arm64:package"
      package_set = "bootfs"
      driver_components = [ "meta/qemu-arm64.cm" ]
    },
    {
      package_target = "//src/devices/rtc/drivers/pl031-rtc:package"
      package_set = "bootfs"
      driver_components = [ "meta/pl031-rtc.cm" ]
    },
  ]
}
