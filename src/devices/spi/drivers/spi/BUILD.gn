# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("spi_bind") {
  rules = "spi.bind"
  bind_output = "spi.bindbc"
  tests = "tests.json"
  deps = [
    "//sdk/fidl/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_bindlib",
    "//src/devices/bind/fuchsia.spi",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":spi-test",
    ":spi_bind_test",
  ]
}

group("common") {
  public_deps = [
    ":spi_bind",
    "//sdk/banjo/fuchsia.hardware.spi:fuchsia.hardware.spi_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.spi:fuchsia.hardware.spi_cpp",
    "//sdk/fidl/fuchsia.hardware.spi.businfo:fuchsia.hardware.spi.businfo_cpp",
    "//sdk/fidl/fuchsia.hardware.spiimpl:fuchsia.hardware.spiimpl_cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("spi-driver") {
  output_name = "spi"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "spi-banjo-child.cc",
    "spi-child.cc",
    "spi-impl-client.cc",
    "spi.cc",
  ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("spi") {
  visibility = [ ":*" ]
  info = "spi-info.json"
  component_name = "spi"
  manifest = "meta/spi.cml"
  deps = [ ":spi-driver" ]
}

fuchsia_driver_package("package") {
  package_name = "spi"
  driver_components = [ ":spi" ]
}

test("spi-test-bin") {
  output_name = "spi-test"
  sources = [
    "spi-banjo-child.cc",
    "spi-child.cc",
    "spi-impl-client.cc",
    "spi-test.cc",
    "spi.cc",
  ]
  deps = [
    ":common",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/lib/fidl-metadata",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/spi",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("spi-test") {
  deps = [ ":spi-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
