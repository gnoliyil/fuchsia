# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml-securemem-bind") {
  rules = "aml-securemem.bind"
  bind_output = "aml-securemem.bindbc"
  tests = "aml-securemem-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.amlogic.platform" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-securemem-bind_test",
    ":aml-securemem-test",
  ]
}

fuchsia_driver("aml-securemem-driver") {
  output_name = "aml-securemem"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "device.cc",
    "device.h",
    "secmem-session.cc",
    "sysmem-secure-mem-server.cc",
  ]

  deps = [
    ":aml-securemem-bind",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.securemem:fuchsia.hardware.securemem_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.tee:fuchsia.hardware.tee_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire",
    "//sdk/fidl/fuchsia.tee:fuchsia.tee_hlcpp",
    "//sdk/lib/async_patterns/cpp",
    "//sdk/lib/fit-promise",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/security/lib/tee/tee-client-api:tee-client-api-headers",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
}

fuchsia_driver_component("aml-securemem") {
  info = "aml-securemem-info.json"
  component_name = "aml-securemem"
  deps = [ ":aml-securemem-driver" ]
  manifest = "meta/aml-securemem.cml"
}

test("aml-securemem-test-bin") {
  output_name = "aml-securemem-test"
  sources = [
    "device.cc",
    "device.h",
    "secmem-session.cc",
    "sysmem-secure-mem-server.cc",
    "test/aml-securemem-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    ":aml-securemem-bind",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.securemem:fuchsia.hardware.securemem_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.tee:fuchsia.hardware.tee_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire",
    "//sdk/fidl/fuchsia.tee:fuchsia.tee_hlcpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/driver/runtime:driver_runtime_testing",
    "//sdk/lib/fit-promise",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/testing/driver-runtime-main:zxtest",
    "//src/devices/testing/mock-ddk",
    "//src/security/lib/tee/tee-client-api:tee-client-api-static",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/zxtest",
    "//zircon/third_party/ulib/safemath",
  ]
}

fuchsia_unittest_package("aml-securemem-test") {
  deps = [ ":aml-securemem-test-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
