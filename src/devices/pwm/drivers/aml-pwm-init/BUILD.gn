# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml-pwm-init-bind") {
  rules = "aml-pwm-init.bind"
  bind_output = "aml-pwm-init.bindbc"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.amlogic.platform" ]
}

fuchsia_driver("aml-pwm-init-driver") {
  output_name = "aml-pwm-init"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "aml-pwm-init.cc" ]
  public_deps =
      [ "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp_wire" ]
  deps = [
    ":aml-pwm-init-bind",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp_wire",
    "//src/devices/bind/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
  ]
}

fuchsia_driver_component("aml-pwm-init") {
  info = "aml-pwm-init-info.json"
  component_name = "aml-pwm-init"
  deps = [ ":aml-pwm-init-driver" ]
  manifest = "meta/aml-pwm-init.cml"
}

test("aml-pwm-init-test-bin") {
  output_name = "aml-pwm-init-test"
  sources = [
    "aml-pwm-init-test.cc",
    "aml-pwm-init.cc",
  ]
  deps = [
    ":aml-pwm-init-bind",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp_wire",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/bind/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp",
    "//src/devices/gpio/testing/fake-gpio",
    "//src/devices/lib/amlogic",
    "//src/devices/testing/no_ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("aml-pwm-init-test") {
  deps = [ ":aml-pwm-init-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":aml-pwm-init-test" ]
}
