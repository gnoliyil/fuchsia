# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":package" ]
}

group("drivers") {
  testonly = true
  deps = [
    "leaf:component",
    "root:component",
  ]
}

fidl("fuchsia.compat.runtime") {
  sources = [ "compat.runtime.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

rustc_test("compat-runtime-v1-test") {
  edition = "2021"
  testonly = true
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    ":fuchsia.compat.runtime_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/device-watcher/rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_unittest_package("package") {
  package_name = "compat-runtime-v1-test"
  deps = [
    ":compat-runtime-v1-test",
    "leaf:component",
    "root:component",
    "//src/devices/misc/drivers/test-parent",
  ]
}
