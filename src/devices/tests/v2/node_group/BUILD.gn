# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/drivers.gni")
import("//build/rust/rustc_test.gni")
import("//sdk/ctf/build/ctf.gni")

group("tests") {
  testonly = true
  deps = [ ":nodegroup_test_latest" ]
}

group("ctf-tests") {
  testonly = true
  deps = [ ":nodegroup_test_archive" ]
}

fidl("fuchsia.nodegroup.test") {
  testonly = true
  sources = [ "nodegroup.test.fidl" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

bind_library("fuchsia.nodegroupbind.test") {
  source = "nodegroup.test.bind"
}

driver_bind_rules("root_bind") {
  rules = "root.bind"
  deps = [ "//src/devices/bind/fuchsia.test" ]
  bind_output = "root.bindbc"
}

fuchsia_driver("root_driver") {
  testonly = true
  output_name = "nodegroup_root"
  sources = [ "root_driver.cc" ]
  deps = [
    ":fuchsia.nodegroup.test_cpp",
    ":fuchsia.nodegroupbind.test_cpp",
    ":root_bind",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("root") {
  testonly = true
  manifest = "meta/root.cml"
  deps = [ ":root_driver" ]
  info = "root-info.json"
}

driver_bind_rules("nodegroup_leaf_bind") {
  rules = "leaf.bind"
  bind_output = "leaf.bindbc"
  deps = [ ":fuchsia.nodegroupbind.test" ]
}

fuchsia_driver("leaf_driver") {
  testonly = true
  output_name = "nodegroup_leaf"
  sources = [ "leaf_driver.cc" ]
  deps = [
    ":fuchsia.nodegroup.test_cpp",
    ":nodegroup_leaf_bind",
    "//sdk/lib/driver/component/cpp:cpp",
    "//src/devices/lib/driver:driver_runtime",
    "//zircon/system/ulib/async:async-cpp",
  ]
}

fuchsia_driver_component("leaf") {
  testonly = true
  manifest = "meta/leaf.cml"
  deps = [ ":leaf_driver" ]
  info = "leaf-info.json"
}

rustc_test("bin") {
  edition = "2021"
  testonly = true
  output_name = "nodegroup_test_bin"
  source_root = "test.rs"
  sources = [ "test.rs" ]
  deps = [
    ":fuchsia.nodegroup.test_rust",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/devices/testing/fidl:fuchsia.driver.testing_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/testing/realm_proxy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component("nodegroup_test_component") {
  testonly = true
  component_name = "test-suite"
  manifest = "meta/test-suite.cml"
  deps = [ ":bin" ]
}

ctf_fuchsia_package("nodegroup_test") {
  testonly = true
  package_name = "nodegroup_test"
  deps = [
    ":leaf",
    ":nodegroup_test_component",
    ":root",
  ]
}

fuchsia_test_component("nodegroup_test_root") {
  testonly = true
  manifest = "meta/test-root.cml"
  test_type = "ctf"
}

fuchsia_test_package("nodegroup_test_latest") {
  test_components = [ ":nodegroup_test_root" ]
  subpackages = [
    "//src/devices/testing/realm-factory",
    ":nodegroup_test",
  ]
}
