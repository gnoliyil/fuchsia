# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/drivers/driver_compat_test.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

fidl("fuchsia.device.inspect.test") {
  testonly = true
  sources = [ "inspect.test.fidl" ]
  visibility = [ ":*" ]
  public_deps = [ "//zircon/vdso/zx" ]
}

driver_bind_rules("inspect-test-bind") {
  rules = "inspect-test.bind"
  deps = [ "//src/devices/bind/fuchsia.test.platform" ]
}

fuchsia_driver("fuchsia_driver") {
  output_name = "inspect-test"
  testonly = true

  sources = [ "test-driver.cc" ]
  deps = [
    ":fuchsia.device.inspect.test_cpp_wire",
    ":inspect-test-bind",
    "//sdk/lib/fdio",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
  ]
  visibility = [
    ":*",
    "//src/devices/bundles:drivers",
  ]
}

fuchsia_driver_component("driver") {
  testonly = true
  component_name = "inspect-test"
  deps = [ ":fuchsia_driver" ]
  info = "driver-info.json"
  manifest = "meta/driver.cml"
}

driver_compat_test("inspect-test") {
  testonly = true
  sources = [ "test.cc" ]
  deps = [
    ":fuchsia.device.inspect.test_cpp_wire",
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp_wire",
    "//sdk/lib/fdio",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/devices/testing/driver-integration-test-shim",
    "//src/lib/ddk",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
  visibility = [ ":*" ]
}

fuchsia_unittest_component("inspect-test-dfv1") {
  deps = [ ":inspect-test_dfv1" ]
}

fuchsia_unittest_component("inspect-test-dfv2") {
  deps = [ ":inspect-test_dfv2" ]
}

fuchsia_test_package("driver-inspect-test") {
  test_components = [
    ":inspect-test-dfv1",
    ":inspect-test-dfv2",
  ]
  deps = [
    ":driver",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
}
