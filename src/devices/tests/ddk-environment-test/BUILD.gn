# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

driver_bind_rules("test-environment-bind") {
  rules = "test-environment.bind"
  deps = [ "//src/devices/bind/fuchsia.test.platform" ]
}

fidl("fuchsia.device.environment.test") {
  sources = [ "environment.test.fidl" ]
  excluded_checks = [
    "string-bounds-not-specified",
    "vector-bounds-not-specified",
  ]
}

fuchsia_driver("ddk-environment-test-driver") {
  output_name = "ddk-environment-test"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "test-driver.cc" ]
  deps = [
    ":fuchsia.device.environment.test_cpp_wire",
    ":test-environment-bind",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp:backend",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/files",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_driver_component("ddk-environment-test") {
  component_name = "ddk-environment-test"
  deps = [ ":ddk-environment-test-driver" ]
  info = "ddk-environment-test-info.json"
  manifest = "meta/ddk-environment-test.cml"
}

test("ddk-environment") {
  sources = [ "test.cc" ]
  deps = [
    ":fuchsia.device.environment.test_cpp_wire",
    "//src/devices/testing/driver-integration-test-shim",
    "//src/lib/ddk",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("ddk-environment-test-package") {
  deps = [
    ":ddk-environment",
    ":ddk-environment-test",
    "//src/devices/board/drivers/integration-test",
    "//src/devices/bus/drivers/platform",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":ddk-environment-test-package" ]
}
