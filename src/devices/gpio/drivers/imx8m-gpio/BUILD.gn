# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("imx8m-gpio-bind") {
  rules = "imx8m-gpio.bind"
  bind_output = "imx8m-gpio.bindbc"
  tests = "imx8m-gpio-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.nxp.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("imx8m-gpio-driver") {
  output_name = "imx8m-gpio"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "imx8m-gpio.cc" ]
  deps = [
    ":imx8m-gpio-bind",
    "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/devices/lib/nxp",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("imx8m-gpio") {
  component_name = "imx8m-gpio"
  deps = [ ":imx8m-gpio-driver" ]
  info = "imx8m-gpio-info.json"
  manifest = "meta/imx8m-gpio.cml"
}

test("imx8m-gpio-test-bin") {
  output_name = "imx8m-gpio-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "imx8m-gpio-test.cc",
    "imx8m-gpio.cc",
  ]
  deps = [
    ":imx8m-gpio-bind",
    "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//sdk/lib/fdio",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/devices/lib/nxp",
    "//src/devices/testing/mock-mmio-reg",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("imx8m-gpio-test") {
  deps = [ ":imx8m-gpio-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":imx8m-gpio-bind_test",
    ":imx8m-gpio-test",
  ]
}
