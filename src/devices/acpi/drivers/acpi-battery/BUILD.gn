# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [ ":acpi-battery-tests" ]
}

driver_bind_rules("bind") {
  rules = "acpi_battery.bind"
  bind_output = "acpi_battery.bindbc"
  deps = [ "//src/devices/bind/fuchsia.acpi" ]
}

common_deps = [
  ":bind",
  "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_cpp_wire",
  "//sdk/fidl/fuchsia.hardware.powersource:fuchsia.hardware.powersource_cpp_wire",
  "//sdk/lib/driver/runtime:driver_runtime_cpp",
  "//src/devices/lib/acpi",
  "//src/devices/lib/driver:driver_runtime",
  "//src/lib/ddktl",
  "//zircon/system/ulib/async-loop",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("driver") {
  output_name = "acpi-battery"
  sources = [ "acpi_battery.cc" ]
  deps = common_deps + [ "//src/devices/lib/driver" ]
}

fuchsia_driver_component("component") {
  component_name = "acpi-battery"
  deps = [ ":driver" ]
  info = "component-info.json"
  manifest = "meta/acpi-battery.cml"
}

fuchsia_driver_package("acpi-battery") {
  driver_components = [ ":component" ]
}

test("acpi-battery-unit-tests") {
  sources = [
    "acpi_battery.cc",
    "acpi_battery.h",
    "unit-tests.cc",
  ]
  deps = common_deps + [
           "//sdk/lib/inspect/testing/cpp:zxtest",
           "//src/devices/lib/acpi/mock:mock-acpi",
           "//src/devices/testing/mock-ddk",
           "//zircon/system/ulib/async-loop:async-loop-cpp",
           "//zircon/system/ulib/zxtest",
         ]
}

fuchsia_unittest_package("acpi-battery-tests") {
  deps = [ ":acpi-battery-unit-tests" ]
}
