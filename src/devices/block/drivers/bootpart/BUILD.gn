# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("bootpart_bind") {
  disable_autobind = true
  rules = "bootpart.bind"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.hardware.block" ]
}

fuchsia_driver("bootpart-driver") {
  output_name = "bootpart"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "bootpart.cc" ]
  deps = [
    ":bootpart_bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/lib/zbi-format",
    "//src/devices/block/lib/common",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver_component("bootpart") {
  component_name = "bootpart"
  deps = [ ":bootpart-driver" ]
  info = "bootpart-info.json"
  manifest = "meta/bootpart.cml"
}

test("bootpart-test-bin") {
  output_name = "bootpart-test"
  sources = [
    "bootpart-test.cc",
    "bootpart.cc",
  ]
  deps = [
    ":bootpart_bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/lib/zbi-format",
    "//src/devices/block/lib/common",
    "//src/devices/lib/driver",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("bootpart-test") {
  deps = [ ":bootpart-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":bootpart-test",
    ":bootpart_bind_test",
  ]
}
