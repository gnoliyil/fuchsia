# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml-sdmmc-bind") {
  rules = "aml-sdmmc.bind"
  bind_output = "aml-sdmmc.bindbc"
  tests = "aml-sdmmc-bind-test.json"
  deps = [ "//src/devices/bind/fuchsia.amlogic.platform" ]
}

fuchsia_driver("aml-sdmmc-driver") {
  output_name = "aml-sdmmc"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",

    # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
    "//build/config:Wno-vla-cxx-extension",
  ]
  sources = [
    "aml-sdmmc-export.cc",
    "aml-sdmmc.cc",
  ]
  deps = [
    ":aml-sdmmc-bind",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/fit",
    "//sdk/lib/inspect/component/cpp",
    "//src/devices/bin/driver_runtime",
    "//src/devices/block/lib/sdio",
    "//src/devices/block/lib/sdmmc",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/mmio",
    "//src/lib/vmo_store",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

# TODO(hanbinyoon): Transition users to the identical component below, and then remove this.
fuchsia_driver_component("aml-sdmmc-dfv2") {
  component_name = "aml-sdmmc-dfv2"
  deps = [ ":aml-sdmmc-driver" ]
  info = "aml-sdmmc-info.json"
  manifest = "meta/aml-sdmmc.cml"
}

fuchsia_driver_component("aml-sdmmc") {
  component_name = "aml-sdmmc"
  deps = [ ":aml-sdmmc-driver" ]
  info = "aml-sdmmc-info.json"
  manifest = "meta/aml-sdmmc.cml"
}

# TODO(hanbinyoon): Transition users to the identical package below, and then remove this.
fuchsia_driver_package("package-dfv2") {
  package_name = "aml-sdmmc-dfv2"
  driver_components = [ ":aml-sdmmc-dfv2" ]
}

fuchsia_driver_package("package") {
  package_name = "aml-sdmmc"
  driver_components = [ ":aml-sdmmc" ]
}

test("aml-sdmmc-test-bin") {
  output_name = "aml-sdmmc-test"
  sources = [
    "aml-sdmmc-test.cc",
    "aml-sdmmc.cc",
  ]
  deps = [
    ":aml-sdmmc-bind",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/testing/cpp:cpp",
    "//sdk/lib/fit",
    "//sdk/lib/inspect/component/cpp",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/devices/block/lib/sdio",
    "//src/devices/block/lib/sdmmc",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/dma-buffer",
    "//src/devices/lib/mmio",
    "//src/devices/lib/mmio:test_helper",
    "//src/devices/testing/fake-bti",
    "//src/lib/vmo_store",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("aml-sdmmc-test") {
  deps = [ ":aml-sdmmc-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":aml-sdmmc-bind_test",
    ":aml-sdmmc-test",
  ]
}
