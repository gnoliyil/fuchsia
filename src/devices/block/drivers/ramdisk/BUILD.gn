# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("ramdisk_bind") {
  rules = "ramdisk.bind"
  bind_output = "ramdisk.bindbc"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

fuchsia_driver("ramdisk-driver") {
  output_name = "ramdisk"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "ramdisk-controller.cc",
    "ramdisk.cc",
  ]
  deps = [
    ":ramdisk_bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.ramdisk:fuchsia.hardware.ramdisk_cpp_wire",
    "//sdk/lib/zbi-format",
    "//src/devices/block/lib/common",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("ramdisk") {
  visibility = [ ":*" ]
  component_name = "ramdisk"
  deps = [ ":ramdisk-driver" ]
  info = "ramdisk-info.json"
  manifest = "meta/ramdisk.cml"
}

bootfs_files_for_assembly("bootfs") {
  deps = [ ":ramdisk" ]
}

group("tests") {
  testonly = true
  deps = [
    ":ramdisk_bind_test",
    "test:tests",
  ]
}
