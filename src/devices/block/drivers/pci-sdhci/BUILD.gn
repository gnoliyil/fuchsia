# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("pci-sdhci-bind") {
  rules = "pci-sdhci.bind"
  header_output = "pci-sdhci-bind.h"
  tests = "pci-sdhci-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.pci",
  ]
}

fuchsia_driver("pci-sdhci-driver") {
  output_name = "pci-sdhci"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "pci-sdhci.cc" ]
  deps = [
    ":pci-sdhci-bind_header",
    "//sdk/banjo/fuchsia.hardware.sdhci:fuchsia.hardware.sdhci_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//src/devices/lib/driver",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddk",
    "//zircon/system/ulib/sync",
  ]
}

fuchsia_driver_component("pci-sdhci") {
  component_name = "pci-sdhci"
  deps = [
    ":pci-sdhci-bind",
    ":pci-sdhci-driver",
  ]
  info = "pci-sdhci-info.json"
  manifest = "meta/pci-sdhci.cml"
}

group("tests") {
  testonly = true
  deps = [ ":pci-sdhci-bind_test" ]
}
