# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("ufs_bind") {
  rules = "ufs.bind"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.pci.massstorage",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":ufs-tests",
    ":ufs_bind_test",
  ]
}

source_set("shared") {
  sources = [
    "logical_unit.cc",
    "request_list.cc",
    "transfer_request_processor.cc",
    "ufs.cc",
    "uic/uic_commands.cc",
  ]
  configs += [ "//build/config:all_source" ]
  deps = [
    ":ufs_bind",
    "//src/devices/block/lib/common",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/safemath",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//src/devices/lib/dev-operation",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/pci/lib/pci",
    "//zircon/system/ulib/hwreg",
  ]
}

fuchsia_driver("ufs-driver") {
  output_name = "ufs"
  deps = [ ":shared" ]
}

fuchsia_driver_component("ufs") {
  component_name = "ufs"
  deps = [ ":ufs-driver" ]
  info = "ufs-info.json"
  manifest = "meta/ufs.cml"
}

fuchsia_driver_package("pkg") {
  package_name = "ufs"
  driver_components = [ ":ufs" ]
}

test_deps = [
  "//sdk/lib/inspect/testing/cpp:zxtest",
  "//src/devices/testing/fake-bti",
  "//src/devices/testing/fake-mmio-reg",
  "//src/devices/testing/mock-ddk",
  "//src/lib/json_parser",
  "//src/lib/storage/block_client/cpp",
  "//zircon/system/ulib/fdio-caller",
  "//zircon/system/ulib/zxtest",
  "//zircon/third_party/ulib/safemath",
]

test("ufs-unit-tests-bin") {
  output_name = "ufs-unit-tests"
  sources = [
    "test/block-op-test.cc",
    "test/init-test.cc",
    "test/mock-device/fake-dma-handler.cc",
    "test/mock-device/query-request-processor.cc",
    "test/mock-device/register-mmio-processor.cc",
    "test/mock-device/scsi-command-processor.cc",
    "test/mock-device/transfer-request-processor.cc",
    "test/mock-device/ufs-mock-device.cc",
    "test/mock-device/uiccmd-processor.cc",
    "test/query-request-test.cc",
    "test/register-test.cc",
    "test/request-processor-test.cc",
    "test/scsi-command-test.cc",
    "test/uic-test.cc",
    "test/unit-lib.cc",
  ]
  deps = test_deps + [ ":shared" ]
}

fuchsia_unittest_component("ufs-unit-tests") {
  manifest = "test/meta/unit_tests.cml"
  deps = [ ":ufs-unit-tests-bin" ]
}

fuchsia_test_package("ufs-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":ufs-unit-tests" ]
}
