# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":gpt-device-test",
    ":gpt_bind_test",
  ]
}

driver_bind_rules("gpt_bind") {
  rules = "gpt.bind"
  bind_output = "gpt.bindbc"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.hardware.block" ]
}

fuchsia_driver("gpt-driver") {
  output_name = "gpt"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "gpt.cc" ]
  deps = [
    ":gpt_bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.gpt.metadata:fuchsia.hardware.gpt.metadata_cpp_wire",
    "//sdk/lib/fit",
    "//sdk/lib/syslog/cpp:backend",
    "//src/devices/bind/fuchsia.block:fuchsia.block_cpp",
    "//src/devices/bind/fuchsia.gpt:fuchsia.gpt_cpp",
    "//src/devices/block/lib/common",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/fidl/cpp",
    "//src/lib/storage/block_client/cpp",
    "//src/storage/gpt",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/third_party/ulib/cksum",
    "//zircon/third_party/ulib/safemath",
  ]
}

fuchsia_driver_component("gpt") {
  visibility = [ ":*" ]
  component_name = "gpt"
  deps = [ ":gpt-driver" ]
  info = "gpt-info.json"
  manifest = "meta/gpt.cml"
}

bootfs_files_for_assembly("bootfs") {
  deps = [ ":gpt" ]
}

test("gpt-device-test-bin") {
  output_name = "gpt-device-test"
  sources = [
    "gpt.cc",
    "gpt_device_test.cc",
  ]
  deps = [
    ":gpt_bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.gpt.metadata:fuchsia.hardware.gpt.metadata_cpp",
    "//sdk/lib/fit",
    "//src/devices/bind/fuchsia.block:fuchsia.block_cpp",
    "//src/devices/bind/fuchsia.gpt:fuchsia.gpt_cpp",
    "//src/devices/block/lib/common",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/storage/gpt",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
    "//zircon/third_party/ulib/cksum",
    "//zircon/third_party/ulib/safemath",
  ]
}

fuchsia_unittest_package("gpt-device-test") {
  deps = [ ":gpt-device-test-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
