# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("abr-shim-bind") {
  rules = "abr-shim.bind"
  tests = "abr-shim-bind-test.json"
  deps = [
    "//src/devices/bind/fuchsia.block",
    "//src/devices/bind/fuchsia.gpt",
    "//src/devices/bind/fuchsia.hardware.block",
  ]
}

fuchsia_driver("abr-shim-driver") {
  output_name = "abr-shim"
  sources = [ "abr-shim.cc" ]
  deps = [
    ":abr-shim-bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/firmware/lib/abr",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/storage/lib/paver:pinecrest-abr",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/cksum",
  ]
}

fuchsia_driver_component("abr-shim") {
  component_name = "abr-shim"
  deps = [ ":abr-shim-driver" ]
  info = "abr-shim-info.json"
  manifest = "meta/abr-shim.cml"
}

test("abr-shim-test-bin") {
  output_name = "abr-shim-test"
  sources = [
    "abr-shim-test.cc",
    "abr-shim.cc",
  ]
  deps = [
    ":abr-shim-bind",
    "//sdk/banjo/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.block.driver:fuchsia.hardware.block.driver_banjo_cpp",
    "//src/devices/lib/dev-operation",
    "//src/devices/lib/driver",
    "//src/devices/testing/mock-ddk",
    "//src/firmware/lib/abr",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/storage/lib/paver:pinecrest-abr",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
    "//zircon/third_party/ulib/cksum",
  ]
}

fuchsia_unittest_package("abr-shim-test") {
  deps = [ ":abr-shim-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [
    ":abr-shim-bind_test",
    ":abr-shim-test",
  ]
}
