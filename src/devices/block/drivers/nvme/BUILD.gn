# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("nvme_bind") {
  rules = "nvme.bind"
  header_output = "nvme_bind.h"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.pci.massstorage",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":nvme-tests",
    ":nvme_bind_test",
  ]
}

common_deps = [
  ":nvme_bind",
  "//sdk/banjo/fuchsia.hardware.block:fuchsia.hardware.block_banjo_cpp",
  "//src/devices/lib/driver",
  "//src/devices/lib/mmio",
  "//src/devices/pci/lib/device-protocol-pci",
  "//src/devices/pci/lib/fuchsia.hardware.pci:fuchsia.hardware.pci_banjo_cpp",
  "//src/devices/pci/lib/pci",
  "//src/lib/ddk",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/fzl",
  "//zircon/system/ulib/hwreg",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/sync",
  "//zircon/system/ulib/zircon-internal",
]

fuchsia_driver("nvme-driver") {
  output_name = "nvme"
  configs += [ "//build/config:all_source" ]
  sources = [
    "namespace.cc",
    "nvme.cc",
    "queue-pair.cc",
    "queue.cc",
  ]
  deps = common_deps
}

fuchsia_driver_component("nvme") {
  component_name = "nvme"
  deps = [ ":nvme-driver" ]
  info = "nvme-info.json"
  colocate = true
}

test("nvme-queue-tests-bin") {
  sources = [
    "queue-test.cc",
    "queue.cc",
  ]
  deps = [
    ":nvme_bind",
    "//src/devices/lib/driver",
    "//src/devices/testing/fake-bti",
    "//src/lib/ddk",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("nvme-queue-tests") {
  deps = [ ":nvme-queue-tests-bin" ]
}

test("nvme-queue-pair-tests-bin") {
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  sources = [
    "queue-pair-test.cc",
    "queue-pair.cc",
    "queue.cc",
  ]
  deps = [
    ":nvme_bind",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake-mmio-reg",
    "//src/lib/ddk",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("nvme-queue-pair-tests") {
  deps = [ ":nvme-queue-pair-tests-bin" ]
}

test("nvme-unit-tests-bin") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "fake/fake-admin-commands.cc",
    "fake/fake-controller.cc",
    "fake/fake-namespace.cc",
    "fake/fake-registers.cc",
    "namespace.cc",
    "nvme-test.cc",
    "nvme.cc",
    "queue-pair.cc",
    "queue.cc",
  ]
  deps = common_deps + [
           "//sdk/lib/inspect/testing/cpp:zxtest",
           "//src/devices/testing/fake-bti",
           "//src/devices/testing/mock-ddk",
           "//zircon/system/ulib/zxtest",
         ]
}

fuchsia_unittest_component("nvme-unit-tests") {
  deps = [ ":nvme-unit-tests-bin" ]
}

fuchsia_test_package("nvme-tests") {
  test_components = [
    ":nvme-queue-pair-tests",
    ":nvme-queue-tests",
    ":nvme-unit-tests",
  ]
}
