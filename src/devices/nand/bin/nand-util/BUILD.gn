# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/components.gni")
import("//build/test.gni")

executable("nand-util") {
  visibility = [
    ":*",
    "//build/images/zedboot:*",
  ]
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "aml.cc",
    "commands.cc",
    "ftl.cc",
    "main.cc",
  ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_cpp",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/cksum",
  ]
}

bootfs_files_for_assembly("bootfs") {
  deps = [ ":nand-util" ]
}

source_set("common") {
  visibility = [ ":*" ]
  sources = [
    "ftl_internal.cc",
    "nand-broker.cc",
  ]
  deps = [ "//zircon/system/ulib/pretty" ]
  public_deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.nand:fuchsia.hardware.nand_cpp",
    "//sdk/fidl/fuchsia.nand:fuchsia.nand_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fdio-caller",
    "//zircon/system/ulib/fzl",
  ]
}

test("nand-util-test-bin") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "nand-util-test"
  sources = [ "test/ftl_internal_test.cc" ]
  include_dirs = [ "." ]
  deps = [
    ":common",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("nand-util-test") {
  deps = [ ":nand-util-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":nand-util-test" ]
}
