# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/host_test_data.gni")

rustc_binary("bin") {
  output_name = "start_driver_test_realm_and_hang"

  edition = "2021"
  testonly = true

  sources = [ "src/main.rs" ]

  deps = [
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_package_with_single_component("start_driver_test_realm_and_hang") {
  testonly = true
  manifest = "meta/start_driver_test_realm_and_hang.cml"
  deps = [ ":bin" ]
}

if (is_host && current_cpu == "x64") {
  # This ensures that ffx is available to the host test.
  #
  # Based on similar code at src/starnix/tests/android/BUILD.gn;l=121-127
  host_test_data("test_tools") {
    sources = [ "$root_out_dir/ffx" ]
  }

  host_test("driver_dump_select_test") {
    binary_path = "driver_dump_select_test.sh"
    args = [ rebase_path("$root_out_dir/ffx", root_build_dir) ]
    deps = [
      ":start_driver_test_realm_and_hang",
      ":test_tools",
      "//src/developer/ffx:ffx_bin",
    ]
    environments = [
      {
        dimensions = {
          device_type = "QEMU"
        }
      },
    ]
  }

  host_test("driver_lspci_test") {
    binary_path = "$root_out_dir/ffx"
    args = [
      "driver",
      "lspci",
    ]
    deps = [
      ":test_tools",
      "//src/developer/ffx:ffx_bin",
    ]
    environments = [
      {
        dimensions = {
          device_type = "QEMU"
        }
      },
    ]
  }

  group("host_tests") {
    testonly = true
    deps = [
      ":driver_dump_select_test",
      ":driver_lspci_test",
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    # TODO(https://fxbug.dev/42070980): Enable this test when CQ issue is fixed.
    # ":host_tests($host_toolchain)",
    #
    # # Included because the host test needs this component to be available on the Fuchsia device.
    # ":start_driver_test_realm_and_hang",
  ]
}
