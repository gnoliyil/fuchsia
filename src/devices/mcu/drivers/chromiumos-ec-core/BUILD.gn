# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":chromiumos-ec-core-tests",
    ":chromiumos-ec-motion-tests",
    ":chromiumos-ec-power-sensor-tests",
    ":chromiumos-ec-usb-pd-tests",
  ]
}

driver_bind_rules("bind") {
  rules = "chromiumos_ec_core.bind"
  header_output = "chromiumos_ec_core_bind.h"
  bind_output = "chromiumos_ec_core.bindbc"
  deps = [
    "//sdk/fidl/fuchsia.hardware.google.ec:fuchsia.hardware.google.ec_bindlib",
  ]
}

source_set("core") {
  sources = [
    "chromiumos_ec_core.cc",
    "chromiumos_ec_core.h",
    "subdriver.cc",
    "subdriver.h",
  ]
  public_deps = [
    ":bind",
    "//sdk/fidl/fuchsia.hardware.google.ec:fuchsia.hardware.google.ec_cpp_wire",
    "//sdk/lib/fit-promise",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/svc",
    "//zircon/system/ulib/zx",
    "//zircon/third_party/ulib/chromiumos-platform-ec",
  ]
}

source_set("test-common") {
  sources = [
    "fake_device.cc",
    "fake_device.h",
  ]

  public_deps = [
    ":core",
    "//sdk/fidl/fuchsia.hardware.google.ec:fuchsia.hardware.google.ec_cpp_wire_testing",
    "//sdk/lib/async-loop-testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/devices/lib/acpi/mock:mock-acpi",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]

  testonly = true
}

source_set("motion") {
  sources = [
    "motion.cc",
    "motion.h",
    "subdriver.h",
  ]

  public_deps = [
    ":core",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//zircon/system/ulib/hid",
  ]
}

source_set("usb-pd") {
  sources = [
    "subdriver.h",
    "usb_pd.cc",
    "usb_pd.h",
  ]

  public_deps = [
    ":core",
    "//sdk/fidl/fuchsia.hardware.powersource:fuchsia.hardware.powersource_cpp_wire",
  ]
}

source_set("power-sensor") {
  sources = [
    "power_sensor.cc",
    "power_sensor.h",
    "subdriver.h",
  ]

  public_deps = [
    ":core",
    "//sdk/fidl/fuchsia.hardware.power.sensor:fuchsia.hardware.power.sensor_cpp_wire",
  ]
}

fuchsia_driver("driver") {
  output_name = "chromiumos-ec-core"
  deps = [
    ":core",
    ":motion",
    ":power-sensor",
    ":usb-pd",
  ]
}

fuchsia_driver_component("component") {
  info = "component-info.json"
  component_name = "chromiumos-ec-core"
  deps = [ ":driver" ]
  manifest = "meta/chromiumos-ec-core.cml"
}

fuchsia_driver_package("chromiumos-ec-core") {
  driver_components = [ ":component" ]
}

test("chromiumos-ec-core-unit-tests") {
  sources = [ "chromiumos_ec_core_tests.cc" ]
  deps = [ ":test-common" ]
}

test("chromiumos-ec-motion-unit-tests") {
  sources = [ "motion-test.cc" ]
  deps = [
    ":motion",
    ":test-common",
    "//zircon/system/ulib/hid-parser",
  ]
}

test("chromiumos-ec-usb-pd-unit-tests") {
  sources = [ "usb_pd_test.cc" ]
  deps = [
    ":test-common",
    ":usb-pd",
  ]
}

test("chromiumos-ec-power-sensor-unit-tests") {
  sources = [ "power_sensor_test.cc" ]
  deps = [
    ":power-sensor",
    ":test-common",
  ]
}

fuchsia_unittest_package("chromiumos-ec-core-tests") {
  deps = [ ":chromiumos-ec-core-unit-tests" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("chromiumos-ec-motion-tests") {
  deps = [ ":chromiumos-ec-motion-unit-tests" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("chromiumos-ec-usb-pd-tests") {
  deps = [ ":chromiumos-ec-usb-pd-unit-tests" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("chromiumos-ec-power-sensor-tests") {
  deps = [ ":chromiumos-ec-power-sensor-unit-tests" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
