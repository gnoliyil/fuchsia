# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#
import("//build/components.gni")

common_deps = [
  "//sdk/fidl/fuchsia.hardware.radar:fuchsia.hardware.radar_cpp_wire",
  "//sdk/lib/component/incoming/cpp",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/fbl",
]

executable("radarutil-bin") {
  output_name = "radarutil"
  configs += [ "//build/config:all_source" ]
  sources = [
    "main.cc",
    "radarutil.cc",
  ]
  deps = common_deps
}

fuchsia_component("radarutil-component") {
  manifest = "meta/radarutil.cml"
  deps = [ ":radarutil-bin" ]
}

executable("radarutil-test-bin") {
  testonly = true
  configs += [ "//build/config:all_source" ]
  sources = [
    "radarutil-test.cc",
    "radarutil.cc",
  ]
  deps = common_deps + [ "//zircon/system/ulib/zxtest" ]
}

fuchsia_unittest_package("radarutil-test") {
  deps = [ ":radarutil-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":radarutil-component",
    ":radarutil-test",
  ]
}
