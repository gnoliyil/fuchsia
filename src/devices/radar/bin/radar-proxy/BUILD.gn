# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/sys/core/build/core_shard.gni")

_common_deps = [
  ":config",
  "//sdk/fidl/fuchsia.hardware.radar:fuchsia.hardware.radar_cpp",
  "//sdk/lib/component/incoming/cpp",
  "//sdk/lib/component/outgoing/cpp",
  "//sdk/lib/fdio",
  "//sdk/lib/fit",
  "//sdk/lib/sys/cpp",
  "//sdk/lib/syslog/cpp",
  "//src/devices/radar/lib/vmo-manager",
  "//src/lib/fsl",
  "//zircon/system/ulib/async:async-cpp",
  "//zircon/system/ulib/async-loop:async-loop-cpp",
  "//zircon/system/ulib/async-loop:async-loop-default",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/fdio-caller",
  "//zircon/system/ulib/fzl",
  "//zircon/system/ulib/zx",
]

executable("radar-proxy-bin") {
  output_name = "radar_proxy"
  sources = [
    "burst-injector.cc",
    "main.cc",
    "radar-provider-proxy.cc",
    "radar-reader-proxy.cc",
    "reader-instance.cc",
  ]
  deps = _common_deps
}

fuchsia_component_manifest("manifest") {
  component_name = "radar-proxy"
  manifest = "meta/radar-proxy.cml"
}

fuchsia_structured_config_values("disable-burst-reader-proxying-config") {
  cm_label = ":manifest"
  values = {
    proxy_radar_burst_reader = false
  }
}

fuchsia_structured_config_values("enable-burst-reader-proxying-config") {
  cm_label = ":manifest"
  values = {
    proxy_radar_burst_reader = true
  }
}

fuchsia_structured_config_cpp_elf_lib("config") {
  cm_label = ":manifest"
}

fuchsia_component("radar-proxy-component") {
  component_name = "radar-proxy"
  cm_label = ":manifest"
  deps = [ ":radar-proxy-bin" ]
}

fuchsia_package("radar-proxy") {
  deps = [ ":radar-proxy-component" ]
}

core_shard("radar-proxy-shard") {
  shard_file = "meta/radar-proxy.core_shard.cml"
}

test("radar-proxy-test-bin") {
  sources = [
    "burst-injector.cc",
    "radar-provider-proxy.cc",
    "radar-proxy-test.cc",
    "radar-reader-proxy.cc",
    "reader-instance.cc",
  ]
  deps = [
           "//sdk/lib/driver/runtime/testing/runtime",
           "//src/devices/bin/driver_runtime",
           "//zircon/system/ulib/sync",
           "//zircon/system/ulib/zxtest",
         ] + _common_deps
}

fuchsia_unittest_package("radar-proxy-test") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [ ":radar-proxy-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [ ":radar-proxy-test" ]
}
