# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

source_set("pipe_io") {
  sources = [
    "pipe_auto_reader.cc",
    "pipe_auto_reader.h",
    "pipe_io.cc",
    "pipe_io.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.hardware.goldfish:fuchsia.hardware.goldfish_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.goldfish.pipe:fuchsia.hardware.goldfish.pipe_cpp_wire",
    "//src/devices/lib/goldfish/pipe_headers",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/fxl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

test("pipe_io_unittest") {
  output_name = "pipe_io_unittest"
  sources = [ "tests/pipe_io_test.cc" ]
  deps = [
    ":pipe_io",
    "//src/devices/testing/goldfish/fake_pipe",
    "//src/devices/testing/no_ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  configs += [ "//build/config:all_source" ]
}

fuchsia_unittest_package("pipe_io_test_package") {
  package_name = "goldfish_pipe_io_test"
  deps = [ ":pipe_io_unittest" ]
}

group("tests") {
  testonly = true
  deps = [ ":pipe_io_test_package" ]
}
