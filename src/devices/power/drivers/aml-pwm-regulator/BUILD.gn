# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("aml-pwm-regulator-bind") {
  rules = "aml-pwm-regulator.bind"
  tests = "bind-tests.json"
  disable_fragment_gen = true
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.pwm",
  ]
}

group("common") {
  public_deps = [
    ":aml-pwm-regulator-bind",
    "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.vreg:fuchsia.hardware.vreg_cpp_wire",
    "//sdk/lib/component/outgoing/cpp",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver("aml-pwm-regulator-driver") {
  output_name = "aml-pwm-regulator"
  sources = [ "aml-pwm-regulator.cc" ]
  deps = [
    ":common",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("aml-pwm-regulator") {
  info = "aml-pwm-regulator-info.json"
  component_name = "aml-pwm-regulator"
  deps = [ ":aml-pwm-regulator-driver" ]
  manifest = "meta/aml-pwm-regulator.cml"
}

test("aml-pwm-regulator-test-bin") {
  output_name = "aml-pwm-regulator-test"
  sources = [
    "aml-pwm-regulator-test.cc",
    "aml-pwm-regulator.cc",
  ]
  deps = [
    ":common",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/lib/metadata:llcpp",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_package("aml-pwm-regulator-test") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  deps = [ ":aml-pwm-regulator-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":aml-pwm-regulator-bind_test",
    ":aml-pwm-regulator-test",
  ]
}
