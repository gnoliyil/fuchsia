# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("vim3_usb_phy_bind") {
  rules = "vim3_usb_phy.bind"
  bind_output = "vim3-usb-phy.bindbc"
  tests = "test_bind.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.gpio",
    "//src/devices/bind/fuchsia.khadas.platform",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.register",
  ]
}

source_set("common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "vim3-usb-phy.cc" ]
  deps = [
    ":vim3_usb_phy_bind",
    "//sdk/fidl/fuchsia.driver.compat:fuchsia.driver.compat_cpp",
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_bindlib_cpp",
    "//sdk/fidl/fuchsia.hardware.usb.phy:fuchsia.hardware.usb.phy_cpp",
    "//sdk/lib/driver/compat/cpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/mmio",
    "//src/devices/usb/lib/usb",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("vim3-usb-phy-driver") {
  output_name = "vim3-usb-phy"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":common",
    "//src/devices/lib/driver:driver_runtime",
  ]
}

fuchsia_driver_component("vim3-usb-phy") {
  info = "vim3-usb-phy-info.json"
  component_name = "vim3-usb-phy"
  deps = [ ":vim3-usb-phy-driver" ]
  manifest = "meta/vim3-usb-phy.cml"
}

fuchsia_driver_package("package") {
  package_name = "vim3-usb-phy"
  driver_components = [ ":vim3-usb-phy" ]
}

test("vim3-usb-phy-test-bin") {
  configs += [ "//build/config:all_source" ]
  sources = [ "vim3-usb-phy-test.cc" ]
  output_name = "vim3-usb-phy-test"
  deps = [
    ":common",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/registers/testing/mock-registers",
    "//src/devices/testing/fake-mmio-reg",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("vim3-usb-phy-test") {
  deps = [ ":vim3-usb-phy-test-bin" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":vim3_usb_phy_bind_test" ]
}
