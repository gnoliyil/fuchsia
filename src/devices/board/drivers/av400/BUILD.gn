# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

declare_args() {
  av400_has_codec = true
  av400_has_loopback = false

  # Audio data can be processed using HW DSP FW.
  av400_tdm_use_dsp = false
  av400_pdm_use_dsp = false
}

driver_bind_rules("av400-emmc-bind") {
  rules = "bind/av400-emmc.bind"
  header_output = "av400-emmc-bind.h"
  bind_output = "av400-emmc-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("av400-sdio-bind") {
  rules = "bind/av400-sdio.bind"
  header_output = "av400-sdio-bind.h"
  bind_output = "av400-sdio-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("pwm_init_bind") {
  rules = "bind/pwm_init.bind"
  header_output = "pwm_init_bind.h"
  bind_output = "pwm_init_bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.hardware.pwm",
  ]
}

driver_bind_rules("av400-bind") {
  rules = "bind/av400.bind"
  header_output = "av400-bind.h"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.platform.bus",
  ]
}

driver_bind_rules("eth-bind") {
  rules = "bind/eth.bind"
  header_output = "eth-bind.h"
  bind_output = "eth-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.hardware.gpio",
  ]
}

driver_bind_rules("dwmac-bind") {
  rules = "bind/dwmac.bind"
  header_output = "dwmac-bind.h"
  bind_output = "dwmac-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.designware.platform",
    "//src/devices/bind/fuchsia.hardware.gpio",
  ]
}

driver_bind_rules("usb-phy-bind") {
  rules = "bind/usb_phy.bind"
  header_output = "usb-phy-bind.h"
  bind_output = "usb-phy-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.registers",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("xhci-bind") {
  rules = "bind/xhci.bind"
  header_output = "xhci-bind.h"
  bind_output = "xhci-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.usb.phy",
  ]
}

driver_bind_rules("udc-phy-bind") {
  rules = "bind/udc_phy.bind"
  header_output = "udc-phy-bind.h"
  bind_output = "udc-phy-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.usb.phy",
  ]
}

driver_bind_rules("spi_1_bind") {
  rules = "bind/spi_1.bind"
  header_output = "spi_1_bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("tdm-i2s-test-codec-bind") {
  rules = "bind/tdm-i2s-test-codec.bind"
  header_output = "tdm-i2s-test-codec-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.codec",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.ti.platform",
  ]
}

driver_bind_rules("tdm-i2s-out-dsp-bind") {
  rules = "bind/tdm-i2s-out-dsp.bind"
  header_output = "tdm-i2s-out-dsp-bind.h"
  bind_output = "tdm-i2s-out-dsp-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.codec",
    "//src/devices/bind/fuchsia.dsp",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.mailbox",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.ti.platform",
  ]
}

driver_bind_rules("tdm-i2s-bind") {
  rules = "bind/tdm-i2s.bind"
  header_output = "tdm-i2s-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.hardware.gpio",
  ]
}

driver_bind_rules("tdm-i2s-in-bind") {
  rules = "bind/tdm-i2s-in.bind"
  header_output = "tdm-i2s-in-bind.h"
  deps = [ "//src/devices/bind/fuchsia.hardware.gpio" ]
}

driver_bind_rules("pdm-bind") {
  rules = "bind/pdm.bind"
  header_output = "pdm-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("pdm-i2s-dsp-bind") {
  rules = "bind/pdm-i2s-dsp.bind"
  header_output = "pdm-i2s-dsp-bind.h"
  bind_output = "pdm-i2s-dsp-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.dsp",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.mailbox",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("tdm-i2s-in-dsp-bind") {
  rules = "bind/tdm-i2s-in-dsp.bind"
  header_output = "tdm-i2s-in-dsp-bind.h"
  bind_output = "tdm-i2s-in-dsp-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.dsp",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.mailbox",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("audio-tas5707-stereo-bind") {
  rules = "bind/audio-tas5707-stereo.bind"
  header_output = "audio-tas5707-stereo-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.i2c",
  ]
}

driver_bind_rules("av400-tee-bind") {
  rules = "bind/av400-tee.bind"
  header_output = "av400-tee-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.rpmb",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("av400-power-regulator-bind") {
  rules = "bind/av400-power-regulator.bind"
  header_output = "av400-power-regulator-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.hardware.pwm",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("av400-power-domain-bind") {
  rules = "bind/av400-power-domain.bind"
  header_output = "av400-power-domain-bind.h"
  deps = [ "//src/devices/bind/fuchsia.power" ]
}

driver_bind_rules("av400-cpu-bind") {
  rules = "bind/av400-cpu.bind"
  header_output = "av400-cpu-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.hardware.power",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("av400-buttons-bind") {
  rules = "bind/av400-buttons.bind"
  header_output = "av400-buttons-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.hardware.gpio",
  ]
}

driver_bind_rules("av400-dsp-bind") {
  rules = "bind/av400-dsp.bind"
  header_output = "av400-dsp-bind.h"
  bind_output = "av400-dsp-bind.bc"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.a5",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.mailbox",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("av400-i2c-bind") {
  rules = "bind/av400-i2c.bind"
  header_output = "av400-i2c-bind.h"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

fuchsia_driver("av400-driver") {
  output_name = "av400"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "av400-audio.cc",
    "av400-buttons.cc",
    "av400-clk.cc",
    "av400-cpu.cc",
    "av400-dsp.cc",
    "av400-emmc.cc",
    "av400-eth.cc",
    "av400-gpio.cc",
    "av400-i2c.cc",
    "av400-mailbox.cc",
    "av400-nna.cc",
    "av400-power.cc",
    "av400-pwm.cc",
    "av400-ram.cc",
    "av400-registers.cc",
    "av400-rtc.cc",
    "av400-sdio.cc",
    "av400-spi.cc",
    "av400-sysmem.cc",
    "av400-tee.cc",
    "av400-thermal.cc",
    "av400-usb.cc",
    "av400.cc",
  ]
  deps = [
    ":audio-tas5707-stereo-bind_header",
    ":av400-bind",
    ":av400-buttons-bind_header",
    ":av400-cpu-bind_header",
    ":av400-dsp-bind_header",
    ":av400-emmc-bind_header",
    ":av400-i2c-bind_header",
    ":av400-power-domain-bind_header",
    ":av400-power-regulator-bind_header",
    ":av400-sdio-bind_header",
    ":av400-tee-bind_header",
    ":dwmac-bind_header",
    ":eth-bind_header",
    ":pdm-bind_header",
    ":pwm_init_bind_header",
    ":spi_1_bind_header",
    ":tdm-i2s-bind_header",
    ":tdm-i2s-in-bind_header",
    ":udc-phy-bind_header",
    ":usb-phy-bind_header",
    ":xhci-bind_header",
    "//sdk/banjo/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.iommu:fuchsia.hardware.iommu_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.serial:fuchsia.hardware.serial_banjo_cpp",
    "//sdk/banjo/fuchsia.sysmem:fuchsia.sysmem_banjo_c",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.gpio.init:fuchsia.hardware.gpio.init_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_cpp_wire",
    "//sdk/lib/driver/component/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.amlogic.platform:fuchsia.amlogic.platform_cpp",
    "//src/devices/bind/fuchsia.hardware.registers:fuchsia.hardware.registers_cpp",
    "//src/devices/bus/lib/platform-bus-composites",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/lib/fidl-metadata",
    "//src/devices/lib/metadata:llcpp",
    "//src/devices/lib/mmio",
    "//src/devices/lib/ti",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:cdc",
    "//src/devices/usb/lib/usb:peripheral",
    "//src/devices/usb/lib/usb:peripheral-config",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
  ]

  defines = []
  if (av400_has_codec) {
    defines += [ "TEST_CODEC" ]
    deps -= [ ":tdm-i2s-bind_header" ]
    if (av400_tdm_use_dsp) {
      defines += [ "TDM_USE_DSP" ]
      deps -= [ ":tdm-i2s-in-bind_header" ]
      deps += [ ":tdm-i2s-in-dsp-bind_header" ]
      deps += [ ":tdm-i2s-out-dsp-bind_header" ]
    } else {
      deps += [ ":tdm-i2s-test-codec-bind_header" ]
    }
  }
  if (av400_has_loopback) {
    defines += [ "TEST_LOOPBACK" ]
  }
  if (av400_pdm_use_dsp) {
    defines += [ "PDM_USE_DSP" ]
    deps -= [ ":pdm-bind_header" ]
    deps += [ ":pdm-i2s-dsp-bind_header" ]
  }
}

fuchsia_driver_component("av400") {
  component_name = "av400"
  deps = [ ":av400-driver" ]
  info = "av400-info.json"
  manifest = "meta/av400.cml"
}

group("tests") {
  testonly = true
  deps = [ ":av400-bind_test" ]
}
