# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

config("factory") {
  defines = [ "FACTORY_BUILD" ]
}

driver_bind_rules("sherlock-bind") {
  rules = "sherlock.bind"
  header_output = "sherlock-bind.h"
  bind_output = "sherlock.bindbc"
  tests = "sherlock-bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.google.platform",
    "//src/devices/bind/fuchsia.hardware.platform.bus",
  ]
}

driver_bind_rules("sherlock-buttons-bind") {
  rules = "bind/sherlock-buttons.bind"
  header_output = "sherlock-buttons-bind.h"
  bind_output = "sherlock-buttons-bind.bc"
  tests = "bind-tests/sherlock-buttons-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.gpio",
  ]
}

driver_bind_rules("sherlock-pwm-bind") {
  rules = "bind/sherlock-pwm.bind"
  header_output = "sherlock-pwm-bind.h"
  bind_output = "sherlock-pwm-bind.bc"
  tests = "bind-tests/sherlock-pwm-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.hardware.pwm",
  ]
}

driver_bind_rules("sherlock-spi-bind") {
  rules = "bind/sherlock-spi.bind"
  header_output = "sherlock-spi-bind.h"
  bind_output = "sherlock-spi-bind.bc"
  tests = "bind-tests/sherlock-spi-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.hardware.registers",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-tee-bind") {
  rules = "bind/sherlock-tee.bind"
  header_output = "sherlock-tee-bind.h"
  bind_output = "sherlock-tee-bind.bc"
  tests = "bind-tests/sherlock-tee-test.json"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.rpmb",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-video-bind") {
  rules = "bind/sherlock-video.bind"
  header_output = "sherlock-video-bind.h"
  bind_output = "sherlock-video-bind.bc"
  tests = "bind-tests/sherlock-video-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.hardware.amlogiccanvas",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.hardware.tee",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-video-enc-bind") {
  rules = "bind/sherlock-video-enc.bind"
  header_output = "sherlock-video-enc-bind.h"
  bind_output = "sherlock-video-enc-bind.bc"
  tests = "bind-tests/sherlock-video-enc-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.hardware.amlogiccanvas",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-hevc-enc-bind") {
  rules = "bind/sherlock-hevc-enc.bind"
  header_output = "sherlock-hevc-enc-bind.h"
  bind_output = "sherlock-hevc-enc-bind.bc"
  tests = "bind-tests/sherlock-hevc-enc-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-emmc-bind") {
  rules = "bind/sherlock-emmc.bind"
  header_output = "sherlock-emmc-bind.h"
  bind_output = "sherlock-emmc-bind.bc"
  tests = "bind-tests/sherlock-emmc-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-securemem-bind") {
  rules = "bind/sherlock-securemem.bind"
  header_output = "sherlock-securemem-bind.h"
  bind_output = "sherlock-securemem-bind.bc"
  tests = "bind-tests/sherlock-securemem-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.tee",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

driver_bind_rules("sherlock-aml-usb-phy-v2-bind") {
  rules = "bind/sherlock-aml-usb-phy-v2.bind"
  header_output = "sherlock-aml-usb-phy-v2-bind.h"
  bind_output = "sherlock-aml-usb-phy-v2-bind.bc"
  tests = "bind-tests/sherlock-aml-usb-phy-v2-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.registers",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-dwc2-phy-bind") {
  rules = "bind/sherlock-dwc2-phy.bind"
  header_output = "sherlock-dwc2-phy-bind.h"
  bind_output = "sherlock-dwc2-phy-bind.bc"
  tests = "bind-tests/sherlock-dwc2-phy-test.json"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.usb.phy",
  ]
}

driver_bind_rules("sherlock-xhci-bind") {
  rules = "bind/sherlock-xhci.bind"
  header_output = "sherlock-xhci-bind.h"
  bind_output = "sherlock-xhci-bind.bc"
  tests = "bind-tests/sherlock-xhci-test.json"
  deps = [
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.usb.phy",
  ]
}

driver_bind_rules("sherlock-ot-radio-bind") {
  rules = "bind/sherlock-ot-radio.bind"
  header_output = "sherlock-ot-radio-bind.h"
  bind_output = "sherlock-ot-radio-bind.bc"
  tests = "bind-tests/sherlock-ot-radio-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.nordic.platform",
    "//src/devices/bind/fuchsia.spi",
  ]
}

driver_bind_rules("sherlock-thermal-bind") {
  rules = "bind/sherlock-thermal.bind"
  header_output = "sherlock-thermal-bind.h"
  bind_output = "sherlock-thermal-bind.bc"
  tests = "bind-tests/sherlock-thermal-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.meson",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.hardware.pwm",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-sdio-bind") {
  rules = "bind/sherlock-sdio.bind"
  header_output = "sherlock-sdio-bind.h"
  bind_output = "sherlock-sdio-bind.bc"
  tests = "bind-tests/sherlock-sdio-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.hardware.pwm",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-wifi-bind") {
  rules = "bind/sherlock-wifi.bind"
  header_output = "sherlock-wifi-bind.h"
  bind_output = "sherlock-wifi-bind.bc"
  tests = "bind-tests/sherlock-wifi-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.t931",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.sdio",
  ]
}

driver_bind_rules("camera-isp-bind") {
  rules = "bind/camera-isp.bind"
  header_output = "camera-isp-bind.h"
  bind_output = "camera-isp-bind.bc"
  tests = "bind-tests/camera-isp-test.json"
  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.arm.platform",
    "//src/devices/bind/fuchsia.camerasensor2",
    "//src/devices/bind/fuchsia.hardware.registers",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("sherlock-woofer-bind") {
  rules = "bind/sherlock-woofer.bind"
  header_output = "sherlock-woofer-bind.h"
  bind_output = "sherlock-woofer-bind.bc"
  tests = "bind-tests/sherlock-woofer-test.json"
  deps = [ "//src/devices/bind/fuchsia.i2c" ]
}

driver_bind_rules("sherlock-tweeter-left-bind") {
  rules = "bind/sherlock-tweeter-left.bind"
  header_output = "sherlock-tweeter-left-bind.h"
  bind_output = "sherlock-tweeter-left-bind.bc"
  tests = "bind-tests/sherlock-tweeter-left-test.json"
  deps = [ "//src/devices/bind/fuchsia.i2c" ]
}

driver_bind_rules("sherlock-tweeter-right-bind") {
  rules = "bind/sherlock-tweeter-right.bind"
  header_output = "sherlock-tweeter-right-bind.h"
  bind_output = "sherlock-tweeter-right-bind.bc"
  tests = "bind-tests/sherlock-tweeter-right-test.json"
  deps = [ "//src/devices/bind/fuchsia.i2c" ]
}

template("sherlock_driver") {
  fuchsia_driver(target_name + "-driver") {
    output_name = invoker.target_name
    forward_variables_from(invoker, "*")

    configs += [
      "//build/config:all_source",
      "//build/config/fuchsia:enable_zircon_asserts",
    ]
    if (use_factory) {
      configs += [ ":factory" ]
    }

    sources = [
      "sherlock-audio.cc",
      "sherlock-backlight.cc",
      "sherlock-bluetooth.cc",
      "sherlock-board.cc",
      "sherlock-buttons.cc",
      "sherlock-camera.cc",
      "sherlock-canvas.cc",
      "sherlock-clk.cc",
      "sherlock-cpu.cc",
      "sherlock-display.cc",
      "sherlock-dsi.cc",
      "sherlock-emmc.cc",
      "sherlock-gpio.cc",
      "sherlock-hevc-enc.cc",
      "sherlock-i2c.cc",
      "sherlock-light.cc",
      "sherlock-mali.cc",
      "sherlock-nna.cc",
      "sherlock-ot-radio.cc",
      "sherlock-pwm.cc",
      "sherlock-ram.cc",
      "sherlock-registers.cc",
      "sherlock-sdio.cc",
      "sherlock-securemem.cc",
      "sherlock-spi.cc",
      "sherlock-sysmem.cc",
      "sherlock-tee.cc",
      "sherlock-thermal.cc",
      "sherlock-thermistor.cc",
      "sherlock-touch.cc",
      "sherlock-usb.cc",
      "sherlock-video-enc.cc",
      "sherlock-video.cc",
      "sherlock.cc",
    ]

    deps = [
      ":camera-isp-bind_header",
      ":sherlock-aml-usb-phy-v2-bind_header",
      ":sherlock-buttons-bind_header",
      ":sherlock-dwc2-phy-bind_header",
      ":sherlock-emmc-bind_header",
      ":sherlock-hevc-enc-bind_header",
      ":sherlock-ot-radio-bind_header",
      ":sherlock-pwm-bind_header",
      ":sherlock-sdio-bind_header",
      ":sherlock-securemem-bind_header",
      ":sherlock-spi-bind_header",
      ":sherlock-tee-bind_header",
      ":sherlock-thermal-bind_header",
      ":sherlock-tweeter-left-bind_header",
      ":sherlock-tweeter-right-bind_header",
      ":sherlock-video-bind_header",
      ":sherlock-video-enc-bind_header",
      ":sherlock-wifi-bind_header",
      ":sherlock-woofer-bind_header",
      ":sherlock-xhci-bind_header",
      "//sdk/banjo/fuchsia.hardware.clockimpl:fuchsia.hardware.clockimpl_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.iommu:fuchsia.hardware.iommu_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_banjo_cpp",
      "//sdk/banjo/fuchsia.hardware.serial:fuchsia.hardware.serial_banjo_cpp",
      "//sdk/banjo/fuchsia.sysmem:fuchsia.sysmem_banjo_c",
      "//sdk/fidl/fuchsia.hardware.amlogiccanvas:fuchsia.hardware.amlogiccanvas_bindlib_cpp",
      "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp_wire",
      "//sdk/fidl/fuchsia.hardware.gpt.metadata:fuchsia.hardware.gpt.metadata_cpp_wire",
      "//sdk/fidl/fuchsia.hardware.gpu.amlogic:fuchsia.hardware.gpu.amlogic_cpp_wire",
      "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
      "//sdk/fidl/fuchsia.hardware.sdmmc:fuchsia.hardware.sdmmc_cpp_wire",
      "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_cpp_wire",
      "//sdk/fidl/fuchsia.hardware.thermal:fuchsia.hardware.thermal_cpp_wire",
      "//sdk/lib/driver/component/cpp:cpp",
      "//src/connectivity/openthread/lib/ot-radio",
      "//src/devices/bind/fuchsia:fuchsia_cpp",
      "//src/devices/bind/fuchsia.amlogic.platform:fuchsia.amlogic.platform_cpp",
      "//src/devices/bind/fuchsia.amlogic.platform.meson:fuchsia.amlogic.platform.meson_cpp",
      "//src/devices/bind/fuchsia.amlogic.platform.t931:fuchsia.amlogic.platform.t931_cpp",
      "//src/devices/bind/fuchsia.ams.platform:fuchsia.ams.platform_cpp",
      "//src/devices/bind/fuchsia.arm.platform:fuchsia.arm.platform_cpp",
      "//src/devices/bind/fuchsia.camerasensor2:fuchsia.camerasensor2_cpp",
      "//src/devices/bind/fuchsia.codec:fuchsia.codec_cpp",
      "//src/devices/bind/fuchsia.gdc:fuchsia.gdc_cpp",
      "//src/devices/bind/fuchsia.ge2d:fuchsia.ge2d_cpp",
      "//src/devices/bind/fuchsia.google.platform:fuchsia.google.platform_cpp",
      "//src/devices/bind/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp",
      "//src/devices/bind/fuchsia.hardware.dsi:fuchsia.hardware.dsi_cpp",
      "//src/devices/bind/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp",
      "//src/devices/bind/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp",
      "//src/devices/bind/fuchsia.hardware.registers:fuchsia.hardware.registers_cpp",
      "//src/devices/bind/fuchsia.i2c:fuchsia.i2c_cpp",
      "//src/devices/bind/fuchsia.isp:fuchsia.isp_cpp",
      "//src/devices/bind/fuchsia.mipicsi:fuchsia.mipicsi_cpp",
      "//src/devices/bind/fuchsia.platform:fuchsia.platform_cpp",
      "//src/devices/bind/fuchsia.sony.platform:fuchsia.sony.platform_cpp",
      "//src/devices/bind/fuchsia.sysmem:fuchsia.sysmem_cpp",
      "//src/devices/bind/fuchsia.thermal:fuchsia.thermal_cpp",
      "//src/devices/bind/fuchsia.ti.platform:fuchsia.ti.platform_cpp",
      "//src/devices/bus/lib/platform-bus-composites",
      "//src/devices/lib/amlogic",
      "//src/devices/lib/broadcom",
      "//src/devices/lib/driver",
      "//src/devices/lib/driver:driver_runtime",
      "//src/devices/lib/fidl-metadata",
      "//src/devices/lib/focaltech",
      "//src/devices/lib/metadata:llcpp",
      "//src/devices/lib/mmio",
      "//src/devices/lib/thermal",
      "//src/devices/lib/ti",
      "//src/devices/usb/lib/usb",
      "//src/devices/usb/lib/usb:cdc",
      "//src/devices/usb/lib/usb:peripheral",
      "//src/devices/usb/lib/usb:peripheral-config",
      "//src/graphics/display/lib/device-protocol-display",
      "//src/lib/ddk",
      "//src/lib/ddk:ddk-metadata-headers",

      # TODO(fxb/38132): Migrate to the new bind rules and delete the below
      ":sherlock-bind",
      ":sherlock-bind_header",
      "//src/lib/ddk:ddk-deprecated-binding-headers",
      "//src/lib/ddktl",
      "//zircon/system/ulib/fbl",
      "//zircon/system/ulib/hwreg",
      "//zircon/system/ulib/inspect",
      "//zircon/system/ulib/zircon-internal",
      "//zircon/system/ulib/zx",
    ]

    # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
    # rather than builders.
    configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
  }

  fuchsia_driver_component(target_name) {
    deps = [ ":${target_name}-driver" ]
    info = "${target_name}-info.json"
    manifest = "meta/${target_name}.cml"
  }
}

sherlock_driver("sherlock") {
  use_factory = false
}

sherlock_driver("sherlock-factory") {
  use_factory = true
}

group("tests") {
  testonly = true
  deps = [
    ":camera-isp-bind_test",
    ":sherlock-aml-usb-phy-v2-bind_test",
    ":sherlock-bind_test",
    ":sherlock-dwc2-phy-bind_test",
  ]
}
