# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/drivers.gni")
import("//build/test.gni")
import("//build/zbi/zbi_input.gni")

group("tests") {
  testonly = true
  deps = [
    ":vim3-devicetree-bind_test",
    ":vim3-devicetree-test-package",
  ]
}

devicetree_fragment("amlogic-a311d") {
  sources = [ "dts/amlogic-a311d.dtsi" ]
}

devicetree("vim3") {
  sources = [ "dts/khadas-vim3.dts" ]
  deps = [ ":amlogic-a311d" ]
  golden = "dts/khadas-vim3.golden.dts"
}

zbi_input("vim3-dtb-item") {
  deps = [ ":vim3" ]
  type = "devicetree"

  dtb_file = get_target_outputs(":vim3_dtb")
  sources = [ dtb_file[0] ]
}

driver_bind_rules("vim3-devicetree-bind") {
  rules = "vim3-devicetree.bind"
  header_output = "vim3-devicetree-bind.h"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.hardware.platform.bus",
    "//src/devices/bind/fuchsia.khadas.platform",
  ]
}

fuchsia_driver("vim3-devicetree-driver") {
  output_name = "vim3-devicetree"
  sources = [ "vim3-devicetree.cc" ]
  deps = [
    ":vim3-devicetree-bind",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_cpp",
    "//sdk/fidl/fuchsia.hardware.platform.bus:fuchsia.hardware.platform.bus_cpp_driver",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/bin/driver_runtime",
  ]
}

fuchsia_driver_component("vim3-devicetree") {
  deps = [
    ":vim3",
    ":vim3-devicetree-driver",
  ]
  info = "vim3-devicetree-info.json"
  manifest = "meta/vim3-devicetree.cml"
}

test("vim3-devicetree-test") {
  sources = [ "vim3-devicetree-test.cc" ]
  deps = [
    "//sdk/lib/driver/devicetree/testing",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/ddk-platform-defs",
    "//zircon/system/ulib/zxtest",
  ]
}

resource("test-dtb") {
  sources = get_target_outputs(":vim3_dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":vim3" ]
}

fuchsia_unittest_package("vim3-devicetree-test-package") {
  package_name = "vim3-devicetree-test"
  deps = [
    ":test-dtb",
    ":vim3-devicetree",
    ":vim3-devicetree-test",
  ]
}
