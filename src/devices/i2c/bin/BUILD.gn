# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/components.gni")
import("//build/test.gni")

executable("i2cutil2") {
  visibility = [ ":*" ]
  sources = [
    "args.cc",
    "i2cutil2.cc",
    "main.cc",
  ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fdio",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

executable("i2cutil") {
  visibility = [ ":*" ]
  sources = [ "i2cutil.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/lib/component/incoming/cpp",
    "//zircon/system/ulib/zx",
  ]
}

bootfs_files_for_assembly("bootfs") {
  deps = [
    ":i2cutil",
    ":i2cutil2",
  ]
}

test("i2cutil-test-bin") {
  output_name = "i2cutil-test"
  sources = [
    "args.cc",
    "i2cutil2.cc",
    "test/i2cutil-test.cc",
  ]
  include_dirs = [ "." ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/lib/fdio",
    "//src/devices/i2c/testing/fake-i2c",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("i2cutil-test") {
  deps = [ ":i2cutil-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":i2cutil-test" ]
}
