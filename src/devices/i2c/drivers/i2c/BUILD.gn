# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("i2c_bind") {
  rules = "i2c.bind"
  bind_output = "i2c.bindbc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.i2c" ]
}

fuchsia_driver("i2c-driver") {
  output_name = "i2c"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "i2c-child.cc",
    "i2c.cc",
  ]
  deps = [
    ":i2c_bind",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.i2c.businfo:fuchsia.hardware.i2c.businfo_cpp_wire",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_wire",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver_component("i2c") {
  component_name = "i2c"
  deps = [ ":i2c-driver" ]
  info = "i2c-info.json"
  manifest = "meta/i2c.cml"
}

test("i2c-test-bin") {
  output_name = "i2c-test"
  configs += [ "//build/config:all_source" ]
  sources = [
    "i2c-child-test.cc",
    "i2c-child.cc",
    "i2c-test.cc",
    "i2c.cc",
  ]
  deps = [
    ":i2c_bind",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.i2c.businfo:fuchsia.hardware.i2c.businfo_cpp_wire",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_wire",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//sdk/lib/driver/runtime/testing/runtime",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("i2c-test") {
  deps = [ ":i2c-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

group("tests") {
  testonly = true
  deps = [ ":i2c-test" ]
}
