# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("intel_i2c_bind") {
  rules = "intel_i2c.bind"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.intel.pci",
    "//src/devices/bind/fuchsia.pci",
  ]
}

driver_bind_rules("intel_i2c_bind_dfv2") {
  rules = "intel_i2c_dfv2.bind"
  bind_output = "intel_i2c_dfv2.bindbc"
  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.driver.framework",
    "//src/devices/bind/fuchsia.intel.pci",
    "//src/devices/bind/fuchsia.pci",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":intel_i2c_bind_test" ]
}

fuchsia_driver("intel-i2c-driver") {
  output_name = "intel-i2c"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "intel-i2c-controller.cc",
    "intel-i2c-subordinate.cc",
  ]
  deps = [
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.acpi:fuchsia.hardware.acpi_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.i2c.businfo:fuchsia.hardware.i2c.businfo_cpp_wire",
    "//sdk/lib/fit",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/pci/lib/pci",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
  ]

  # TODO(fxbug.dev/58161): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-unused-function" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("intel-i2c") {
  component_name = "intel-i2c"
  deps = [
    ":intel-i2c-driver",
    ":intel_i2c_bind",
  ]
  info = "intel-i2c-info.json"
  manifest = "meta/intel-i2c.cml"
}

fuchsia_driver_component("intel-i2c-dfv2") {
  deps = [
    ":intel-i2c-driver",
    ":intel_i2c_bind_dfv2",
  ]
  info = "intel-i2c-info.json"
  manifest = "meta/intel-i2c-dfv2.cml"
}

group("drivers") {
  deps = [
    ":intel-i2c",
    ":intel-i2c-dfv2",
  ]
}
