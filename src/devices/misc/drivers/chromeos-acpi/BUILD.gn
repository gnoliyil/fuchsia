# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

group("chromeos-acpi") {
  deps = [ ":pkg" ]
}

group("tests") {
  testonly = true
  deps = [
    ":chromeos-acpi-tests",
    ":chromeos_acpi_bind_test",
  ]
}

driver_bind_rules("chromeos_acpi_bind") {
  rules = "chromeos_acpi.bind"
  bind_output = "chromeos_acpi.bindbc"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.driver.framework",
  ]
}

common_deps = [
  ":chromeos_acpi_bind",
  "//sdk/fidl/fuchsia.acpi.chromeos:fuchsia.acpi.chromeos_cpp_wire",
  "//src/devices/lib/acpi",
  "//src/devices/lib/driver",
  "//src/lib/ddktl",
  "//zircon/system/ulib/fbl",
  "//zircon/system/ulib/inspect",
  "//zircon/system/ulib/zx",
]

fuchsia_driver("driver") {
  output_name = "chromeos-acpi"
  sources = [ "chromeos_acpi.cc" ]
  deps = common_deps
}

fuchsia_driver_component("component") {
  component_name = "chromeos-acpi"
  deps = [ ":driver" ]
  info = "component-info.json"
  manifest = "meta/chromeos-acpi.cml"
}

fuchsia_driver_package("pkg") {
  package_name = "chromeos-acpi"
  driver_components = [ ":component" ]
}

test("chromeos-acpi-tests-bin") {
  output_name = "chromeos-acpi-tests"
  sources = [
    "chromeos_acpi.cc",
    "unit-tests.cc",
  ]
  deps = common_deps + [
           "//sdk/lib/inspect/testing/cpp:zxtest",
           "//src/devices/lib/acpi/mock:mock-acpi",
           "//src/devices/testing/mock-ddk",
           "//zircon/system/ulib/async-loop:async-loop-cpp",
           "//zircon/system/ulib/zxtest",
         ]
}

fuchsia_unittest_package("chromeos-acpi-tests") {
  deps = [ ":chromeos-acpi-tests-bin" ]
}
