# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

rustc_test("bin") {
  name = "audio_integration_test"
  edition = "2021"
  source_root = "src/audio_tests.rs"
  sources = [
    "src/audio_tests.rs",
    "src/common.rs",
    "src/mock_audio_core_service.rs",
  ]
  deps = [
    "//sdk/fidl/fuchsia.media:fuchsia.media_rust",
    "//sdk/fidl/fuchsia.media.audio:fuchsia.media.audio_rust",
    "//sdk/fidl/fuchsia.settings:fuchsia.settings_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/settings/service/integration_tests/utils",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:test-case",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("audio_integration_test") {
  testonly = true
  manifest = "meta/audio_integration_test.cml"
  deps = [ ":bin" ]
}

fuchsia_test_package("audio_integration_test_pkg") {
  package_name = "audio_integration_test"
  test_components = [ ":audio_integration_test" ]
  deps = [
    ":audio_integration_test",
    ":tests_config",
    "//src/settings/service:setui_service_v2",
    "//src/sys/stash:pkg",
  ]
  subpackages = [ "//src/sys/stash:pkg" ]

  # invalid_missing_input_tests produces error logs due to purposefully passing
  # invalid input to the settings service.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

config_data("tests_config") {
  for_pkg = "audio_integration_test_pkg"
  sources = [ "testdata/interface_configuration.json" ]
}

group("audio_tests") {
  testonly = true
  public_deps = [ ":audio_integration_test_pkg" ]
}
