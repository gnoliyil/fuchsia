# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test_package.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_with_expectations_package.gni")

# Define a test package for a linux test.
#
# Arguments:
#
#   galaxy [mandatory]
#   The target defining the galaxy package containing the starnix kernel and the system image.
#
#   Any fuchsia_test_package arguments are supported.
template("starnix_test_package") {
  fuchsia_test_package(target_name) {
    galaxy = invoker.galaxy
    forward_variables_from(invoker, "*")

    if (target_cpu == "x64") {
      renameable_subpackages = [
        {
          name = "starnix_kernel"
          package = galaxy
        },
      ]
    }

    # This package intentionally contains many non-Fuchia ELF binaries that
    # are not under /data/, and some of them are even unstripped, so disable
    # theses checks to build successfully.
    disable_elf_binaries_checks = true
  }
}

# Define a test package with expectations for a linux test.
#
# Arguments:
#
#   galaxy [mandatory]
#   The target defining the galaxy package containing the starnix kernel and the system image.
#
#   Any fuchsia_test_package arguments are supported.
template("starnix_test_package_with_expectations") {
  fuchsia_test_with_expectations_package(target_name) {
    galaxy = invoker.galaxy
    forward_variables_from(invoker, "*")

    if (target_cpu == "x64") {
      renameable_subpackages = [
        {
          name = "starnix_kernel"
          package = galaxy
        },
      ]
    }
  }
}

# Define a test package for a linux test running in a starbionic galaxy.
#
# The parameters are the same as fuchsia_test_package
template("starbionic_test_package") {
  starnix_test_package(target_name) {
    forward_variables_from(invoker, "*")

    galaxy = "//src/proc/bin/galaxies/starbionic_test"
  }
}

# Define a test package with expectations for a linux test running in a starbionic galaxy.
#
# The parameters are the same as fuchsia_test_package
template("starbionic_test_package_with_expectations") {
  starnix_test_package_with_expectations(target_name) {
    forward_variables_from(invoker, "*")

    galaxy = "//src/proc/bin/galaxies/starbionic_test"
  }
}

# Define a test package for a linux test running in a starmium galaxy.
#
# The parameters are the same as fuchsia_test_package
template("starmium_test_package") {
  starnix_test_package(target_name) {
    forward_variables_from(invoker, "*")

    galaxy = "//src/proc/bin/galaxies/starmium_test"
  }
}

# Define a test package for a linux test running in a starless galaxy.
#
# The parameters are the same as fuchsia_test_package
template("starless_test_package") {
  starnix_test_package(target_name) {
    forward_variables_from(invoker, "*")

    galaxy = "//src/proc/bin/galaxies/starless"
  }
}
