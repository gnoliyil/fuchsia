# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/proc/tests/chromiumos/build/starnix_linux_test_component.gni")
import("//src/proc/tests/starnix_test_package.gni")

group("tests") {
  testonly = true
  deps = []

  if (target_cpu == "x64") {
    deps += [ ":test_chromiumos_syscalls" ]
  }
}

test("syscalls_test_bin") {
  exclude_toolchain_tags = [ "asan" ]
  sources = [
    "bpf_test.cc",
    "custom_artifact_test.cc",
    "epoll_test.cc",
    "fcntl_test.cc",
    "fs_test.cc",
    "mmap_test.cc",
    "mount_test.cc",
    "pipe_test.cc",
    "poll_test.cc",
    "prctl_test.cc",
    "read_write_test.cc",
    "restart_read_test.cc",
    "socket_test.cc",
    "sw_breakpoint_test.cc",
    "task_test.cc",
    "terminal_test.cc",
    "test_helper.cc",
    "test_helper.h",
    "test_helper_test.cc",
    "timerfd_test.cc",
    "trivial_test.cc",
    "waitpid_exit_signal_test.cc",
    "waitpid_flags_test.cc",
  ]
  deps = [
    "//src/lib/files",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
  include_dirs = [
    "//third_party/android/platform/bionic/libc/kernel/uapi",
    "//third_party/android/platform/bionic/libc/kernel/android/uapi",
  ]
  if (target_cpu == "x64") {
    include_dirs +=
        [ "//third_party/android/platform/bionic/libc/kernel/uapi/asm-x86" ]
  }
}

test("crash") {
  exclude_toolchain_tags = [ "asan" ]
  sources = [ "crash.cc" ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

starnix_linux_test_component("syscalls_test") {
  test_label = ":syscalls_test_bin"
  test_binary = "syscalls_test_bin"
  uses_expectations = true
}

starnix_linux_test_component("crash_test") {
  test_label = ":crash"
  test_binary = "crash"
}

starmium_test_package("test_chromiumos_syscalls") {
  test_components = [
    ":syscalls_test",
    ":crash_test",
  ]
  deps = [ "//src/lib/testing/expectation:expectation_comparer" ]
}
