# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/python/python_host_test.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_component_with_expectations.gni")

group("tests") {
  testonly = true
  if (target_cpu == "x64" && !is_asan) {
    deps = [
      ":android",
      ":android_with_init",
      "benchmarks",
      "gvisor",
      "wayland",
    ]
  }
}

resource("binder_smoke_test_bin") {
  sources = [
    "//prebuilt/starnix/tests/syscalls/linux-amd64/stripped/binder_smoke_test",
  ]
  outputs = [ "data/tests/binder_smoke_test" ]
}

fuchsia_test_component("binder_smoke_test") {
  manifest = "meta/binder_smoke_test.cml"
  deps = [
    ":binder_smoke_test_bin",
    "//src/proc/tests:starnix_test",
  ]
}

fuchsia_test_component_with_expectations("adbd_test") {
  manifest = "meta/adbd_test.cml"
  expectations = "expectations/adbd_test.json5"
  deps = [
    "//src/connectivity/network/netstack:component",
    "//src/proc/tests:starnix_test_with_expectations",
  ]
}

fuchsia_test_component_with_expectations("libutils_test") {
  manifest = "meta/libutils_test.cml"
  expectations = "expectations/libutils_test.json5"
  deps = [ "//src/proc/tests:starnix_test_with_expectations" ]
}

fuchsia_test_component("init_test") {
  manifest = "meta/init_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_component("sh_test") {
  testonly = true
  check_references = false
  manifest = "meta/sh_test.cml"
  deps = [ "//src/proc/tests:starnix_test" ]
}

fuchsia_component("boringssl_self_test64") {
  testonly = true
  check_references = false
  manifest = "meta/boringssl_self_test64.cml"
}

group("container_resources") {
  if (target_cpu == "x64") {
    deps = [
      "//src/proc/bin/containers:data_tmp_target",
      "//src/proc/bin/containers:default_init",
      "//src/proc/bin/containers/bionic:system_image",
      "//src/proc/bin/containers/bionic:testcases_image",
    ]
  }
}

fuchsia_test_package("android") {
  test_components = [
    ":adbd_test",
    ":boringssl_self_test64",
    ":sh_test",
    ":binder_smoke_test",
    ":libutils_test",
  ]

  if (target_cpu == "x64") {
    subpackages = [ "//src/proc/bin/starnix:starnix_kernel_package" ]
  }

  deps = [
    ":container_resources",
    "//src/lib/testing/expectation:expectation_comparer",
  ]
}

fuchsia_test_package("android_with_init") {
  test_components = [ ":init_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }

  if (target_cpu == "x64") {
    subpackages = [ "//src/proc/bin/starnix:starnix_kernel_package" ]
  }

  deps = [
    ":container_resources",
    "//src/lib/testing/expectation:expectation_comparer",
  ]
}
