# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

gvisor_prebuilt_test_root = "//prebuilt/starnix/tests/syscalls/linux-amd64"

# Defines a Starnix test component.
#
# Parameters:
#   use_expectations (optional)
#     If true, include dependencies on tests with expectations.
#     Type: bool
#     Default: false
template("starnix_test_component") {
  test_name = target_name

  resource("${test_name}_bin") {
    sources = [ "${gvisor_prebuilt_test_root}/stripped/${test_name}" ]
    outputs = [ "data/tests/${test_name}" ]
  }

  fuchsia_component(test_name) {
    forward_variables_from(invoker, "*")

    testonly = true
    check_references = false
    manifest = "${invoker.meta_dir}/${test_name}.cml"

    if (!defined(deps)) {
      deps = []
    }

    deps += [ ":${test_name}_bin" ]

    use_expectations = false
    if (defined(invoker.use_expectations)) {
      use_expectations = invoker.use_expectations
    }

    if (use_expectations) {
      deps += [ "//src/proc/tests:starnix_test_with_expectations" ]
    } else {
      deps += [ "//src/proc/tests:starnix_test" ]
    }
  }
}
