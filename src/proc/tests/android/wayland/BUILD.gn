# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test_package.gni")

group("wayland") {
  testonly = true
  deps = [ ":starnix_wayland_tests" ]
}

wayland_tests = [
  "array-test",

  "client-test",

  # Fails with INVALID_ARGS (doesn't even run).
  # "compositor-introspection-test",

  # This test uses a hard-coded /tmp/ path which fails.
  # "connection-test",

  # This fails by timing out, looks like it uses futex and ends up stuck in a recvmsg.
  # "display-test",

  # This test might be failing due to epoll.
  # "event-loop-test",
  "fixed-test",
  "headers-protocol-core-test",
  "headers-protocol-test",
  "interface-test",
  "list-test",
  "map-test",
  "message-test",
  "newsignal-test",

  # Test hangs.
  # "os-wrappers-test",

  # This test gets stuck on epoll_pwait.
  # "protocol-logger-test",
  # This test is flaky, not sure what is causing it.
  # "queue-test",
  "resources-test",

  # This test fails because it tries to kill SIGTERM itself. There are also two leak check tests,
  # one is a malloc leak check that should be deleted (since the leak check code is deleted in the
  # version that we use anyway), and another is an FD leak check.
  # "sanity-test",
  "signal-test",

  # This test seems to require some flock functionality that we do not have (it uses this to, for
  # figure out whether or not to unlink the socket file).
  # "socket-test",
]

template("wayland_test_component") {
  test_name = target_name
  resource("${test_name}_bin") {
    sources = [
      "//prebuilt/starnix/tests/syscalls/linux-amd64/stripped/${test_name}",
    ]
    outputs = [ "data/tests/${test_name}" ]
  }

  fuchsia_test_component(target_name) {
    forward_variables_from(invoker, "*")

    manifest = "meta/" + target_name + ".cml"

    deps = [ ":${test_name}_bin" ]
  }
}

foreach(test_name, wayland_tests) {
  wayland_test_component(test_name) {
  }
}

group("container_resources") {
  deps = [
    "//src/proc/bin/containers:data_tmp_target",
    "//src/proc/bin/containers:default_init",
    "//src/proc/bin/containers/bionic:system_image",
  ]
}

fuchsia_test_package("starnix_wayland_tests") {
  test_components = []

  foreach(test_name, wayland_tests) {
    test_components += [ ":${test_name}" ]
  }

  if (target_cpu == "x64") {
    subpackages = [ "//src/proc/bin/starnix:starnix_kernel_package" ]
  }

  deps = [ ":container_resources" ]
}
