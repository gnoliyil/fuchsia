# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/proc/bin/starnix/starnix.gni")

group("chromiumos") {
  deps = [ ":chromiumos_package" ]
}

if (target_cpu == "x64") {
  resource("data_tmp_target") {
    sources = [ "//src/proc/bin/galaxies/placeholder" ]
    outputs = [ "data/tmp/.placeholder" ]
  }

  resource("system_image") {
    sources = [ "//prebuilt/starnix/chromiumos-image-amd64/system.img" ]
    outputs = [ "data/system.img" ]
  }

  resource("init") {
    init_label = "//src/proc/bin/galaxies/default_init($target_linux_toolchain)"
    out_dir = get_label_info(init_label, "root_out_dir")
    sources = [ "${out_dir}/default_init" ]
    outputs = [ "data/init" ]
    deps = [ init_label ]
  }
}

fuchsia_package_with_single_component("chromiumos_package") {
  manifest = "meta/chromiumos_galaxy.cml"
  package_name = "starmium"
  component_name = "chromiumos_galaxy"

  if (target_cpu == "x64") {
    deps = [
      ":data_tmp_target",
      ":init",
      ":system_image",
    ]
  }
}
