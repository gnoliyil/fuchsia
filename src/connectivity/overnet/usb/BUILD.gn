# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("overnet_usb_bind") {
  rules = "overnet_usb.bind"
  bind_output = "overnet-usb.bindbc"
  tests = "bind-tests.json"
  deps = [
    "//src/devices/bind/fuchsia.google.usb",
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.comm",
  ]
}

source_set("overnet-usb-lib") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "overnet_usb.cc",
    "overnet_usb.h",
  ]
  public_deps = [
    ":overnet_usb_bind",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.composite:fuchsia.hardware.usb.composite_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.overnet:fuchsia.hardware.overnet_cpp",
    "//src/devices/bind/fuchsia.google.usb:fuchsia.google.usb_cpp",
    "//src/devices/usb/lib/usb",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("overnet-usb-driver") {
  # visibility = [ ":*" ]
  output_name = "overnet-usb"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":overnet-usb-lib",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("overnet-usb") {
  component_name = "overnet-usb"
  deps = [ ":overnet-usb-driver" ]
  info = "overnet-usb-info.json"
  colocate = true
}

test("overnet-usb-test-bin") {
  testonly = true
  sources = [ "overnet_usb_test.cc" ]
  deps = [
    ":overnet-usb-lib",
    "//sdk/banjo/fuchsia.hardware.usb.function:fuchsia.hardware.usb.function_banjo_cpp",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("overnet-usb-test") {
  deps = [ ":overnet-usb-test-bin" ]
}

group("tests") {
  testonly = true
  deps = [
    ":overnet-usb-test",
    ":overnet_usb_bind_test",
  ]
}
