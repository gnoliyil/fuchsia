# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  name = "overnetstack"
  with_unit_tests = true
  edition = "2021"
  deps = [
    "//sdk/fidl/fuchsia.hardware.serial:fuchsia.hardware.serial_rust",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.mdns:fuchsia.net.mdns_rust",
    "//sdk/fidl/fuchsia.overnet:fuchsia.overnet_rust",
    "//sdk/fidl/fuchsia.overnet.protocol:fuchsia.overnet.protocol_rust",
    "//src/connectivity/overnet/lib/core",
    "//src/connectivity/overnet/lib/serial_link",
    "//src/connectivity/overnet/lib/stream_link",
    "//src/connectivity/overnet/lib/udp_link",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "src/main.rs",
    "src/mdns.rs",
    "src/serial.rs",
  ]
}

fuchsia_component("component") {
  component_name = "overnetstack"

  deps = [
    ":bin",
    "//src/connectivity/overnet:certificates",
  ]
  manifest = "meta/overnetstack.cml"
}

fuchsia_package("overnetstack") {
  visibility = [
    ":*",
    "//bundles/assembly/*",
  ]
  deps = [ ":component" ]
}

fuchsia_package_archive("overnetstack-archive") {
  package = ":overnetstack"
}

fuchsia_component("component-with-udp") {
  component_name = "overnetstack-with-udp"

  deps = [
    ":bin",
    "//src/connectivity/overnet:certificates",
  ]
  manifest = "meta/overnetstack-with-udp.cml"
}

fuchsia_unittest_package("overnetstack-tests") {
  deps = [
    ":bin_test",
    "//src/connectivity/overnet:certificates",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":overnetstack-tests" ]
}
