# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("rndishost_bind") {
  rules = "rndishost.bind"
  header_output = "rndishost_bind.h"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.usb",
    "//src/devices/bind/fuchsia.usb.wireless",
  ]
}

fuchsia_driver("fuchsia_driver") {
  visibility = [
    ":*",
    "//src/connectivity/ethernet:drivers",
  ]
  output_name = "rndis-host"

  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  sources = [
    "rndis_host.cc",
    "rndis_host.h",
  ]
  deps = [
    ":rndishost_bind",
    "//sdk/banjo/fuchsia.hardware.ethernet:fuchsia.hardware.ethernet_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb:fuchsia.hardware.usb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.composite:fuchsia.hardware.usb.composite_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.usb.request:fuchsia.hardware.usb.request_banjo_cpp",
    "//src/connectivity/ethernet/lib/rndis",
    "//src/devices/lib/driver",
    "//src/devices/usb/lib/usb",
    "//src/devices/usb/lib/usb:cdc",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_driver_component("driver") {
  component_name = "rndis-host"
  deps = [ ":fuchsia_driver" ]
  info = "driver-info.json"
  manifest = "meta/driver.cml"
}

fuchsia_driver_package("rndis-host") {
  driver_components = [ ":driver" ]
}

group("tests") {
  testonly = true
  deps = [ ":rndishost_bind_test" ]
}
