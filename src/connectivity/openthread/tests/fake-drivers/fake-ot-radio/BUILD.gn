# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("fake_ot_radio_bind") {
  rules = "fake-ot-radio.bind"
  header_output = "fake_ot_radio_bind.h"
  tests = "bind-tests.json"
  deps = [ "//src/devices/bind/fuchsia.test.platform" ]
}

fuchsia_driver("fake-ot-radio-driver-driver") {
  output_name = "fake-ot-radio"
  testonly = true

  sources = [
    "fake-ot-radio.cc",
    "fake-ot-radio.h",
  ]

  deps = [
    ":fake_ot_radio_bind",
    "//sdk/fidl/fuchsia.lowpan.spinel:fuchsia.lowpan.spinel_cpp_wire",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("fake-ot-radio-driver") {
  info = "fake-ot-radio-driver-info.json"
  testonly = true
  component_name = "fake-ot-radio"
  deps = [ ":fake-ot-radio-driver-driver" ]
  colocate = true
}

group("tests") {
  testonly = true
  deps = [ ":fake_ot_radio_bind_test" ]
}
