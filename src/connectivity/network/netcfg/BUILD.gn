# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")

rustc_library("lib") {
  name = "netcfg"
  with_unit_tests = true
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_rust",
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.dhcp:fuchsia.net.dhcp_rust",
    "//sdk/fidl/fuchsia.net.dhcpv6:fuchsia.net.dhcpv6_rust",
    "//sdk/fidl/fuchsia.net.filter:fuchsia.net.filter_rust",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces_rust",
    "//sdk/fidl/fuchsia.net.interfaces.admin:fuchsia.net.interfaces.admin_rust",
    "//sdk/fidl/fuchsia.net.masquerade:fuchsia.net.masquerade_rust",
    "//sdk/fidl/fuchsia.net.name:fuchsia.net.name_rust",
    "//sdk/fidl/fuchsia.net.stack:fuchsia.net.stack_rust",
    "//sdk/fidl/fuchsia.net.virtualization:fuchsia.net.virtualization_rust",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_rust",
    "//src/connectivity/lib/fidl_fuchsia_net_dhcpv6_ext",
    "//src/connectivity/lib/net-declare",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/network/dhcpv4/lib",
    "//src/connectivity/network/lib/dns_server_watcher",
    "//src/connectivity/network/lib/netfilter",
    "//src/lib/async-utils",
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/lib/network/fidl_fuchsia_net_dhcp_ext",
    "//src/lib/network/fidl_fuchsia_net_ext",
    "//src/lib/network/fidl_fuchsia_net_interfaces_ext",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:derivative",
    "//third_party/rust_crates:either",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:nonzero_ext",
    "//third_party/rust_crates:pretty_assertions",
    "//third_party/rust_crates:tempfile",
    "//third_party/rust_crates:test-case",
  ]

  sources = [
    "src/devices.rs",
    "src/dhcpv4.rs",
    "src/dhcpv6.rs",
    "src/dns.rs",
    "src/errors.rs",
    "src/interface.rs",
    "src/lib.rs",
    "src/masquerade.rs",
    "src/virtualization.rs",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

rustc_binary("bin-basic") {
  edition = "2021"
  name = "netcfg_basic"
  source_root = "src/bin/basic.rs"

  deps = [
    ":lib",
    "//src/lib/fuchsia-async",
  ]

  sources = [ "src/bin/basic.rs" ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

rustc_binary("bin-advanced") {
  edition = "2021"
  name = "netcfg_advanced"
  source_root = "src/bin/advanced.rs"

  deps = [
    ":lib",
    "//src/lib/fuchsia-async",
  ]

  sources = [ "src/bin/advanced.rs" ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("component-basic") {
  component_name = "netcfg-basic"
  manifest = "meta/netcfg-basic.cml"
  deps = [ ":bin-basic" ]
}

fuchsia_component("component-advanced") {
  component_name = "netcfg-advanced"
  manifest = "meta/netcfg-advanced.cml"
  deps = [ ":bin-advanced" ]
}

config_data("config-default") {
  for_pkg = "netcfg"
  sources = [ "config/default.json" ]
  outputs = [ "default.json" ]
}

config_data("config-no-ssh-over-wlan") {
  for_pkg = "netcfg"
  sources = [ "config/no_ssh_over_wlan.json" ]
  outputs = [ "default.json" ]
}

config_data("config-prefer-wired") {
  for_pkg = "netcfg"
  sources = [ "config/prefer_wired.json" ]
  outputs = [ "default.json" ]
}

fuchsia_unittest_package("netcfg-tests") {
  deps = [ ":lib_test" ]
  manifest = "meta/netcfg_lib_test.cml"
}

group("tests") {
  testonly = true
  deps = [ ":netcfg-tests" ]
}
