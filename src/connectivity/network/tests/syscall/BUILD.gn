# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

name_provider_service_not_present_label = "name_provider_service_not_present"
bsdsocket_label = "bsdsocket"
dgramsocket_label = "dgramsocket"
if_nameindex_label = "if_nameindex"
streamsocket_label = "streamsocket"
fuchsia_label = "fuchsia"
packetsocket_label = "packetsocket"
rawsocket_label = "rawsocket"

test("netstack_${name_provider_service_not_present_label}_test") {
  sources = [ "name_provider_service_not_present_test.cc" ]

  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_hlcpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

test("netstack_${bsdsocket_label}_test") {
  sources = [
    "../os.h",
    "bsdsocket_test.cc",
    "no_network_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp_wire",
      "//src/lib/testing/predicates",
    ]
  }
}

test("netstack_${dgramsocket_label}_test") {
  sources = [
    "../os.h",
    "dgramsocket_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp_wire",
      "//sdk/lib/fdio",
      "//src/connectivity/network/netstack/udp_serde",
      "//src/lib/testing/predicates",
    ]
  }
}

test("netstack_${if_nameindex_label}_test") {
  sources = [ "if_nameindex_test.cc" ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp_wire",
      "//src/lib/testing/predicates",
    ]
  }
}

test("netstack_${streamsocket_label}_test") {
  sources = [
    "../os.h",
    "streamsocket_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp_wire",
      "//src/lib/testing/predicates",
    ]
  }
}

test("netstack_${fuchsia_label}_test") {
  sources = [
    "../os.h",
    "fdio_test.cc",
    "util.cc",
    "util.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp_wire",
    "//sdk/lib/fdio",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/sync",
  ]
}

test("netstack_${packetsocket_label}_test") {
  sources = [ "packetsocket_test.cc" ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
  ]
}

test("netstack_${rawsocket_label}_test") {
  sources = [
    "../os.h",
    "rawsocket_test.cc",
  ]

  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/fbl",
  ]
}

tests_without_manifest = [
  dgramsocket_label,
  streamsocket_label,
  bsdsocket_label,
  if_nameindex_label,
]

test_components_without_manifest = []

foreach(test, tests_without_manifest) {
  name = "netstack_${test}_test"
  fuchsia_unittest_component("${name}_component") {
    component_name = name
    deps = [
      ":${name}",
      ":socket_test_includes",
    ]
  }
  test_components_without_manifest += [ name ]
}

expect_includes("socket_test_includes") {
  includes = [
    "//src/connectivity/network/dns/meta/client.shard.cml",
    "//src/connectivity/network/netstack/meta/client-with-sync-udp-debug.shard.cml",
    "//src/sys/test_runners/gtest/default.shard.cml",
    "//sdk/lib/syslog/client.shard.cml",
  ]
}

name_provider_test_name =
    "netstack_${name_provider_service_not_present_label}_test"
fuchsia_unittest_component("${name_provider_test_name}_component") {
  component_name = name_provider_test_name
  deps = [
    ":${name_provider_test_name}",
    ":name_provider_includes",
  ]
}

expect_includes("name_provider_includes") {
  includes = [
    "//src/sys/test_runners/gtest/default.shard.cml",
    "//sdk/lib/syslog/client.shard.cml",
  ]
}

test_components_without_manifest += [ name_provider_test_name ]

tests_with_manifest = [
  packetsocket_label,
  fuchsia_label,
  rawsocket_label,
]

test_components_with_manifest = []

foreach(test, tests_with_manifest) {
  name = "netstack_${test}_test"
  fuchsia_unittest_component("${name}_component") {
    component_name = name
    deps = [ ":${name}" ]
    manifest = "meta/netstack2/${test}.cml"
  }
  test_components_with_manifest += [ name ]
}

tests_with_fast_udp = [
  bsdsocket_label,
  dgramsocket_label,
  fuchsia_label,
  if_nameindex_label,
]

test_components_with_fast_udp = []

foreach(test, tests_with_fast_udp) {
  name = "netstack_with_fast_udp_${test}_test"
  fuchsia_unittest_component("${name}_component") {
    component_name = name
    deps = [ ":netstack_${test}_test" ]
    manifest = "meta/netstack2/fastudp/${test}.cml"
  }
  test_components_with_fast_udp += [ name ]
}

tests_with_netstack3 = [ if_nameindex_label ]

test_components_with_netstack3 = []

foreach(test, tests_with_netstack3) {
  name = "netstack3_${test}_test"
  fuchsia_unittest_component("${name}_component") {
    component_name = name
    deps = [ ":netstack_${test}_test" ]
    manifest = "meta/netstack3/${test}.cml"
  }
  test_components_with_netstack3 += [ name ]
}

fuchsia_test_package("netstack-c-api-tests") {
  test_components = []
  foreach(test,
          test_components_without_manifest + test_components_with_manifest +
              test_components_with_fast_udp + test_components_with_netstack3) {
    test_components += [ ":${test}_component" ]
  }

  deps = [
    # dns_resolver is under test.
    "//src/connectivity/network/dns:component",

    # netstack is under test.
    "//src/connectivity/network/netstack:component-with-fast-udp-debug",
    "//src/connectivity/network/netstack:component-with-sync-udp-debug",
    "//src/connectivity/network/netstack3:component-debug",

    # cat is used to test FD passing in :fuchsia_test.
    "//third_party/sbase:cat_bin",
  ]
}

group("tests") {
  testonly = true
  public_deps = [ ":netstack-c-api-tests" ]
  if (host_os == "linux") {
    public_deps += [
      ":netstack_${bsdsocket_label}_test($host_toolchain)",
      ":netstack_${dgramsocket_label}_test($host_toolchain)",
      ":netstack_${if_nameindex_label}_test($host_toolchain)",
      ":netstack_${packetsocket_label}_test($host_toolchain)",
      ":netstack_${rawsocket_label}_test($host_toolchain)",
      ":netstack_${streamsocket_label}_test($host_toolchain)",
    ]
  }
}
