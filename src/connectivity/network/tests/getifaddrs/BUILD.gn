# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

test("test") {
  output_name = "getifaddrs-test"
  if (is_linux || is_fuchsia) {
    sources = [
      "../os.h",
      "getifaddrs_test.cc",
    ]
  }
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_test_component("test-driver") {
  manifest = "meta/test-driver.cml"
  deps = [ ":test" ]
}

common_deps = [ ":test-driver" ]

fuchsia_test_component("ns2-tests") {
  manifest = "meta/netstack2.cml"
  deps = common_deps +
         [ "//src/connectivity/network/testing/netemul/configurable-netstack" ]
}

fuchsia_test_component("ns3-tests") {
  manifest = "meta/netstack3.cml"
  deps = common_deps + [ "//src/connectivity/network/testing/netemul/configurable-netstack:configurable-netstack-netstack3" ]
}

fuchsia_test_package("getifaddrs-tests") {
  test_components = [
    ":ns2-tests",
    ":ns3-tests",
  ]
}

group("tests") {
  testonly = true

  deps = [
    ":getifaddrs-tests",
    ":ns2-tests($host_toolchain)",
    ":ns3-tests($host_toolchain)",
  ]
}
