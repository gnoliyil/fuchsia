# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

test("test") {
  output_name = "getifaddrs-test"
  if (is_linux || is_fuchsia) {
    sources = [
      "../os.h",
      "getifaddrs_test.cc",
    ]
  }
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
  ]
}

fuchsia_test_component("test-driver") {
  manifest = "meta/test-driver.cml"
  deps = [ ":test" ]
}

fuchsia_test_component("getifaddrs-test") {
  manifest = "meta/getifaddrs-test.cml"
  deps = [
    ":test-driver",
    "//src/connectivity/network/testing/netemul/configurable-netstack",
  ]
}

fuchsia_unittest_package("getifaddrs-test-ns3") {
  manifest = "meta/getifaddrs-test-ns3.cml"
  deps = [
    ":test-driver",
    "//src/connectivity/network/testing/netemul/configurable-netstack:configurable-netstack-netstack3",
  ]

  # TODO(https://fxbug.dev/123587): Re-enable this test and move it into the
  # getifaddrs-tests package when either:
  #  * Netstack3 passes the test, or
  #  * Netemul runner tests are compatible with the expectations framework and
  #    we can mark it "expect fail".
  test_specs = {
    environments = []
  }
}

fuchsia_test_package("getifaddrs-tests") {
  test_components = [ ":getifaddrs-test" ]
}

group("tests") {
  testonly = true

  deps = [
    ":getifaddrs-test($host_toolchain)",
    ":getifaddrs-test-ns3",
    ":getifaddrs-tests",
  ]
}
