# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_component_with_expectations.gni")

test("bin") {
  output_name = "multi_nic_test_client"
  sources = [
    "main.cc",
    "socket_test.cc",
  ]

  deps = [
    "//src/connectivity/network/testing/netemul/sync-manager/fidl:sync_cpp_wire",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_test_component("server") {
  manifest = "meta/server.cml"
  deps = [ "server" ]
}

fuchsia_unittest_component("client") {
  manifest = "meta/client.cml"
  deps = [ ":bin" ]
}

common_deps = [
  ":client",
  ":server",
]

fuchsia_test_component_with_expectations("ns3-tests") {
  expectations = "expects/netstack3.json5"
  manifest = "meta/netstack3.cml"
  deps = common_deps + [
           "//src/connectivity/network/testing/netemul/configurable-netstack:configurable-netstack-netstack3",
           "//src/connectivity/network/testing/netemul/sync-manager",
         ]
}

fuchsia_test_package("ns3-multi-nic-tests") {
  test_components = [ ":ns3-tests" ]
  deps = [ "//src/lib/testing/expectation:expectation_comparer" ]
}

all_test_targets = [ ":ns3-multi-nic-tests" ]

if (target_cpu != "riscv64") {
  fuchsia_test_component_with_expectations("ns2-tests") {
    expectations = "expects/netstack2.json5"
    manifest = "meta/netstack2.cml"
    deps = common_deps + [
             "//src/connectivity/network/testing/netemul/configurable-netstack",
             "//src/connectivity/network/testing/netemul/sync-manager",
           ]
  }

  fuchsia_test_component_with_expectations("ns2-tests-with-fast-udp") {
    expectations = "expects/netstack2.json5"
    manifest = "meta/netstack2-fast-udp.cml"
    deps = common_deps + [
             "//src/connectivity/network/testing/netemul/configurable-netstack:configurable-netstack-fast-udp",
             "//src/connectivity/network/testing/netemul/sync-manager",
           ]
  }

  fuchsia_test_package("ns2-multi-nic-tests") {
    test_components = [
      ":ns2-tests",
      ":ns2-tests-with-fast-udp",
    ]
    deps = [ "//src/lib/testing/expectation:expectation_comparer" ]
  }

  all_test_targets += [ ":ns2-multi-nic-tests" ]
}

group("tests") {
  testonly = true
  public_deps = all_test_targets
}
