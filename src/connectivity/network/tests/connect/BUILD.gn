# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_component_with_expectations.gni")

rustc_test("connect") {
  edition = "2021"
  deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//src/connectivity/lib/net-declare",
    "//src/connectivity/network/testing/netemul/network-context/fidl:network_rust",
    "//src/connectivity/network/testing/netemul/rust:lib",
    "//src/connectivity/network/tests/integration/common:netstack_testing_common",
    "//src/connectivity/network/tests/integration/macros:netstack_testing_macros",
    "//src/lib/fuchsia-async",
    "//src/lib/tcp-stream-ext",
    "//third_party/rust_crates:futures-util",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:socket2",
  ]
  sources = [ "src/lib.rs" ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

package_deps = [
  "//src/connectivity/network/netstack3:component-debug",
  "//src/connectivity/network/testing/netemul/service:netemul-sandbox",
  "//src/lib/testing/expectation:expectation_comparer",
]

# RISC-V on Fuchsia does not support Golang.
if (target_cpu != "riscv64") {
  package_deps += [ "//src/connectivity/network/netstack:component-debug" ]
}

fuchsia_test_component_with_expectations("netstack-connect-test-component") {
  expectations = "expects.json5"
  manifest = "meta/connect.cml"
  deps = [ ":connect" ]
}

fuchsia_test_package("netstack-connect-tests") {
  test_components = [ ":netstack-connect-test-component" ]
  deps = package_deps
}

group("tests") {
  testonly = true
  deps = [ ":netstack-connect-tests" ]
}
