# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/go/go_binary.gni")
import("//build/go/go_library.gni")
import("//build/go/toolchain.gni")

go_library("puppet_lib") {
  deps = [
    "//src/connectivity/network/netstack/inspect",
    "//src/connectivity/network/netstack/inspect/vmobuffer",
    "//src/lib/component",
  ]

  sources = [ "main.go" ]
}

go_binary("puppet_bin") {
  library = ":puppet_lib"
  deps = [
    "//src/diagnostics/validator/inspect/fidl:validate_go($go_toolchain)",
    "//src/lib/component",
  ]
}

fuchsia_unittest_component("puppet") {
  deps = [ ":puppet_bin" ]

  # This must be named puppet.cml or have `component_name = "puppet"` so
  # `children: url: "#meta/puppet.cm"` in
  # //src/diagnostics/validator/inspect/meta/validator.shard.cml can find it.
  manifest = "meta/puppet.cml"
}

# The parent component of #validator and #puppet.
fuchsia_test_component("inspect_validator_test_component") {
  component_name = "validator_test"
  manifest = "meta/test.cml"
}

fuchsia_package("go") {
  testonly = true
  package_name = "go-puppet"
  deps = [ ":puppet" ]
}

fuchsia_test_package("inspect-validator-tests-go") {
  subpackages = [ "//src/diagnostics/validator/inspect:validator" ]
  renameable_subpackages = [
    {
      name = "puppet"
      package = "//src/connectivity/network/netstack/inspect/validator:go"
    },
  ]
  test_components = [ ":inspect_validator_test_component" ]
}

group("tests") {
  testonly = true
  deps = [ ":inspect-validator-tests-go" ]
}
