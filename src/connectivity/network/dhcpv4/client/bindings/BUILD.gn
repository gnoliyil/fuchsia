# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_with_expectations_package.gni")

rustc_binary("dhcp_client") {
  edition = "2021"
  with_unit_tests = true
  deps = [
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.dhcp:fuchsia.net.dhcp_rust",
    "//sdk/fidl/fuchsia.net.interfaces.admin:fuchsia.net.interfaces.admin_rust",
    "//sdk/fidl/fuchsia.posix:fuchsia.posix_rust",
    "//sdk/fidl/fuchsia.posix.socket.packet:fuchsia.posix.socket.packet_rust",
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/dhcpv4/client/core",
    "//src/connectivity/network/dhcpv4/protocol",
    "//src/connectivity/network/lib/sockaddr",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/network/fidl_fuchsia_net_ext",
    "//src/lib/network/fidl_fuchsia_posix_socket_ext",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]
  test_deps = [
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_rust",
    "//src/connectivity/lib/net-declare",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/testing/netemul/network-context/fidl:network_rust",
    "//src/connectivity/network/testing/netemul/rust:lib",
    "//src/connectivity/network/tests/integration/common:netstack_testing_common",
    "//src/lib/network/packet",
  ]
  sources = [
    "src/client.rs",
    "src/main.rs",
    "src/packetsocket.rs",
    "src/provider.rs",
    "src/udpsocket.rs",
  ]
  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_component("component") {
  component_name = "dhcp-client"
  manifest = "meta/dhcp-client.cml"
  deps = [ ":dhcp_client" ]
}

fuchsia_test_component("dhcp-client-test") {
  manifest = "meta/dhcp-client-test.cml"
  deps = [
    ":dhcp_client_test",
    "//src/connectivity/network/netstack:component-debug",
    "//src/connectivity/network/testing/netemul/service:netemul-sandbox",
  ]
}

fuchsia_package("dhcp-client") {
  deps = [ ":component" ]
}

fuchsia_test_with_expectations_package("dhcp-client-tests") {
  test_components = [ ":dhcp-client-test" ]
  treatment_of_cases_with_error_logs = "SKIP_CASES_WITH_ERROR_LOGS"
  expectations = "expects/expectations.json5"
  deps = [ ":component" ]
}

fuchsia_test_with_expectations_package("dhcp-client-tests-with-error-logs") {
  test_components = [ ":dhcp-client-test" ]
  treatment_of_cases_with_error_logs = "RUN_ONLY_CASES_WITH_ERROR_LOGS"
  expectations = "expects/expectations.json5"
  deps = [ ":component" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":dhcp-client-tests",
    ":dhcp-client-tests-with-error-logs",
  ]
}
