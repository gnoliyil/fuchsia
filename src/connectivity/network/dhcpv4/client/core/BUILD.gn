# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

rustc_library("core") {
  name = "dhcp_client_core"
  edition = "2021"
  with_unit_tests = true
  deps = [
    "//src/connectivity/lib/net-types",
    "//src/connectivity/lib/packet-formats",
    "//src/connectivity/network/dhcpv4/protocol",
    "//src/lib/fuchsia-async",
    "//src/lib/network/packet",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:derive_builder",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]
  test_deps = [
    "//src/connectivity/lib/net-declare",
    "//src/connectivity/network/dhcpv4/lib",
    "//src/lib/const-unwrap",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:test-case",
    "//third_party/rust_crates:tracing-subscriber",
  ]

  sources = [
    "src/client.rs",
    "src/deps.rs",
    "src/lib.rs",
    "src/parse.rs",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_unittest_package("dhcp_client_core_test") {
  deps = [ ":core_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":dhcp_client_core_test" ]

  # include the non-test library so that we observe unused warnings for fns used
  # only in tests.
  deps += [
    ":core",
    ":core($host_toolchain)",
  ]
}
