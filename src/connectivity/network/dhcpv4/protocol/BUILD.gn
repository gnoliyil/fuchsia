# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

rustc_library("protocol") {
  name = "dhcp-protocol"
  with_unit_tests = true
  edition = "2021"
  deps = [
    "//src/connectivity/lib/net-types",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:hex",
    "//third_party/rust_crates:num-derive",
    "//third_party/rust_crates:num-traits",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:static_assertions",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:zerocopy",
  ]
  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
      "//sdk/fidl/fuchsia.net.dhcp:fuchsia.net.dhcp_rust",
    ]
  }

  sources = [
    "src/lib.rs",
    "src/size_constrained.rs",
    "src/size_of_contents.rs",
  ]

  test_deps = [
    "//src/connectivity/lib/net-declare",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:rand",
    "//third_party/rust_crates:test-case",
  ]

  configs -= [ "//build/config/rust/lints:allow_unused_results" ]
}

fuchsia_unittest_package("dhcpv4_protocol_test") {
  deps = [ ":protocol_test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":dhcpv4_protocol_test",
    ":protocol_test($host_toolchain)",
  ]
}
