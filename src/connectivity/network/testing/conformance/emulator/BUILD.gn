# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/board.gni")
import("//build/go/go_library.gni")
import("//build/go/go_test.gni")

if (is_linux && has_board) {
  go_library("emulator") {
    sources = [
      "emulator.go",
      "emulator_test.go",
    ]
    deps = [
      "//src/connectivity/network/testing/conformance/util",
      "//tools/emulator",
      "//tools/virtual_device:proto",
    ]
    non_go_deps = [ ":zbi_bin" ]
    testonly = true
  }

  go_test("conformance_emulator_test") {
    library = ":emulator"
    deps = [ "//src/connectivity/network/testing/conformance/ffx" ]
    non_go_deps = [
      "//build/images/network-conformance:network-conformance-base-test-data",
      "//src/connectivity/network/testing/network-test-realm/ffx:ffx_net_test_realm_tool_test_data",
      "//src/developer/ffx:test_data",
      "//src/developer/ffx/plugins/component:ffx_component_tool_test_data",
      "//src/developer/ffx/plugins/target:ffx_target_tool_test_data",
    ]
  }

  out_dir =
      "${root_out_dir}/src/connectivity/network/testing/conformance/emulator"

  host_test_data("zbi_bin") {
    deps = [ "//zircon/tools/zbi" ]
    sources = [ "${root_out_dir}/zbi" ]
    outputs = [ "${out_dir}/{{source_file_part}}" ]
  }
}

group("tests") {
  if (host_os == "linux" && has_board) {
    deps = [ ":conformance_emulator_test($host_toolchain)" ]
  }
  testonly = true
}
