# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("wlantapctl_bind") {
  rules = "wlantapctl.bind"
  bind_output = "wlantapctl.bindbc"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

group("tests") {
  testonly = true
  deps = [ ":wlantapctl_bind_test" ]
}

fuchsia_driver("wlantap_driver-driver") {
  output_name = "wlantap"

  sources = [
    "utils.cc",
    "utils.h",
    "wlantap-ctl.cc",
    "wlantap-driver-context.h",
    "wlantap-driver.cc",
    "wlantap-mac.cc",
    "wlantap-mac.h",
    "wlantap-phy-impl.cc",
    "wlantap-phy-impl.h",
    "wlantap-phy.cc",
    "wlantap-phy.h",
  ]

  deps = [
    ":wlantapctl_bind",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_banjo_c",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_hlcpp",
    "//sdk/fidl/fuchsia.wlan.device:fuchsia.wlan.device_hlcpp",
    "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_cpp",
    "//sdk/fidl/fuchsia.wlan.phyimpl:fuchsia.wlan.phyimpl_cpp_driver",
    "//sdk/fidl/fuchsia.wlan.softmac:fuchsia.wlan.softmac_cpp_driver",
    "//sdk/fidl/fuchsia.wlan.softmac:fuchsia.wlan.softmac_cpp_wire",
    "//sdk/fidl/fuchsia.wlan.tap:fuchsia.wlan.tap_cpp_driver",

    # ddk is still needed for ZX_PROTOCOL* definitions
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devfs/cpp",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//zircon/system/public:public",
    "//zircon/system/ulib/zx",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("wlantap_driver") {
  info = "wlantap_driver-info.json"
  component_name = "wlantap"
  manifest = "meta/wlantap.cml"
  deps = [ ":wlantap_driver-driver" ]
}
