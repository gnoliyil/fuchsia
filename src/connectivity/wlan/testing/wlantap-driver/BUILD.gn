# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

driver_bind_rules("wlantapctl_bind") {
  rules = "wlantapctl.bind"
  header_output = "wlantapctl_bind.h"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.test" ]
}

group("tests") {
  testonly = true
  deps = [ ":wlantapctl_bind_test" ]
}

fuchsia_driver("wlantap_driver-driver") {
  output_name = "wlantap"

  sources = [
    "utils.cc",
    "utils.h",
    "wlantap-ctl.cc",
    "wlantap-mac.cc",
    "wlantap-mac.h",
    "wlantap-phy.cc",
    "wlantap-phy.h",
  ]

  deps = [
    ":wlantapctl_bind",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_banjo_c",
    "//sdk/fidl/fuchsia.wlan.common:fuchsia.wlan.common_hlcpp",
    "//sdk/fidl/fuchsia.wlan.device:fuchsia.wlan.device_hlcpp",
    "//sdk/fidl/fuchsia.wlan.internal:fuchsia.wlan.internal_banjo_cpp",
    "//sdk/fidl/fuchsia.wlan.phyimpl:fuchsia.wlan.phyimpl_cpp_driver",
    "//sdk/fidl/fuchsia.wlan.softmac:fuchsia.wlan.softmac_cpp_driver",
    "//sdk/fidl/fuchsia.wlan.softmac:fuchsia.wlan.softmac_cpp_wire",
    "//sdk/fidl/fuchsia.wlan.tap:fuchsia.wlan.tap_cpp_driver",
    "//sdk/fidl/fuchsia.wlan.tap:fuchsia.wlan.tap_cpp_hlcpp_conversion",
    "//sdk/fidl/fuchsia.wlan.tap:fuchsia.wlan.tap_cpp_wire",
    "//sdk/fidl/fuchsia.wlan.tap:fuchsia.wlan.tap_hlcpp",
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/connectivity/wlan/drivers/wlansoftmac:wlansoftmac_convert",
    "//src/connectivity/wlan/lib/common/cpp:common",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/sync:sync-cpp",
    "//zircon/system/ulib/zx",
  ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("wlantap_driver") {
  info = "wlantap_driver-info.json"
  component_name = "wlantap"
  deps = [ ":wlantap_driver-driver" ]
  manifest = "meta/wlantap_driver.cml"
}
