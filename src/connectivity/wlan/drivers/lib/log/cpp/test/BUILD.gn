# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

config("log_overrides_config") {
  include_dirs = [ "include" ]
}

source_set("log_overrides") {
  testonly = true
  public_configs = [ ":log_overrides_config" ]
  sources = [ "mock_logging_calls.cc" ]

  deps = [
    "//src/connectivity/wlan/drivers/lib/log/cpp",
    "//src/lib/ddk",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/zx",
  ]
}

test("wlan_drivers_log_test") {
  sources = [
    "basic_log_test.cc",
    "fmt_test.cc",
    "hexdump_log_test.cc",
    "throttle_counter_test.cc",
    "throttle_log_test.cc",
    "zx_ticks_override.cc",
  ]
  deps = [
    ":log_overrides",
    "//sdk/fidl/fuchsia.wlan.ieee80211:fuchsia.wlan.ieee80211_hlcpp",
    "//src/connectivity/wlan/drivers/lib/log/cpp",
    "//src/lib/fxl/test:gtest_main",
  ]

  # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]
}

fuchsia_unittest_package("wlan-drivers-log-tests") {
  deps = [ ":wlan_drivers_log_test" ]
}
