# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/connectivity/bluetooth/core/bt-host/build/bt_gtest_package.gni")

# Basic target with protocol definitions and no logic, suitable for test
# emulation.
source_set("definitions") {
  sources = [
    "att.h",
    "packet.cc",
    "packet.h",
  ]

  public_deps = [
    "//src/connectivity/bluetooth/core/bt-host/common",
    "//src/connectivity/bluetooth/core/bt-host/hci-spec",
    "//src/connectivity/bluetooth/core/bt-host/l2cap:definitions",
  ]
}

static_library("att") {
  sources = [
    "attribute.cc",
    "attribute.h",
    "bearer.cc",
    "bearer.h",
    "database.cc",
    "database.h",
    "error.cc",
    "error.h",
    "permissions.cc",
    "permissions.h",
    "write_queue.cc",
    "write_queue.h",
  ]

  public_deps = [
    ":definitions",
    "//sdk/lib/fit",
    "//src/connectivity/bluetooth/core/bt-host/l2cap",
    "//src/connectivity/bluetooth/core/bt-host/sm:definitions",
  ]

  deps = [ "//third_party/pigweed:pw_string" ]
}

source_set("tests") {
  testonly = true

  sources = [
    "attribute_unittest.cc",
    "bearer_unittest.cc",
    "database_unittest.cc",
    "error_unittest.cc",
    "permissions_unittest.cc",
  ]

  deps = [
    ":att",
    "//src/connectivity/bluetooth/core/bt-host/l2cap:testing",
    "//third_party/googletest:gtest",
  ]
}

bt_gtest_package("bt-host-att-tests") {
  with_death_tests = true
  deps = [ ":tests" ]
}
