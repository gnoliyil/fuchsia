# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("main") {
  testonly = true
  edition = "2021"
  name = "default_test_realm_proxy"
  sources = [ "src/main.rs" ]
  deps = [
    ":config_lib",
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_rust",
    "//src/lib/fidl_server",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/testing/realm_proxy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component_manifest("manifest") {
  testonly = true
  component_name = "default"
  manifest = "meta/default.cml"
}

fuchsia_structured_config_rust_lib("config_lib") {
  testonly = true
  cm_label = ":manifest"
}

fuchsia_component("component") {
  testonly = true
  cm_label = ":manifest"
  deps = [ ":main" ]
}

group("tests") {
  testonly = true
  deps = [ "tests" ]
}
