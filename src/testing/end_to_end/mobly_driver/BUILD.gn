# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/python/python_binary.gni")
import("//build/python/python_library.gni")
import("//build/testing/host_test_data.gni")

if (is_host) {
  python_library("api_infra") {
    sources = [ "api_infra.py" ]
  }

  python_library("api_mobly") {
    sources = [ "api_mobly.py" ]
    library_deps = [
      ":api_infra",
      "//third_party/mobly",
    ]
  }

  python_library("mobly_driver_lib") {
    sources = [ "mobly_driver_lib.py" ]
    library_deps = [ "drivers:base_mobly_driver" ]
  }

  python_library("driver_factory") {
    sources = [ "driver_factory.py" ]
    library_deps = [
      ":api_infra",
      "drivers:common",
      "drivers:infra_driver",
      "drivers:local_driver",
    ]
  }

  # Mobly Driver Python binary definition.
  _md_bin_target = "mobly_driver_binary_target"
  _md_bin_name = "mobly_driver.pyz"
  python_binary(_md_bin_target) {
    testonly = true
    main_source = "main.py"
    sources = [
      "api_infra.py",
      "api_mobly.py",
      "driver_factory.py",
      "drivers/base_mobly_driver.py",
      "drivers/common.py",
      "drivers/infra_driver.py",
      "drivers/local_driver.py",
      "main.py",
      "mobly_driver_lib.py",
    ]
    main_callable = "main"
    output_name = _md_bin_name
    deps = [
      ":driver_factory",
      ":mobly_driver_lib",
    ]
  }

  # Mobly Driver test data definition.
  # This enables the Python binary to be used as a host test data dependency..
  host_test_data("mobly_driver_test_data_target") {
    testonly = true
    sources = [ "${target_out_dir}/${_md_bin_name}" ]
    outputs = [ "${target_out_dir}/test_data/${_md_bin_name}" ]
    deps = [ ":${_md_bin_target}" ]
  }
}

group("tests") {
  testonly = true
  public_deps = [
    "tests/functional_tests:tests",
    "tests/unit_tests:tests",
  ]
}
