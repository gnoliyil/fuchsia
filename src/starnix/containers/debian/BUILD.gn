# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/storage/ext4/ext4_to_pkg.gni")

group("debian") {
  deps = [ ":debian_package" ]
}

ext4_to_pkg("system_image") {
  if (target_cpu == "x64") {
    input = "//prebuilt/starnix/linux-amd64/amd64_system.img"
  } else if (target_cpu == "arm64") {
    input = "//prebuilt/starnix/linux-arm64/arm64_system.img"
  } else if (target_cpu == "riscv64") {
    # Define a nonexistent but valid value to allow GN to complete.
    # TODO(fxbug.dev/128554): Add a RISC-V image.
    input = "//see_fxbug_dev_128554"
  } else {
    assert(false, "Unsupported CPU architecture.")
  }
  prefix = "data/system"
}

fuchsia_package_with_single_component("debian_package") {
  manifest = "meta/debian_container.cml"
  package_name = "debian"
  component_name = "debian_container"

  deps = [
    ":system_image",
    "//src/starnix/containers:data_tmp_target",
    "//src/starnix/containers:default_init",
  ]
}
