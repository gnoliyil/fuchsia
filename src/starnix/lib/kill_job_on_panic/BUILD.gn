# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")

rustc_library("kill_job_on_panic") {
  edition = "2021"
  sources = [ "src/lib.rs" ]
  deps = [
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:tracing",
  ]
  configs += [ "//src/starnix/config:starnix_clippy_lints" ]
}

rustc_test("kill_job_on_panic_integration_bin") {
  testonly = true
  edition = "2021"
  source_root = "tests/integration.rs"
  sources = [ source_root ]
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//src/lib/diagnostics/reader",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:futures",
  ]
}

rustc_binary("kill_parent_job_panicker") {
  testonly = true
  edition = "2021"
  source_root = "tests/panicker.rs"
  sources = [ source_root ]
  deps = [
    ":kill_job_on_panic",
    "//src/lib/fuchsia",
  ]
}

rustc_binary("launch_panicker_and_sleep_forever") {
  testonly = true
  edition = "2021"
  source_root = "tests/launch_panicker_and_sleep_forever.rs"
  sources = [ source_root ]
}

fuchsia_component("panicking_child") {
  testonly = true
  manifest = "meta/panicking_child.cml"
  deps = [
    ":kill_parent_job_panicker",
    ":launch_panicker_and_sleep_forever",
  ]
}

fuchsia_test_component("kill_job_on_panic_integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":kill_job_on_panic_integration_bin" ]
}

fuchsia_test_package("kill_job_on_panic_tests") {
  test_components = [ ":kill_job_on_panic_integration_test" ]
  deps = [ ":panicking_child" ]
  test_specs = {
    log_settings = {
      # We generate an ERROR-level log when killing the job.
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":kill_job_on_panic_tests" ]
}
