# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/host_test_data.gni")

group("gen_android_starnix_container") {
  deps = [ ":host" ]
}

install_host_tools("host") {
  deps = [ ":gen_android_starnix_container_bin" ]
  outputs = [ "gen-android-starnix-container" ]
}

if (is_host) {
  host_test_data("test_ext4_image") {
    sources = [ "//src/storage/ext4/tests/test.img" ]
    outputs =
        [ "$root_out_dir/test_data/gen-android-starnix-container/test.img" ]
    visibility = [ ":*" ]
  }

  rustc_binary("gen_android_starnix_container_bin") {
    name = "gen-android-starnix-container"
    edition = "2021"
    with_unit_tests = true
    deps = [
      "//src/lib/fuchsia-url",
      "//src/storage/ext4/lib/ext4_extract",
      "//src/sys/pkg/lib/fuchsia-pkg",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:camino",
    ]

    test_deps = [
      ":test_ext4_image",
      "//third_party/rust_crates:tempfile",
    ]

    sources = [ "src/main.rs" ]
  }

  group("tests") {
    testonly = true
    deps = [ ":gen_android_starnix_container_bin_test" ]
  }
}
