# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/rust/rustc_binary.gni")

group("create_container_package") {
  deps = [ ":host" ]
}

install_host_tools("host") {
  deps = [ ":create_container_package_bin" ]
  outputs = [ "create_container_package" ]
}

if (is_host) {
  _default_init_arm64_target =
      "//src/starnix/containers/default_init(//build/toolchain:linux_arm64)"
  _default_init_arm64_path =
      get_label_info(_default_init_arm64_target, "root_out_dir") + "/" +
      get_label_info(_default_init_arm64_target, "name")

  _default_init_x64_target =
      "//src/starnix/containers/default_init(//build/toolchain:linux_x64)"
  _default_init_x64_path =
      get_label_info(_default_init_x64_target, "root_out_dir") + "/" +
      get_label_info(_default_init_x64_target, "name")

  rustc_binary("create_container_package_bin") {
    name = "create_container_package"
    edition = "2021"

    deps = [
      "//src/lib/fidl/rust/fidl",
      "//src/lib/fuchsia-async",
      "//src/storage/ext4/lib/metadata",
      "//src/sys/pkg/lib/fuchsia-pkg",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:argh",
      "//third_party/rust_crates:itertools",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:tar",
      "//third_party/rust_crates:tempfile",
      "//tools/lib/cml",
      _default_init_arm64_target,
      _default_init_x64_target,
    ]

    sources = [
      "src/args.rs",
      "src/component_manifest.rs",
      "src/default_init.rs",
      "src/docker_archive.rs",
      "src/layered_image.rs",
      "src/main.rs",
    ]

    inputs = [
      _default_init_arm64_path,
      _default_init_x64_path,
    ]

    # The code being compiled uses these paths to embed the corresponding generated files at compile
    # time, which makes the resulting files less reusable for caching because they depend on the
    # path of the output directory.
    configs += [ "//build/config/rust:output_dir_sensitive" ]
    rustenv = [
      "DEFAULT_INIT_ARM64_PATH=" + rebase_path(_default_init_arm64_path, "src"),
      "DEFAULT_INIT_X64_PATH=" + rebase_path(_default_init_x64_path, "src"),
    ]
  }
}

group("tests") {
  testonly = true
  deps = [
    # NOTE: create_container_package does not have any test yet. By listing it here, we can at least
    # verify that it compiles in CQ.
    ":create_container_package_bin($host_toolchain)",
  ]
}
