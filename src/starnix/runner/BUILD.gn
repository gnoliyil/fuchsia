# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/starnix/kernel/starnix.gni")
import("//src/sys/core/build/core_shard.gni")

group("runner") {
  deps = [
    ":starnix_package",
    "//src/starnix/kernel:starnix_kernel_package",
  ]
}

rustc_binary("starnix_runner_bin") {
  name = "starnix_runner"
  edition = "2021"
  with_unit_tests = true

  source_root = "main.rs"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.runner:fuchsia.component.runner_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/starnix/lib/kernel_manager",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "main.rs" ]
}

core_shard("starnix_runner_core_shard") {
  shard_file = "meta/starnix_runner.core_shard.cml"
}

fuchsia_package_with_single_component("starnix_package") {
  manifest = "meta/starnix_runner.cml"
  package_name = "starnix"
  component_name = "starnix_runner"

  deps = [ ":starnix_runner_bin" ]
}
