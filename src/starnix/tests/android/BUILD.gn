# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/python/python_host_test.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_component_with_expectations.gni")
import("//src/starnix/kernel/starnix.gni")

group("tests") {
  testonly = true
  if (target_cpu == "x64" && !is_asan) {
    deps = [ ":android" ]
  }
}

fuchsia_test_component_with_expectations("adbd_test") {
  manifest = "meta/adbd_test.cml"
  expectations = "expectations/adbd_test.json5"
  deps = [
    "//src/connectivity/network/netstack:component",
    "//src/starnix/tests:starnix_test_with_expectations",
  ]
  test_type = "starnix"
}

fuchsia_test_component_with_expectations("libutils_test") {
  manifest = "meta/libutils_test.cml"
  expectations = "expectations/libutils_test.json5"
  deps = [ "//src/starnix/tests:starnix_test_with_expectations" ]
  test_type = "starnix"
}

fuchsia_test_component("sh_test") {
  check_references = false
  manifest = "meta/sh_test.cml"
  deps = [ "//src/starnix/tests:starnix_test" ]
  test_type = "starnix"
}

fuchsia_test_component("boringssl_self_test64") {
  check_references = false
  manifest = "meta/boringssl_self_test64.cml"
  test_type = "starnix"
}

group("container_resources") {
  deps = [ "//src/starnix/containers:default_init" ]

  if (target_cpu == "x64") {
    deps += [
      "//src/starnix/containers:data_tmp_target",
      "//src/starnix/containers/bionic:system_image",
      "//src/starnix/containers/bionic:testcases_image",
    ]
  }
}

fuchsia_test_package("android") {
  test_components = [
    ":adbd_test",
    ":boringssl_self_test64",
    ":sh_test",
    ":libutils_test",
  ]

  if (target_cpu == "x64") {
    subpackages = [ "//src/starnix/kernel:starnix_kernel_package" ]
  }

  deps = [
    ":container_resources",
    "//src/lib/testing/expectation:expectation_comparer",
  ]
}
