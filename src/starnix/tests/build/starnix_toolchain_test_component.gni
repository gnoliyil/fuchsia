# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import(
    "//src/lib/testing/expectation/fuchsia_test_component_with_expectations.gni")
import("//src/starnix/kernel/starnix.gni")

# Defines a Fuchsia component that contains a test binary built with a specific toolchain.
#
# This template expects to find a CML file named "meta/${test_name}.cml" where
# "test_name" is the name of this target. This file is used as the component
# manifest for the test.
#
# Parameters
#
#   test_label (required)
#     The label of the test.
#     Type: GN label string
#   test_toolchain (optional, defaults to target_linux_toolchain)
#      The toolchain to use to build the test, e.g. //build/toolchain:linux_x64.
#     parameter.
#     Type: GN label string
#   test_binary (optional)
#     The name of the binary produced by test_label. Defaults to target_name.
#     Type: string
#   uses_expectations (optional)
#     Set to true if this test has an expectations file. "expectations/${target_name}.json5" is
#     used by default, unless `generated_expectations` is also set.
#     Type: bool
#   generated_expectations (optional)
#     A `generated_expectations_file` target that generates expectations for this test.
#     Type: GN label string
#
#   The remaining parameters are forwarded to the fuchsia_component template.
template("starnix_toolchain_test_component") {
  assert(defined(invoker.test_label))

  if (defined(invoker.test_toolchain)) {
    test_toolchain = invoker.test_toolchain
  } else {
    test_toolchain = target_linux_toolchain
  }

  if (defined(invoker.test_binary)) {
    test_binary = invoker.test_binary
  } else {
    test_binary = target_name
  }
  test_label = "${invoker.test_label}($test_toolchain)"

  resource("${target_name}_resource") {
    testonly = true
    out_dir = get_label_info(test_label, "root_out_dir")
    sources = [ "${out_dir}/${test_binary}" ]
    outputs = [ "data/tests/${test_binary}" ]
    deps = [ test_label ]
  }

  if (defined(invoker.uses_expectations) && invoker.uses_expectations) {
    target_type = "fuchsia_test_component_with_expectations"
  } else {
    target_type = "fuchsia_test_component"
  }

  target(target_type, target_name) {
    forward_variables_from(invoker, "*", [ "test_label" ])

    testonly = true
    check_references = false
    if (!defined(invoker.manifest)) {
      manifest = "meta/${target_name}.cml"
    }

    if (!defined(deps)) {
      deps = []
    }
    deps += [ ":${target_name}_resource" ]
    if (defined(invoker.uses_expectations) && invoker.uses_expectations) {
      if (defined(invoker.generated_expectations)) {
        generated_expectations = invoker.generated_expectations
      } else {
        expectations = "expectations/${target_name}.json5"
      }
      deps += [ "//src/starnix/tests:starnix_test_with_expectations" ]
    } else {
      deps += [ "//src/starnix/tests:starnix_test" ]
    }
  }
}
