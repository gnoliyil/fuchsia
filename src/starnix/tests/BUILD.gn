# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")
import("//src/starnix/tests/build/starnix_host_test_component.gni")
import("//src/starnix/tests/build/starnix_linux_test_component.gni")
import("//tools/cmc/build/expect_includes.gni")

group("tests") {
  testonly = true

  deps = [
    "android:tests",
    "gvisor:tests",
    "syscalls:tests",
  ]

  if (target_cpu == "x64") {
    deps += [ ":test_chromiumos_distro" ]

    # TODO(https://fxbug.dev/116773): Remove !is_asan check when Starnix can run binaries built for
    # ASAN.
    if (!is_asan) {
      deps += [ ":test_starnix_magma" ]
    }
  }
}

# Tests that use starnix_test_runner should depend on one of these.
expect_includes("starnix_test") {
  includes = [ "starnix_test.shard.cml" ]
}

expect_includes("starnix_test_with_expectations") {
  includes = [ "starnix_test_with_expectations.shard.cml" ]
}

group("container_resources") {
  deps = [ "//src/starnix/containers:default_init" ]

  if (target_cpu == "x64") {
    deps += [
      "//src/starnix/containers:data_tmp_target",
      "//src/starnix/containers/chromiumos:system_image",
    ]
  }
}

fuchsia_component("sh_test") {
  testonly = true
  check_references = false
  manifest = "meta/sh_test.cml"
}

if (host_os == "linux" && host_cpu == "x64") {
  starnix_host_test_component("fdio-test") {
    test_label = "//sdk/lib/fdio/tests:fdio-test"
  }
}

fuchsia_test_package("test_chromiumos_distro") {
  test_components = [ ":sh_test" ]

  # TODO(https://fxbug.dev/116773): Remove !is_asan check when Starnix can run binaries built for
  # ASAN.
  if (host_os == "linux" && host_cpu == "x64" && !is_asan) {
    test_components += [ ":fdio-test" ]
  }

  if (target_cpu == "x64") {
    subpackages = [ "//src/starnix/kernel:starnix_kernel_package" ]
  }
  deps = [ ":container_resources" ]
}

if (target_cpu == "x64") {
  starnix_linux_test_component("virtmagma_conformance_tests") {
    test_label =
        "//src/graphics/lib/magma/tests/integration:virtmagma_conformance_tests"
  }

  starnix_linux_test_component("virtmagma_image_tests") {
    test_label =
        "//src/graphics/lib/magma/tests_linux/unit_tests:virtmagma_image_tests"
  }

  fuchsia_test_package("test_starnix_magma") {
    test_components = [
      ":virtmagma_conformance_tests",
      # TODO(fxbug.dev/104611) - enable this when all tests passing
      #":virtmagma_image_tests",
    ]

    deps = [ ":container_resources" ]

    subpackages = [ "//src/starnix/kernel:starnix_kernel_package" ]

    test_specs = {
      log_settings = {
        max_severity = "ERROR"
      }
      environments = [
        atlas_env,
        nuc_env,
      ]
    }
  }
}
