# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//src/starnix/kernel/starnix.gni")
import("//src/starnix/tests/build/starnix_linux_test_executable.gni")

group("tests") {
  testonly = true
  deps = []

  # TODO(https://fxbug.dev/137012): Re-enable on asan builders.
  if (!is_asan) {
    deps += [ ":starnix_profile_provider_integration_test" ]
  }
}

rustc_binary("profile_provider_integration_test_bin") {
  testonly = true
  edition = "2021"
  sources = [ "src/main.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.scheduler:fuchsia.scheduler_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json5",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_test_component("profile_provider_test") {
  manifest = "meta/integration_test.cml"
  deps = [
    ":linux_scheduler_puppet",
    ":profile_provider_integration_test_bin",
    "//src/starnix/config:starnix.profiles",
  ]
  test_type = "starnix"
}

fuchsia_component("container") {
  testonly = true
  manifest = "//src/starnix/containers/debian/meta/debian_container.cml"
}

starnix_linux_test_executable("linux_scheduler_puppet") {
  sources = [ "src/puppet.cc" ]
}

fuchsia_component("puppet") {
  testonly = true
  manifest = "meta/puppet.cml"
  deps = [ ":linux_scheduler_puppet" ]
}

fuchsia_component("test_realm") {
  testonly = true
  manifest = "meta/test_realm.cml"
}

fuchsia_test_package("starnix_profile_provider_integration_test") {
  test_components = [ ":profile_provider_test" ]
  deps = [
    ":container",
    ":puppet",
    ":test_realm",
    "//src/starnix/containers/debian:container_resources",
  ]
  subpackages = [ "//src/starnix/kernel:starnix_kernel_package" ]
}
