# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/host_test.gni")
import("//build/testing/host_test_data.gni")
import("//src/lib/testing/expectation/generated_expectations_file.gni")
import("//src/starnix/kernel/starnix.gni")
import("//src/starnix/tests/build/starnix_linux_test_component.gni")
import("//src/starnix/tests/starnix_test_subpackages.gni")

group("tests") {
  testonly = true
  deps = [ ":starnix_syscalls_cpp_tests" ]
  if (host_os == "linux" && host_cpu == "x64" && has_board) {
    deps += [
      # These are Linux tests, so we can run them as host tests on our host Linux bot.
      ":syscalls_cpp_host_tests($host_toolchain)",
    ]
  }
}

if (is_host && has_board) {
  host_test("syscalls_cpp_host_tests") {
    binary_path =
        get_label_info(":syscalls_cpp_test_bin($target_linux_toolchain)",
                       "root_out_dir") + "/syscalls_cpp_test_bin"
    deps = [
      ":host_runtime_deps",
      ":syscalls_cpp_test_bin($target_linux_toolchain)",
    ]
  }

  host_test_data("host_runtime_deps") {
    sources = [
      get_label_info(
              ":extended_pstate_initial_state_child($target_linux_toolchain)",
              "root_out_dir") + "/extended_pstate_initial_state_child",
      get_label_info(":syscall_test_exec_child($target_linux_toolchain)",
                     "root_out_dir") + "/syscall_test_exec_child",
    ]
    deps = [
      ":extended_pstate_initial_state_child($target_linux_toolchain)",
      ":syscall_test_exec_child($target_linux_toolchain)",
    ]
  }
}

source_set("test_utils") {
  testonly = true

  sources = [
    "proc_test_base.h",
    "syscall_matchers.h",
    "task_test.h",
    "test_helper.cc",
    "test_helper.h",
  ]
  public_deps = [
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
  ]
  deps = [ "//src/lib/fxl" ]
}

if (target_cpu == "x64" || target_cpu == "arm64") {
  resource("fuse_overlayfs") {
    sources = []
    if (target_cpu == "x64") {
      sources += [ "//prebuilt/starnix/fuse-overlayfs-amd64/fuse-overlayfs" ]
    }
    if (target_cpu == "arm64") {
      sources += [ "//prebuilt/starnix/fuse-overlayfs-arm64/fuse-overlayfs" ]
    }
    outputs = [ "data/fuse-overlayfs" ]
  }
} else {
  group("fuse_overlayfs") {
  }
}

resource("simple_ext4_image") {
  sources = [
    "data/hello_world.txt",
    "data/simple_ext4.img",
  ]
  outputs = [ "data/{{source_file_part}}" ]
}

test("syscalls_cpp_test_bin") {
  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
  sources = [
    "abort_test.cc",
    "ashmem_test.cc",
    "bpf_test.cc",
    "custom_artifact_test.cc",
    "epoll_test.cc",
    "extended_pstate_test.cc",
    "fcntl_test.cc",
    "fs_test.cc",
    "fsverity_test.cc",
    "fuse_test.cc",
    "futex_test.cc",
    "input_test.cc",
    "ioctl_test.cc",
    "loop_test.cc",
    "memfd_test.cc",
    "mmap_test.cc",
    "mount_test.cc",
    "overlayfs_test.cc",
    "pidfd_test.cc",
    "pipe_test.cc",
    "poll_test.cc",
    "prctl_test.cc",
    "proc_test.cc",
    "ptrace_test.cc",
    "read_write_test.cc",
    "restart_read_test.cc",
    "seccomp_test.cc",
    "sendfile_test.cc",
    "signal_handling_test.cc",
    "socket_test.cc",
    "sw_breakpoint_test.cc",
    "sysfs_kernel_test.cc",
    "sysfs_power_test.cc",
    "syslog_test.cc",
    "task_test.cc",
    "terminal_test.cc",
    "test_helper_test.cc",
    "time_test.cc",
    "timerfd_test.cc",
    "trivial_test.cc",
    "udev_test.cc",
    "uio_test.cc",
    "vdso_test.cc",
    "waitpid_exit_signal_test.cc",
    "waitpid_flags_test.cc",
  ]
  if (target_cpu == "x64") {
    # TODO(fxbug.dev/108588): arm64 emulator does not have Vulkan support yet.
    # This test requires test-ui-stack.
    sources += [ "uinput_test.cc" ]
  }
  deps = [
    ":test_utils",
    "//sdk/fidl/fuchsia.fs:fuchsia.fs_cpp",
    "//sdk/lib/fit",
    "//sdk/lib/stdcompat",
    "//src/lib/files",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
  ]
  include_dirs = [
    "//third_party/android/platform/bionic/libc/kernel/uapi",
    "//third_party/android/platform/bionic/libc/kernel/android/uapi",
  ]
  if (target_cpu == "x64") {
    include_dirs +=
        [ "//third_party/android/platform/bionic/libc/kernel/uapi/asm-x86" ]
  } else if (target_cpu == "arm64") {
    include_dirs +=
        [ "//third_party/android/platform/bionic/libc/kernel/uapi/asm-arm64" ]
  }

  # TODO(https://fxbug.dev/135694): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]
}

test("crash") {
  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
  sources = [ "crash.cc" ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

test("syslog_non_root") {
  # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
  exclude_toolchain_tags = [ "asan" ]
  sources = [ "syslog_non_root_test.cc" ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

if (is_linux) {
  executable("extended_pstate_initial_state_child") {
    # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
    exclude_toolchain_tags = [ "instrumented" ]
    sources = [ "extended_pstate_initial_state_child.cc" ]

    # This binary does not use libc or a dynamic linker since it is testing the state
    # presented to _start.
    disable_syslog_backend = true
    cflags = [ "-fno-builtin" ]
    ldflags = [
      "-nostdlib",
      "-nostartfiles",
      "-static",
    ]

    # On RISC-V the linker uses base address that's below the base allowed by
    # Zircon (0x200000). See b/311221240. Specify the base address to
    # workaround this issue.
    if (current_cpu == "riscv64") {
      ldflags += [ "-Wl,--image-base=0x200000" ]
    }

    configs -= [
      "//build/config/linux:default-pie",
      "//build/config/linux:implicit-host-libs",
    ]
  }

  executable("syscall_test_exec_child") {
    testonly = true
    sources = [ "syscall_test_exec_child.cc" ]

    # TODO(https://fxbug.dev/297293167) enable ASan instrumentation for Linux binaries in Starnix
    exclude_toolchain_tags = [
      "asan",
      "coverage",
    ]
    deps = [ ":test_utils" ]
  }

  resource("syscall_test_exec_child_resource") {
    testonly = true
    sources = [ "$root_out_dir/syscall_test_exec_child" ]
    outputs = [ "data/tests/syscall_test_exec_child" ]
    deps = [ ":syscall_test_exec_child" ]
  }
}

resource("extended_pstate_initial_state_child_resource") {
  binary_name = "extended_pstate_initial_state_child"
  exe_label = ":${binary_name}($target_linux_toolchain)"
  out_dir = get_label_info(exe_label, "root_out_dir")
  sources = [ "${out_dir}/${binary_name}" ]
  outputs = [ "data/tests/${binary_name}" ]
  deps = [ exe_label ]
}

generated_expectations_file("syscalls_cpp_test_expectations") {
  includes = [
    "expectations/syscalls_cpp_test.json5",
    "expectations/syscalls_cpp_test_${current_cpu}.json5",
  ]
}

starnix_linux_test_component("syscalls_cpp_test") {
  test_label = ":syscalls_cpp_test_bin"
  test_binary = "syscalls_cpp_test_bin"
  deps = [
    ":fuse_overlayfs",
    ":simple_ext4_image",
    ":syscall_test_exec_child_resource($target_linux_toolchain)",
  ]
  uses_expectations = true
  generated_expectations = ":syscalls_cpp_test_expectations"
  test_type = "starnix"
}

starnix_linux_test_component("crash_test") {
  test_label = ":crash"
  test_binary = "crash"
  test_type = "starnix"
}

starnix_linux_test_component("syslog_non_root_test") {
  test_label = ":syslog_non_root"
  test_binary = "syslog_non_root"
  test_type = "starnix"
}

fuchsia_test_package("starnix_syscalls_cpp_tests") {
  test_components = [
    ":syscalls_cpp_test",
    ":syslog_non_root_test",
    ":crash_test",
  ]

  subpackages = starnix_test_subpackages
  subpackages += [ "//src/starnix/containers/debian:debian_package" ]

  if (target_cpu == "x64") {
    # TODO(fxbug.dev/108588): arm64 emulator does not have Vulkan support yet.
    # Without Vulkan, test-ui-stack will stuck on Scenic startup.
    subpackages += [ "//src/ui/testing/test_ui_stack:test-ui-stack" ]
  }

  deps = [
    ":extended_pstate_initial_state_child_resource",
    "//src/lib/testing/expectation:expectation_comparer",
  ]
}
