# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//build/testing/bootfs_test.gni")
import("//build/zircon/zx_library.gni")

zx_library("crashsvc") {
  sdk = "source"

  sdk_headers = [
    "crashsvc/crashsvc.h",
    "crashsvc/exception_handler.h",
    "crashsvc/logging.h",
  ]

  sources = [
    "crashsvc.cc",
    "exception_handler.cc",
    "logging.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.exception:fuchsia.exception_cpp_wire",
    "//sdk/lib/component/incoming/cpp",
    "//src/lib/fsl",
    "//zircon/system/ulib/async",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/backtrace-request:backtrace-request-utils",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspector",
  ]

  public_deps = [ "//zircon/system/ulib/zx" ]

  configs += [ "//build/config:all_source" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "svchost"
  manifest = "meta/svchost.cml"
}

fuchsia_structured_config_cpp_elf_lib("svchost_config") {
  cm_label = ":manifest"
}

executable("svchost") {
  sources = [ "svchost.cc" ]
  deps = [
    ":crashsvc",
    ":svchost_config",
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_cpp_wire",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp_wire",
    "//sdk/fidl/fuchsia.mem:fuchsia.mem_cpp_wire",
    "//sdk/lib/component/incoming/cpp",
    "//src/sys/lib/stdout-to-debuglog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/kcounter",
    "//zircon/system/ulib/kernel-debug",
    "//zircon/system/ulib/ktrace",
    "//zircon/system/ulib/profile",
    "//zircon/system/ulib/svc",
    "//zircon/system/ulib/zx",
  ]
  output_dir = "$root_out_dir/zircon-migrated"
}

fuchsia_component("component") {
  cm_label = ":manifest"
  deps = [ ":svchost" ]
}

fuchsia_package("pkg") {
  package_name = "svchost"
  deps = [ ":component" ]
}

test("crashsvc-test") {
  output_name = "crashsvc-test"
  sources = [ "crashsvc-test.cc" ]
  deps = [
    ":crashsvc",
    "//sdk/fidl/fuchsia.exception:fuchsia.exception_cpp_wire",
    "//sdk/lib/async-loop-testing/cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/mini-process",
    "//zircon/system/ulib/zxtest",
  ]
}

bootfs_test("crashsvc-test-bootfs-test") {
  name = "crashsvc-test"
  deps = [ ":crashsvc-test" ]
}
