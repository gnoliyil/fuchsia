# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("stdout") {
  testonly = true
  deps = [ ":elf_runner_stdout_test" ]
}

rustc_test("test") {
  edition = "2021"
  name = "elf_runner_stdout"

  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/diagnostics/reader/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:test-case",
  ]

  sources = [ "src/lib.rs" ]
}

fuchsia_component("stdout_integration_test") {
  testonly = true
  manifest = "meta/stdout.cml"
  deps = [ ":test" ]
}

fuchsia_test_package("elf_runner_stdout_test") {
  test_components = [ ":stdout_integration_test" ]

  deps = [
    "cpp",
    "rust",
  ]

  # The Go toolchain does not support RISC-V.
  if (current_cpu != "riscv64") {
    deps += [ "go" ]
  }
}
