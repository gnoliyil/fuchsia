# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

test("bin") {
  testonly = true
  output_name = "sc_capabilities_offer_and_expose"
  sources = [ "test.cc" ]
  deps = [
    ":config_lib",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_component_manifest("manifest") {
  testonly = true
  component_name = "sc_capabilities_offer_and_expose"
  manifest = "meta/test.cml"
}

fuchsia_component("sc_capabilities_offer_and_expose") {
  testonly = true
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_structured_config_cpp_elf_lib("config_lib") {
  testonly = true
  namespace = "config"
  cm_label = ":manifest"
}

fuchsia_structured_config_values("values") {
  testonly = true
  cm_label = ":manifest"
  component_name = "sc_capabilities_offer_and_expose"
  values = {
    my_flag = false
  }
}

fuchsia_component("child") {
  testonly = true
  manifest = "meta/child.cml"
  restricted_features = [ "config_capabilities" ]
}

fuchsia_component("parent") {
  testonly = true
  manifest = "meta/parent.cml"
}

fuchsia_test_package("tests") {
  package_name = "sc_capabilites_offer_and_expose"
  test_components = [ ":parent" ]
  deps = [
    ":child",
    ":sc_capabilities_offer_and_expose",
    ":values",
  ]
}
