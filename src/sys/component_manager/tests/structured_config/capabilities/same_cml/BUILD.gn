# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

test("bin") {
  testonly = true
  output_name = "sc_capabilities_same_cml"
  sources = [ "test.cc" ]
  deps = [
    ":config_lib",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_component_manifest("manifest") {
  testonly = true
  component_name = "sc_capabilities_same_cml"
  manifest = "meta/test.cml"
  restricted_features = [ "config_capabilities" ]
}

fuchsia_component("sc_capabilities_same_cml") {
  testonly = true
  cm_label = ":manifest"
  deps = [ ":bin" ]
}

fuchsia_structured_config_cpp_elf_lib("config_lib") {
  testonly = true
  namespace = "config"
  cm_label = ":manifest"
}

fuchsia_structured_config_values("values") {
  testonly = true
  cm_label = ":manifest"
  component_name = "sc_capabilities_same_cml"
  values = {
    my_flag = false
    my_uint8 = 0
    my_uint16 = 0
    my_uint32 = 0
    my_uint64 = 0
    my_int8 = 0
    my_int16 = 0
    my_int32 = 0
    my_int64 = 0
    my_string = "default"
    my_uint8_vector = [ 0 ]
    my_string_vector = [ "default" ]
  }
}

fuchsia_test_package("tests") {
  package_name = "sc_capabilites_same_cml"
  test_components = [ ":sc_capabilities_same_cml" ]
  deps = [ ":values" ]
}
