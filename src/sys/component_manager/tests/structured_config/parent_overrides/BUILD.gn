# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")

fidl("test.config.parentoverrides") {
  testonly = true
  sources = [ "fidl/config_reporter.fidl" ]
}

rustc_binary("config_receiver_bin") {
  testonly = true
  edition = "2021"
  source_root = "config_receiver.rs"
  sources = [ "config_receiver.rs" ]
  deps = [
    ":received_config",
    ":test.config.parentoverrides_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:futures",
  ]
}

fuchsia_component_manifest("config_receiver_manifest") {
  component_name = "config_receiver"
  manifest = "meta/config_receiver.cml"
}

fuchsia_structured_config_rust_lib("received_config") {
  testonly = true
  cm_label = ":config_receiver_manifest"
}

fuchsia_component("config_receiver") {
  testonly = true
  cm_label = ":config_receiver_manifest"
  deps = [ ":config_receiver_bin" ]
}

fuchsia_structured_config_values("receiver_defaults") {
  testonly = true
  cm_label = ":config_receiver_manifest"
  values = {
    parent_provided = "default value"
    not_parent_provided = "default value which should not change"
  }
}

rustc_test("parent_override_integration_test_bin") {
  edition = "2021"
  source_root = "integration_test.rs"
  sources = [ "integration_test.rs" ]
  deps = [
    ":test.config.parentoverrides_rust",
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:assert_matches",
  ]
}

fuchsia_test_component("integration_test") {
  manifest = "meta/integration_test.cml"
  deps = [ ":parent_override_integration_test_bin" ]
}

fuchsia_test_package("parent_overrides_integration_test") {
  test_components = [ ":integration_test" ]
  deps = [
    ":config_receiver",
    ":receiver_defaults",
  ]
}

group("parent_overrides") {
  testonly = true
  deps = [ ":parent_overrides_integration_test" ]
}
