# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("test_manager_test_bin") {
  edition = "2021"
  name = "test_manager_test"
  source_root = "src/tests.rs"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/sys/test_manager:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [ "src/tests.rs" ]

  inputs = [
    "//src/sys/test_runners/gtest/test_data/sample_tests_golden_events.rsf",
  ]
}

rustc_test("test_manager_stress_test_bin") {
  edition = "2021"
  name = "test_manager_stress_test"
  source_root = "src/stress_tests.rs"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/sys/test_manager:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [ "src/stress_tests.rs" ]
}

rustc_test("test_manager_legacy_test_bin") {
  edition = "2021"
  name = "test_manager_legacy_test"
  source_root = "src/legacy_tests.rs"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/sys/test_manager:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [ "src/legacy_tests.rs" ]

  inputs = [
    "//src/sys/test_runners/gtest/test_data/sample_tests_golden_events.rsf",
  ]
}

fuchsia_unittest_package("test_manager_test") {
  manifest = "meta/test_manager_test.cml"
  deps = [
    ":test_manager_test_bin",
    "test_data",
    "//src/sys/test_manager:test_manager_cmp",
  ]

  restricted_features = [ "allow_non_hermetic_packages" ]

  # There's expected error logs that happen during the test such as not resolving an instance when
  # testing that we fail to execute a test with an unknown URL.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("test_manager_stress_test") {
  restricted_features = [ "allow_non_hermetic_packages" ]
  manifest = "meta/test_manager_stress_test.cml"
  deps = [
    ":test_manager_stress_test_bin",
    "test_data:debug_data_spam_test",
    "//src/sys/test_manager:test_manager_cmp",
  ]
}

fuchsia_unittest_package("test_manager_legacy_test") {
  restricted_features = [ "allow_non_hermetic_packages" ]
  manifest = "meta/test_manager_legacy_test.cml"
  deps = [
    ":test_manager_legacy_test_bin",
    "test_data:test_data_v1",
    "//src/sys/test_manager:test_manager_cmp",
  ]

  # There's expected error logs that happen during the test such as not resolving an instance when
  # testing that we fail to execute a test with an unknown URL.
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("legacy_tests") {
  testonly = true
  deps = [
    ":test_manager_legacy_test",

    # although these tests don't use test_manager itself, this group includes test_manager's
    # dependencies (such as test runners).
    "//src/sys/test_manager",
    "//src/sys/test_runners/gtest/test_data:gtest-runner-example-tests",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":test_manager_stress_test",
    ":test_manager_test",
    "chromium_tests:tests",
    "diagnostics_tests:tests",
    "nonhermetic_packaged_tests:tests",
    "test_data:debug-data-test",

    # although these tests don't use test_manager itself, this group includes test_manager's
    # dependencies (such as test runners).
    #
    # TODO(http://fxbug.dev/112006) Remove when added to platform assembly input
    # bundles.
    "//src/sys/test_manager",

    # test_manager_test launches these tests.
    # TODO(fxbug.dev/104764): consider reorganizing these tests so that these dependencies
    # are not necessary.
    "//examples/components/routing/integration_tests:tests",
    "//src/sys/test_runners/gtest/test_data:gtest-runner-example-tests",
  ]
}
