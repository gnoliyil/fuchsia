# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  testonly = true
  name = "test_realm_proxy"
  visibility = [ ":*" ]
  edition = "2021"
  sources = [ "src/main.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/pkg/lib/fuchsia-pkg-testing",
    "//src/sys/pkg/lib/package-directory",
    "//src/sys/pkg/testing/blobfs-ramdisk",
    "//src/testing/realm_proxy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component("test_realm_proxy_component") {
  testonly = true
  visibility = [ ":*" ]
  component_name = "default"
  manifest = "meta/default.cml"
  deps = [
    ":bin",
    "//src/sys/pkg/bin/pm:pm_bin",
  ]
}

fuchsia_package("test_realm_proxy") {
  testonly = true
  deps = [ ":test_realm_proxy_component" ]
}
