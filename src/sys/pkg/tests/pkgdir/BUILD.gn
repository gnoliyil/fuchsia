# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//sdk/ctf/build/ctf.gni")

ctf_rustc_test("driver") {
  name = "pkgdir_integration_test"
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [
    "src/directory.rs",
    "src/file.rs",
    "src/lib.rs",
    "src/node.rs",
  ]
}

ctf_test_component("default") {
  testonly = true
  manifest = "meta/pkgdir-integration-test.cml"
  deps = [ ":driver" ]
}

ctf_fuchsia_package("pkgdir-tests") {
  package_name = "pkgdir-tests"
  testonly = true
  deps = [ ":default" ]
}

fuchsia_component("test-root") {
  testonly = true
  manifest = "meta/test-root.cml"
  deps = [ "//src/storage/testing:storage_driver_test_realm" ]
}

fuchsia_test_package("pkgdir-integration-tests") {
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
  test_components = [ ":test-root" ]
  subpackages = [
    "test_realm_proxy",
    ":pkgdir-tests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":pkgdir-integration-tests" ]
}

group("ctf-tests") {
  testonly = true
  deps = [ ":pkgdir-tests_archive" ]
}
