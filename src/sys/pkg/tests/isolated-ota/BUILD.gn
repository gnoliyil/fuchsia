# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("driver") {
  name = "isolated-ota-integration-test"
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.paver:fuchsia.paver_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-url",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/fidl-fuchsia-pkg-ext",
    "//src/sys/pkg/fidl/fuchsia.update.installer:fuchsia.update.installer_rust",
    "//src/sys/pkg/lib/fuchsia-pkg-testing",
    "//src/sys/pkg/lib/isolated-ota",
    "//src/sys/pkg/lib/isolated-swd",
    "//src/sys/pkg/testing/blobfs-ramdisk",
    "//src/sys/pkg/testing/isolated-ota-env",
    "//src/sys/pkg/testing/mock-omaha-server",
    "//src/sys/pkg/testing/mock-paver",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:http",
    "//third_party/rust_crates:parking_lot",
  ]

  sources = [ "src/lib.rs" ]
  configs += [ "//build/config/rust/lints:clippy_warn_all" ]
}

fuchsia_test_component("isolated-ota-integration-test") {
  restricted_features = [ "allow_non_hermetic_packages" ]
  testonly = true
  manifest = "meta/isolated-ota-integration-test.cml"
  deps = [ ":driver" ]

  # Depends on root ssl certificate which is currently a system capability.
  test_type = "system"
}

fuchsia_test_package("isolated-ota-integration-tests") {
  test_components = [ ":isolated-ota-integration-test" ]
  deps = [
    "//src/connectivity/network/dns:component",
    "//src/connectivity/network/netstack:component",
    "//src/storage/testing:storage_driver_test_realm",
    "//src/storage/tools/blobfs-compression",
    "//src/sys/pkg/bin/fake-system-update-committer",
    "//src/sys/pkg/bin/pkg-cache:ignore-system-image-component-and-config",
    "//src/sys/pkg/bin/pkg-resolver:component",
    "//src/sys/pkg/bin/pm:pm_bin",
    "//src/sys/pkg/lib/fuchsia-pkg-testing/certs",
    "//src/sys/pkg/lib/isolated-swd:pkg-component",
    "//src/sys/pkg/tests/pkg-resolver:empty-repo",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  public_deps = [
    ":isolated-ota-integration-tests",
    "//src/sys/pkg/lib/isolated-swd:isolated-swd-components",
  ]
}
