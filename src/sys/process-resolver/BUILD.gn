# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/security.gni")
import("//src/sys/core/build/core_shard.gni")

rustc_binary("bin") {
  name = "process_resolver"
  with_unit_tests = true
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.ldsvc:fuchsia.ldsvc_rust",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
    "//sdk/fidl/fuchsia.process:fuchsia.process_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-url",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/lib/library_loader",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
  test_deps = [ "//src/lib/fuchsia-async" ]

  sources = [
    "src/main.rs",
    "src/resolve/mod.rs",
    "src/resolve/serve.rs",
  ]

  if (auto_update_packages) {
    # Allow resolving packages from the universe
    features = [ "auto_update_packages" ]
  }
}

fuchsia_component("component") {
  component_name = "process_resolver"
  manifest = "meta/process_resolver.cml"
  deps = [ ":bin" ]
}

# Enabling a soft-transition, remove when complete
# TODO(122864) Remove transitional labels
group("for_transition") {
  public_deps = [ ":process-resolver" ]
}

fuchsia_package("process-resolver") {
  deps = [ ":component" ]
}

core_shard("core_shard") {
  shard_file = "meta/process_resolver.core_shard.cml"
}

fuchsia_unittest_package("process-resolver-unittests") {
  deps = [ ":bin_test" ]
}

group("tests") {
  testonly = true
  deps = [ ":process-resolver-unittests" ]
}
