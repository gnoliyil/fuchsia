# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

visibility = [ "//src/sys/bedrock/*" ]

group("testbed") {
  testonly = true
  deps = [
    ":package",
    "exec:tests",
  ]
}

rustc_test("bin") {
  name = "testbed"
  edition = "2021"

  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fdio/rust:fdio",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/lib/process_builder",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/sys/bedrock/cap",
    "//src/sys/bedrock/exec",
    "//src/sys/bedrock/processargs",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:libc",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "src/echo_multishot.rs",
    "src/echo_oneshot.rs",
    "src/echo_oneshot_channel.rs",
    "src/echo_oneshot_remote.rs",
    "src/elf.rs",
    "src/lib.rs",
  ]
}

fuchsia_component("component") {
  testonly = true
  component_name = "testbed"
  manifest = "meta/testbed.cml"
  deps = [ ":bin" ]
}

fuchsia_component("root-component") {
  testonly = true
  component_name = "testbed-root"
  manifest = "meta/testbed_root.cml"
}

# Run with `fx test bedrock-testbed`.
fuchsia_test_package("package") {
  package_name = "bedrock-testbed"
  deps = [
    ":component",
    ":runner-component",
    "exec:echo_server",
    "exec:echo_server_cap",
    "exec:hello",
    "//src/sys/test_runners/lib_loader_cache:lib_loader_runner_cache_cmp",
  ]
  test_components = [ ":root-component" ]
}

fuchsia_component("runner-component") {
  testonly = true
  manifest = "meta/testbed_runner.cml"
  component_name = "testbed-runner"
  deps = [ "//src/sys/test_runners/rust:bin" ]
}
