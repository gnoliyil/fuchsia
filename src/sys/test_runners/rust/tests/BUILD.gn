# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("tests_bin") {
  edition = "2021"
  source_root = "main.rs"
  name = "rust_runner_integration_test"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [
    "helpers.rs",
    "main.rs",
  ]
}

fuchsia_test_component("rust_runner_integration_test") {
  testonly = true
  manifest = "meta/rust_runner_integration_test.cml"
  deps = [ ":tests_bin" ]
  test_type = "test_arch"
}

rustc_test("stress_tests_bin") {
  edition = "2021"
  source_root = "stress_main.rs"
  name = "rust_runner_stress_test"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [
    "helpers.rs",
    "stress_main.rs",
  ]
}

fuchsia_test_component("rust_runner_stress_test") {
  manifest = "meta/rust_runner_stress_test.cml"
  deps = [ ":stress_tests_bin" ]
  test_type = "test_arch"
}

fuchsia_test_package("rust-runner-integration-test") {
  test_components = [ ":rust_runner_integration_test" ]
  subpackages = [ "//src/sys/test_manager:pkg" ]

  if (!is_coverage) {
    # https://fxbug.dev/77236: This stress test times out on coverage bot.
    test_components += [ ":rust_runner_stress_test" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":rust-runner-integration-test",
    "../test_data",
  ]
}
