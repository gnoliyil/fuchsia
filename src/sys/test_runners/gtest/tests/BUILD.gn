# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

rustc_test("tests_bin") {
  edition = "2021"
  source_root = "tests.rs"
  name = "gtest_runner_integration_test"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:maplit",
    "//third_party/rust_crates:pretty_assertions",
  ]

  sources = [
    "helpers.rs",
    "tests.rs",
  ]

  inputs = [ "../test_data/sample_tests_golden_events.rsf" ]
}

fuchsia_component("gtest_runner_integration_test") {
  restricted_features = [ "allow_non_hermetic_packages" ]
  testonly = true
  manifest = "meta/gtest_runner_integration_test.cml"
  deps = [ ":tests_bin" ]
}

rustc_test("stress_tests_bin") {
  edition = "2021"
  source_root = "stress_tests.rs"
  name = "gtest_runner_stress_test"

  deps = [
    "//sdk/fidl/fuchsia.test.manager:fuchsia.test.manager_rust",
    "//src/lib/fuchsia-async",
    "//src/sys/test_manager:test_lib",
    "//src/sys/test_runners:test_lib",
    "//third_party/rust_crates:anyhow",
  ]

  sources = [
    "helpers.rs",
    "stress_tests.rs",
  ]
}

fuchsia_component("gtest_runner_stress_test") {
  restricted_features = [ "allow_non_hermetic_packages" ]
  testonly = true
  manifest = "meta/gtest_runner_stress_test.cml"
  deps = [ ":stress_tests_bin" ]
}

fuchsia_test_package("gtest-runner-integration-test") {
  test_components = [
    ":gtest_runner_integration_test",
    ":gtest_runner_stress_test",
  ]
  subpackages = [ "//src/sys/test_manager:test_manager_pkg" ]

  # fxbug.dev/74652: This stress test fails on coverage bot.
  if (is_coverage) {
    test_components -= [ ":gtest_runner_stress_test" ]
  }
}

group("tests") {
  testonly = true
  deps = [
    ":gtest-runner-integration-test",
    "../test_data:gtest-runner-example-tests",
  ]
}
