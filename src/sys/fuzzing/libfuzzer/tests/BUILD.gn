# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dist/renamed_binary.gni")
import("//build/test.gni")

executable("fuzzer-bin") {
  testonly = true
  output_name = "libfuzzer_test_fuzzer"
  deps = [
    "//src/sys/fuzzing/common/testing:sanitizer",
    "//src/sys/fuzzing/common/tests:fuzzer-lib",
  ]
  configs += [ "//src/sys/fuzzing/libfuzzer/testing:libfuzzer" ]
  exclude_toolchain_tags = [ "instrumented" ]
}

test("tests-bin") {
  output_name = "libfuzzer_integration_tests"
  sources = [ "integration-tests.cc" ]
  deps = [
    "//src/sys/fuzzing/common/testing",
    "//src/sys/fuzzing/common/tests:integration-tests-lib",
  ]
}

fuchsia_test_component("integration-tests") {
  manifest = "meta/fuzzer.cml"
  deps = [
    ":fuzzer-bin",
    ":tests-bin",
    "//src/sys/fuzzing/common/testing/data:corpus",
    "//src/sys/fuzzing/libfuzzer:engine-bin",
  ]
}

fuchsia_test_package("tests") {
  package_name = "libfuzzer-integration-tests"
  test_components = [ ":integration-tests" ]
}
