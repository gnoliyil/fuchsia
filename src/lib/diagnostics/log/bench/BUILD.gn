# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/perf/test.gni")

rustc_binary("encoding-bin") {
  name = "diagnostics_log_encoding_bench"
  edition = "2018"
  with_unit_tests = true

  source_root = "src/encoding.rs"

  deps = [
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//src/developer/fuchsia-criterion",
    "//src/lib/diagnostics/log/encoding/rust",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:once_cell",
    "//third_party/rust_crates:paste",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:tracing-core",
    "//third_party/rust_crates:tracing-subscriber",
  ]

  sources = [
    "src/common.rs",
    "src/encoding.rs",
  ]
}

rustc_binary("core-bin") {
  name = "diagnostics_log_bench"
  edition = "2018"

  source_root = "src/core.rs"

  deps = [
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//src/developer/fuchsia-criterion",
    "//src/lib/diagnostics/log/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:tracing-log",
  ]

  sources = [ "src/core.rs" ]
}

rustc_binary("decoding-bin") {
  name = "diagnostics_log_decoding_bench"
  edition = "2018"
  with_unit_tests = true

  source_root = "src/decoding.rs"

  deps = [
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//src/developer/fuchsia-criterion",
    "//src/lib/diagnostics/log/encoding/rust",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:once_cell",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:tracing-core",
    "//third_party/rust_crates:tracing-subscriber",
  ]

  sources = [
    "src/common.rs",
    "src/decoding.rs",
  ]
}

fuchsia_component("encoding-component") {
  component_name = "encoding"
  manifest = "meta/encoding.cml"
  deps = [ ":encoding-bin" ]
}

fuchsia_component("decoding-component") {
  component_name = "decoding"
  manifest = "meta/decoding.cml"
  deps = [ ":decoding-bin" ]
}

fuchsia_component("core-component") {
  component_name = "core"
  manifest = "meta/core.cml"
  deps = [ ":core-bin" ]
}

fuchsia_package("diagnostics-log-rust-benchmarks") {
  deps = [
    ":core-component",
    ":decoding-component",
    ":encoding-component",
  ]
}

fuchsia_component_perf_test("diagnostics-log-rust-core-benchmark") {
  package = ":diagnostics-log-rust-benchmarks"
  component_name = "core"
  expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.diagnostics_log_rust.core.txt"
}

fuchsia_component_perf_test("diagnostics-log-rust-encoding-benchmark") {
  package = ":diagnostics-log-rust-benchmarks"
  component_name = "encoding"
  expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.diagnostics_log_rust.encoding.txt"
}

fuchsia_component_perf_test("diagnostics-log-rust-decoding-benchmark") {
  package = ":diagnostics-log-rust-benchmarks"
  component_name = "decoding"
  expected_metric_names_filepath = "//src/tests/end_to_end/perf/expected_metric_names/fuchsia.diagnostics_log_rust.decoding.txt"
}

group("bench") {
  testonly = true
  deps = [
    ":diagnostics-log-rust-core-benchmark",
    ":diagnostics-log-rust-decoding-benchmark",
  ]

  # TODO(https://fxbug.dev/131000): diagnostics_log_encoding_bench
  # currently fails to build for ARM64/VIM3 due to a toolchain bug.
  # Re-enable it for ARM64 when the bug is fixed.
  if (target_cpu != "arm64") {
    deps += [ ":diagnostics-log-rust-encoding-benchmark" ]
  }
}
