# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")

group("tests") {
  testonly = true

  deps = [
    ":diagnostics-log-tests",
    ":rust_test($host_toolchain)",
  ]
}

rustc_library("rust") {
  name = "diagnostics_log"
  with_unit_tests = true
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
    "//third_party/rust_crates:tracing-log",
    "//third_party/rust_crates:tracing-subscriber",
  ]

  if (is_fuchsia) {
    deps += [
      "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
      "//sdk/lib/syslog:client_includes",
      "//src/lib/diagnostics/log/encoding/rust",
      "//src/lib/fidl/rust/fidl",
      "//src/lib/fuchsia-async",
      "//src/lib/fuchsia-component",
      "//src/lib/fuchsia-runtime",
      "//src/lib/zircon/rust:fuchsia-zircon",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:tracing-core",
    ]

    test_deps = [
      "//sdk/fidl/fuchsia.diagnostics.stream:fuchsia.diagnostics.stream_rust",
      "//src/lib/fuchsia-async",
      "//third_party/rust_crates:futures",
    ]
  } else {
    test_deps = [ "//third_party/rust_crates:regex" ]
  }

  sources = [
    "src/fuchsia/filter.rs",
    "src/fuchsia/mod.rs",
    "src/fuchsia/sink.rs",
    "src/lib.rs",
    "src/portable/mod.rs",
  ]
}

fuchsia_unittest_package("diagnostics-log-tests") {
  deps = [ ":rust_test" ]
}
