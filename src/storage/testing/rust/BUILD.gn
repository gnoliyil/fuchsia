# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_library.gni")
import("//src/storage/testing/driver_test_realm.gni")

rustc_library("storage-isolated-driver-manager") {
  edition = "2021"
  with_unit_tests = true
  deps = [
    "//sdk/fidl/fuchsia.device:fuchsia.device_rust",
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_rust",
    "//sdk/fidl/fuchsia.hardware.block.encrypted:fuchsia.hardware.block.encrypted_rust",
    "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_rust",
    "//sdk/fidl/fuchsia.hardware.block.volume:fuchsia.hardware.block.volume_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/lib/device-watcher/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/storage/fs_management/rust:fs_management",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:uuid",
  ]

  non_rust_deps = [ "//src/lib/storage/fs_management/c" ]

  sources = [
    "src/fvm.rs",
    "src/lib.rs",
    "src/zxcrypt.rs",
  ]

  test_deps = [
    "//src/lib/fuchsia",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/test_util",
  ]
}

storage_driver_test_realm_v2_component(
    "storage-isolated-driver-manager-unittests-component") {
  manifest = "meta/storage-isolated-driver-manager-unittests.cml"
  deps = [ ":storage-isolated-driver-manager_test" ]
}

fuchsia_test_package("storage-isolated-driver-manager-unittests") {
  test_components = [ ":storage-isolated-driver-manager-unittests-component" ]
  test_specs = {
    parallel = 1
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [ ":storage-isolated-driver-manager-unittests" ]
}
