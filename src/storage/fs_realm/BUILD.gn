# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//src/sys/core/build/core_shard.gni")

rustc_binary("bin") {
  output_name = "fs_realm"
  edition = "2021"

  deps = [
    "//sdk/fidl/fuchsia.hardware.block:fuchsia.hardware.block_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.process.lifecycle:fuchsia.process.lifecycle_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-fs",
    "//src/lib/fuchsia-runtime",
    "//src/lib/storage/fs_management/rust:fs_management",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/storage/bin/blobfs:component",
    "//src/storage/bin/f2fs:f2fs-component",
    "//src/storage/bin/minfs:minfs-component",
    "//src/storage/fidl/fuchsia.fs.realm:fuchsia.fs.realm_rust",
    "//src/storage/fxfs:fxfs_component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  sources = [ "src/main.rs" ]

  # fs_realm is only used in eng products, not production.
  deps += [ "//build/validate:non_production_tag" ]
}

# Enabling a soft-transition, remove when complete
# TODO(122864) Remove transitional labels
group("core_shard_for_transition") {
  public_deps = [ ":fs_realm_core_shard" ]
}

core_shard("fs_realm_core_shard") {
  shard_file = "meta/fs_realm.core_shard.cml"
}

fuchsia_component("component") {
  component_name = "fs_realm"
  manifest = "meta/fs_realm.cml"
  deps = [ ":bin" ]
}

# Enabling a soft-transition, remove when complete
# TODO(122864) Remove transitional labels
group("for_transition") {
  public_deps = [ ":fs_realm" ]
}

fuchsia_package("fs_realm") {
  deps = [ ":component" ]
  # This package is added by product assembly into eng builds only.
  # It should not be added manually anywhere else.
  #
  # This is temporarily relaxed for a transition
  # visibility = [ "//bundles/assembly/*" ]
}
