# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_test_package.gni")
import("//build/components/fuchsia_unittest_component.gni")
import("//build/host.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("ext4_to_pkg.gni")

group("ext4") {
  deps = [
    "lib:parser",
    "server:bin",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":ext4_tests",
    "lib:tests",
    "read-only:tests",
    "server:tests",
  ]
}

rustc_library("ext4_metadata") {
  edition = "2021"
  sources = [ "metadata.rs" ]
  source_root = "metadata.rs"
  with_unit_tests = true
  deps = [
    "//third_party/rust_crates:bincode",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:thiserror",
  ]
  test_deps = [ "//third_party/rust_crates:assert_matches" ]
}

fuchsia_unittest_component("ext4_metadata_tests") {
  deps = [ ":ext4_metadata_test" ]
}

rustc_binary("ext4_to_pkg") {
  edition = "2021"
  sources = [ "ext4_to_pkg.rs" ]
  source_root = "ext4_to_pkg.rs"
  with_unit_tests = true
  deps = [
    ":ext4_metadata",
    "read-only:ext4-read-only",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:argh",
    "//third_party/rust_crates:zerocopy",
  ]
  test_deps = [ "//third_party/rust_crates:assert_matches" ]
}

ext4_to_pkg("test_img") {
  input = "test.img"
  prefix = "data/test-image"
}

fuchsia_unittest_component("ext4_to_pkg_tests") {
  deps = [
    ":ext4_to_pkg_test",
    ":test_img",
  ]
}

fuchsia_test_package("ext4_tests") {
  test_components = [
    ":ext4_metadata_tests",
    ":ext4_to_pkg_tests",
  ]
}
