# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/toolchain.gni")
import("//build/test.gni")
import("//src/sys/core/build/core_shard.gni")

## Groups
group("tests") {
  testonly = true
  deps = [
    ":audio_services_device_registry_unittests",
    ":audio_services_device_registry_warning_unittests",
    "testing",
  ]
}

## Shards
core_shard("core_shard") {
  shard_file = "meta/audio_device_registry.core_shard.cml"
}

## Packages
fuchsia_package("device_registry") {
  package_name = "audio_device_registry"

  deps = [ ":device_registry_comp" ]
}

fuchsia_unittest_package("audio_services_device_registry_unittests") {
  deps = [ ":device_registry_unittests_bin" ]

  test_specs = {
    log_settings = {
      max_severity = "INFO"
    }
  }
}

fuchsia_unittest_package("audio_services_device_registry_warning_unittests") {
  deps = [ ":device_registry_warning_unittests_bin" ]

  test_specs = {
    log_settings = {
      max_severity = "WARN"
    }
  }
}

## Components
fuchsia_component("device_registry_comp") {
  component_name = "audio_device_registry"
  manifest = "//src/media/audio/services/device_registry/meta/audio_device_registry.cml"

  deps = [ ":device_registry_bin" ]
}

## Executables
executable("device_registry_bin") {
  output_name = "audio_device_registry"
  sources = [ "main.cc" ]
  deps = [
    ":device_registry_lib",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("device_registry_unittests_bin") {
  testonly = true

  sources = [
    "audio_device_registry_unittest.cc",
    "control_creator_server_unittest.cc",
    "control_server_unittest.cc",
    "device_detector_unittest.cc",
    "device_unittest.cc",
    "observer_server_unittest.cc",
    "provider_server_unittest.cc",
    "registry_server_unittest.cc",
    "ring_buffer_server_unittest.cc",
  ]

  deps = [
    ":unittest_base",
    "//src/lib/storage/vfs/cpp",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

executable("device_registry_warning_unittests_bin") {
  testonly = true

  sources = [
    "audio_device_registry_warning_unittest.cc",
    "control_creator_server_warning_unittest.cc",
    "control_server_warning_unittest.cc",
    "device_warning_unittest.cc",
    "observer_server_warning_unittest.cc",
    "provider_server_warning_unittest.cc",
    "registry_server_warning_unittest.cc",
    "ring_buffer_server_warning_unittest.cc",
  ]

  deps = [ ":unittest_base" ]
}

## Source sets
source_set("device_registry_lib") {
  sources = [
    "audio_device_registry.cc",
    "audio_device_registry.h",
    "basic_types.h",
    "control_creator_server.cc",
    "control_creator_server.h",
    "control_notify.h",
    "control_server.cc",
    "control_server.h",
    "device.cc",
    "device.h",
    "device_detector.cc",
    "device_detector.h",
    "device_presence_watcher.h",
    "logging.cc",
    "logging.h",
    "observer_notify.h",
    "observer_server.cc",
    "observer_server.h",
    "provider_server.cc",
    "provider_server.h",
    "registry_server.cc",
    "registry_server.h",
    "ring_buffer_server.cc",
    "ring_buffer_server.h",
    "validate.cc",
    "validate.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.audio.device:fuchsia.audio.device_cpp",
    "//sdk/fidl/fuchsia.hardware.audio:fuchsia.hardware.audio_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//src/lib/fsl",
    "//src/media/audio/lib/clock",
    "//src/media/audio/services/common",
  ]

  deps = [
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fdio",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/public",
    "//zircon/system/ulib/zx",
  ]
}

source_set("unittest_base") {
  testonly = true

  sources = [
    "adr_server_unittest_base.h",
    "device_unittest.h",
  ]

  public_deps = [
    ":device_registry_lib",
    "testing:fakes",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/media/audio/lib/clock",
    "//src/media/audio/services/common/testing",
    "//third_party/googletest:gmock",
    "//zircon/system/public",
  ]
}
