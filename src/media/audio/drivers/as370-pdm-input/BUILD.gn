# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("as370_audio_in_bind") {
  rules = "as370_audio_in.bind"
  bind_output = "as370_audio_in.bindc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.synaptics.platform" ]
}

source_set("common") {
  sources = [ "audio-stream-in.cc" ]

  public_deps = [
    ":as370_audio_in_bind",
    "//sdk/banjo/fuchsia.hardware.shareddma:fuchsia.hardware.shareddma_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp_wire",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/as370",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/lib/simple-audio-stream",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]
}
fuchsia_driver("as370-pdm-input-driver") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]

  deps = [ ":common" ]
}

fuchsia_driver_component("as370-pdm-input") {
  component_name = "as370-pdm-input"
  deps = [ ":as370-pdm-input-driver" ]
  info = "as370-pdm-input-info.json"
  manifest = "meta/as370-pdm-input.cml"
}

test("as370-pdm-input-unit-tests") {
  sources = [ "unit-tests.cc" ]
  deps = [
    ":common",
    "//sdk/banjo/fuchsia.hardware.shareddma:fuchsia.hardware.shareddma_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp_wire",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_unittest_package("as370-pdm-input-tests") {
  deps = [ ":as370-pdm-input-unit-tests" ]
}

group("tests") {
  testonly = true
  deps = [
    ":as370-pdm-input-tests",
    ":as370_audio_in_bind_test",
  ]
}
