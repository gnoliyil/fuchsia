# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

source_set("common") {
  sources = [
    "audio-stream-out.cc",
    "audio-stream-out.h",
  ]
  public_deps = [
    ":as370_audio_out_bind",
    "//sdk/banjo/fuchsia.hardware.shareddma:fuchsia.hardware.shareddma_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp_wire",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/as370",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddktl",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/lib/simple-audio-stream",
    "//src/media/audio/lib/simple-codec",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/zx",
  ]
}

driver_bind_rules("as370_audio_out_bind") {
  rules = "as370_audio_out.bind"
  bind_output = "as370_audio_out.bindc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.synaptics.platform" ]
}

fuchsia_driver("as370-tdm-output-driver") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [ ":common" ]
}

fuchsia_driver_component("as370-tdm-output") {
  component_name = "as370-tdm-output"
  deps = [ ":as370-tdm-output-driver" ]
  info = "as370-tdm-output-info.json"
  manifest = "meta/as370-tdm-output.cml"
}

test("as370-tdm-output-unit-tests") {
  sources = [ "unit-tests.cc" ]
  deps = [
    ":common",
    "//sdk/banjo/fuchsia.hardware.shareddma:fuchsia.hardware.shareddma_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp_wire",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_unittest_package("as370-tdm-output-tests") {
  deps = [ ":as370-tdm-output-unit-tests" ]
}

group("tests") {
  testonly = true
  deps = [
    ":as370-tdm-output-tests",
    ":as370_audio_out_bind_test",
  ]
}
