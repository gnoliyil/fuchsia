# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("ti_tas5720-bind") {
  rules = "ti_tas5720.bind"
  bind_output = "tas5720.bindbc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.ti.platform" ]
}

fuchsia_driver("tas5720-driver") {
  output_name = "tas5720"
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  sources = [
    "tas5720-inspect.cc",
    "tas5720.cc",
  ]
  deps = [
    ":ti_tas5720-bind",
    "//sdk/lib/sys/inspect/cpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/lib/simple-codec",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("tas5720") {
  component_name = "tas5720"
  deps = [ ":tas5720-driver" ]
  info = "tas5720-info.json"
  manifest = "meta/tas5720.cml"
}

test("tas5720-test-bin") {
  defines = [ "_ALL_SOURCE" ]
  output_name = "tas5720-test"
  sources = [
    "tas5720-inspect.cc",
    "tas5720-test.cc",
    "tas5720.cc",
  ]
  deps = [
    ":tas5720",
    ":ti_tas5720-bind",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//sdk/lib/sys/inspect/cpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/lib/simple-codec",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("tas5720-test-package") {
  package_name = "tas5720-test"
  deps = [ ":tas5720-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":tas5720-test-package",
    ":ti_tas5720-bind_test",
  ]
}
