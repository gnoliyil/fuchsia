# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("ti_tas27xx-bind") {
  rules = "ti_tas27xx.bind"
  bind_output = "tas27xx.bindbc"
  tests = "tests.json"

  # TODO(fxb/115757): Remove this once we no longer need to generate
  # fragments for composite bind rules.
  disable_fragment_gen = true
  deps = [
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.i2c",
    "//src/devices/bind/fuchsia.ti.platform",
  ]
}

fuchsia_driver("tas27xx-driver") {
  output_name = "tas27xx"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "tas27xx.cc" ]
  deps = [
    ":ti_tas27xx-bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/lib/driver",
    "//src/devices/lib/ti",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/lib/simple-codec",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_driver_component("tas27xx") {
  component_name = "tas27xx"
  deps = [ ":tas27xx-driver" ]
  info = "tas27xx-info.json"
  manifest = "meta/tas27xx.cml"
}

test("tas27xx-test-bin") {
  configs += [ "//build/config:all_source" ]
  output_name = "tas27xx-test"
  sources = [
    "tas27xx-test.cc",
    "tas27xx.cc",
  ]
  deps = [
    ":tas27xx",
    ":ti_tas27xx-bind",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/lib/ti",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/lib/simple-codec",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/mock-function",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

fuchsia_unittest_package("tas27xx-test-package") {
  package_name = "tas27xx-test"
  deps = [ ":tas27xx-test-bin" ]
  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":tas27xx-test-package",
    ":ti_tas27xx-bind_test",
  ]
}
