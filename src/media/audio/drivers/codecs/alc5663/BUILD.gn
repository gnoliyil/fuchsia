# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("alc5663_bind") {
  rules = "alc5663.bind"
  tests = "tests.json"
  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.i2c",
  ]
}

# Also used by test/.
source_set("shared") {
  visibility = [ "./*" ]
  configs += [ "//build/config:all_source" ]
  sources = [ "alc5663.cc" ]
  public_deps = [
    ":alc5663_bind",
    "//sdk/lib/fit",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("alc5663-driver") {
  output_name = "alc5663"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":shared",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
  ]
}

fuchsia_driver_component("alc5663") {
  component_name = "alc5663"
  deps = [ ":alc5663-driver" ]
  info = "alc5663-info.json"
  manifest = "meta/alc5663.cml"
}

group("tests") {
  testonly = true
  deps = [
    ":alc5663_bind_test",
    "tests",
  ]
}
