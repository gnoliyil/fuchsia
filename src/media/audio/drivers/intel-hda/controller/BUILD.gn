# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/cipd.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/fuzz.gni")
import("//build/test.gni")

driver_bind_rules("ihda_controller_bind") {
  rules = "intel_hda.bind"
  header_output = "intel_hda-bind.h"
  deps = [ "//src/devices/bind/fuchsia.pci" ]
  tests = "bind_test.json"
}

firmware_path = "dsp_fw_kbl_v3420.bin"

# All non-test targets.
group("controller") {
  deps = [ ":intel-hda" ]
}

# All test targets.
group("test") {
  testonly = true
  deps = [ ":controller_test" ]
}

source_set("src") {
  visibility = [ ":*" ]
  defines = [ "ADSP_FIRMWARE_PATH=\"$firmware_path\"" ]
  sources = [
    "binary_decoder.h",
    "codec-cmd-job.cc",
    "codec-cmd-job.h",
    "debug-logging.h",
    "device-ids.h",
    "hda-codec-connection.cc",
    "hda-codec-connection.h",
    "intel-dsp-code-loader.cc",
    "intel-dsp-code-loader.h",
    "intel-dsp-debug.cc",
    "intel-dsp-ipc.cc",
    "intel-dsp-ipc.h",
    "intel-dsp-modules.cc",
    "intel-dsp-modules.h",
    "intel-dsp-stream.cc",
    "intel-dsp-stream.h",
    "intel-dsp-topology.cc",
    "intel-dsp-topology.h",
    "intel-dsp.cc",
    "intel-dsp.h",
    "intel-hda-controller-init.cc",
    "intel-hda-controller.cc",
    "intel-hda-controller.h",
    "intel-hda-debug.cc",
    "intel-hda-irq.cc",
    "intel-hda-stream.cc",
    "intel-hda-stream.h",
    "nhlt.cc",
    "nhlt.h",
    "pci_regs.h",
    "utils.cc",
    "utils.h",
  ]
  deps = [
    ":ihda_controller_bind",
    "//sdk/banjo/fuchsia.hardware.intelhda.codec:fuchsia.hardware.intelhda.codec_banjo_cpp",
    "//src/devices/lib/acpi",
    "//src/devices/lib/driver",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.intel.hda:fuchsia.hardware.intel.hda_cpp_wire",
    "//sdk/lib/fit",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/lib/ddk",

    # TODO(fxb/38132): Migrate to the new bind rules and delete the below
    "//src/lib/ddk:ddk-deprecated-binding-headers",
    "//src/media/audio/drivers/lib/intel-hda",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/refcount",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

resource("firmware") {
  sources = [ "//prebuilt/firmware/media/intel-adsp-sst/$firmware_path" ]
  outputs = [ "lib/firmware/$firmware_path" ]
}

fuchsia_driver("intel-hda-driver") {
  output_name = "intel-hda"
  defines = [ "ADSP_FIRMWARE_PATH=\"$firmware_path\"" ]
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [ ":src" ]
  if (internal_access) {
    deps += [ ":firmware" ]
  }
}

fuchsia_driver_component("intel-hda") {
  component_name = "intel-hda"
  deps = [ ":intel-hda-driver" ]
  info = "intel-hda-info.json"
  manifest = "meta/intel-hda.cml"
}

test("controller_test") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  output_name = "intel-hda-controller-test"
  sources = [
    "binary_decoder_test.cc",
    "intel-dsp-ipc-test.cc",
    "intel-dsp-modules-test.cc",
    "intel-dsp-topology-test.cc",
    "intel-hda-controller-test.cc",
    "nhlt_test.cc",
  ]
  deps = [
    ":src",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fdio",
    "//src/devices/lib/acpi/mock:mock-acpi",
    "//src/devices/pci/testing:pci-protocol-fake",
    "//src/devices/testing/mock-ddk",
    "//src/media/audio/drivers/lib/intel-hda",
    "//zircon/system/ulib/async-loop/testing:async-loop-testing",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("intel-hda-controller-test") {
  deps = [ ":controller_test" ]

  test_specs = {
    environments = basic_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

executable("intel-hda-stream-test-bin") {
  testonly = true
  sources = [ "intel-hda-stream-test.cc" ]
  deps = [
    ":src",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_package("intel-hda-stream-test") {
  package_name = "intel-hda-stream-test"
  deps = [ ":intel-hda-stream-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

executable("intel-dsp-stream-test-bin") {
  testonly = true
  sources = [ "intel-dsp-stream-test.cc" ]
  deps = [
    ":src",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("intel-dsp-stream-test") {
  package_name = "intel-dsp-stream-test"
  deps = [ ":intel-dsp-stream-test-bin" ]
  test_specs = {
    environments = basic_envs
  }
}

# Fuzz testing for NHLT parsing.
#
# Name needs to end with "-fuzzer" to be included in the Zircon fuzz test package.
fuchsia_library_fuzzer("nhlt-fuzzer") {
  sources = [ "nhlt_fuzzer.cc" ]
  deps = [
    ":src",
    "//zircon/system/ulib/fbl",
  ]

  # TODO(fxbug.dev/47959): Fix the leaks and remove this.
  deps += [ "//build/config/sanitizers:suppress-lsan.DO-NOT-USE-THIS" ]
}

fuchsia_fuzzer_component("nhlt-fuzzer-component") {
  manifest = "meta/nhlt-fuzzer.cml"
  deps = [ ":nhlt-fuzzer" ]
}

fuchsia_fuzzer_package("intel-hda-controller-fuzzers") {
  cpp_fuzzer_components = [ ":nhlt-fuzzer-component" ]
}

group("tests") {
  testonly = true
  deps = [
    ":ihda_controller_bind_test",
    ":intel-dsp-stream-test",
    ":intel-hda-controller-fuzzers",
    ":intel-hda-controller-test",
    ":intel-hda-stream-test",
  ]
}
