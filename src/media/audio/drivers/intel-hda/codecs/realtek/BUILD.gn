# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("ihda_codecs_realtek_bind") {
  rules = "realtek_ihda_codec.bind"
  deps = [ "//src/devices/bind/fuchsia.intelhda" ]
  tests = "bind_test.json"
}

group("realtek") {
  deps = [ ":realtek-audio-codec" ]
}

fuchsia_driver("realtek-audio-codec-driver") {
  output_name = "realtek-audio-codec"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "realtek-codec.cc",
    "realtek-stream.cc",
  ]
  deps = [
    ":ihda_codecs_realtek_bind",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/drivers/lib/intel-hda",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_driver_component("realtek-audio-codec") {
  component_name = "realtek-audio-codec"
  deps = [ ":realtek-audio-codec-driver" ]
  info = "realtek-audio-codec-info.json"
  manifest = "meta/realtek-audio-codec.cml"
}

group("tests") {
  testonly = true

  deps = [ ":ihda_codecs_realtek_bind_test" ]
}
