# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("ihda_codecs_qemu_bind") {
  rules = "qemu_ihda_codec.bind"
  header_output = "qemu_ihda_codec-bind.h"
  deps = [ "//src/devices/bind/fuchsia.intelhda" ]
  tests = "bind_test.json"
}

group("qemu") {
  deps = [ ":qemu-audio-codec" ]
}

fuchsia_driver("qemu-audio-codec-driver") {
  output_name = "qemu-audio-codec"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "qemu-codec.cc",
    "qemu-stream.cc",
  ]
  deps = [
    ":ihda_codecs_qemu_bind",
    "//sdk/banjo/fuchsia.hardware.intelhda.codec:fuchsia.hardware.intelhda.codec_banjo_cpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/drivers/lib/intel-hda",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_driver_component("qemu-audio-codec") {
  component_name = "qemu-audio-codec"
  deps = [ ":qemu-audio-codec-driver" ]
  info = "qemu-audio-codec-info.json"
  manifest = "meta/qemu-audio-codec.cml"
}

group("tests") {
  testonly = true

  deps = [ ":ihda_codecs_qemu_bind_test" ]
}
