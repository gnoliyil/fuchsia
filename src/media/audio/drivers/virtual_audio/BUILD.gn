# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/zbi/kernel_cmdline.gni")

fuchsia_driver_package("virtual_audio") {
  driver_components = [ ":virtual_audio_driver" ]
}

driver_bind_rules("virtual_audio_bind") {
  rules = "virtual_audio.bind"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.platform" ]
}

group("tests") {
  testonly = true
  deps = [ ":virtual_audio_bind_test" ]
}

fuchsia_driver("virtual_audio_driver-driver") {
  output_name = "virtual_audio_driver"

  sources = [
    "virtual_audio.h",
    "virtual_audio_bus.cc",
    "virtual_audio_composite.cc",
    "virtual_audio_composite.h",
    "virtual_audio_control_impl.cc",
    "virtual_audio_control_impl.h",
    "virtual_audio_dai.cc",
    "virtual_audio_dai.h",
    "virtual_audio_device_impl.cc",
    "virtual_audio_device_impl.h",
    "virtual_audio_stream.cc",
    "virtual_audio_stream.h",
  ]

  deps = [
    ":virtual_audio_bind",
    "//sdk/fidl/fuchsia.hardware.audio:fuchsia.hardware.audio_cpp",
    "//sdk/fidl/fuchsia.virtualaudio:fuchsia.virtualaudio_cpp",
    "//sdk/fidl/fuchsia.virtualaudio:fuchsia.virtualaudio_cpp_wire",
    "//sdk/lib/driver/runtime:driver_runtime_cpp",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/media/audio/drivers/lib/audio-driver-proto",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//src/media/audio/lib/simple-audio-stream",
    "//zircon/system/ulib/affine",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fzl",
  ]
}

fuchsia_driver_component("virtual_audio_driver") {
  component_name = "virtual_audio_driver"
  deps = [ ":virtual_audio_driver-driver" ]
  info = "virtual_audio-info.json"
  manifest = "meta/virtual_audio_driver.cml"
}

kernel_cmdline("kernel-logging") {
  args = [ "driver.virtual_audio.log=trace" ]
}
