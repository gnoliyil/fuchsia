# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_component.gni")
import("//build/components/fuchsia_package.gni")
import("//build/components/fuchsia_unittest_package.gni")
import("//src/sys/core/build/core_shard.gni")

group("consumer") {
  deps = [ ":audio_consumer" ]
}

group("tests") {
  testonly = true
  deps = [ ":audio_consumer_tests" ]
}

fuchsia_package("audio_consumer") {
  deps = [ ":audio_consumer_component" ]
}

fuchsia_component("audio_consumer_component") {
  component_name = "audio_consumer"
  deps = [ ":audio_consumer_bin" ]
  manifest = "meta/audio_consumer.cml"
}

executable("audio_consumer_bin") {
  output_name = "audio_consumer"

  sources = [
    "consumer.cc",
    "consumer.h",
    "consumer_creator.cc",
    "consumer_creator.h",
    "main.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.media:fuchsia.media_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/fit",
    "//sdk/lib/fit-promise",
    "//sdk/lib/media/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("audio_consumer_tests_bin") {
  testonly = true
  output_name = "audio_consumer_tests"

  sources = [
    "consumer.cc",
    "consumer.h",
    "test/audio_consumer_tests.cc",
    "test/fake_audio_core.h",
    "test/fake_audio_renderer.h",
    "test/fake_gain_control.h",
    "test/get_koid.cc",
    "test/get_koid.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.media:fuchsia.media_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/media/cpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
  ]
}

fuchsia_unittest_package("audio_consumer_tests") {
  deps = [ ":audio_consumer_tests_bin" ]
}

core_shard("audio_consumer_shard") {
  shard_file = "meta/audio_consumer.core_shard.cml"
}
