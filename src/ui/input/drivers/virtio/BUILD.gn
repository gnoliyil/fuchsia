# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("virtio_input_bind") {
  rules = "virtio_input.bind"
  tests = "bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

fuchsia_driver("virtio_input-driver") {
  output_name = "virtio_input"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "input.cc",
    "input_driver.cc",
    "input_kbd.cc",
    "input_mouse.cc",
    "input_touch.cc",
  ]
  deps = [
    ":virtio_input_bind",
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_cpp",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddktl",
    "//src/ui/input/lib/hid",
    "//zircon/system/ulib/inspect",
  ]
}

fuchsia_driver_component("virtio_input") {
  component_name = "virtio_input"
  deps = [ ":virtio_input-driver" ]
  info = "virtio_input-info.json"
  manifest = "meta/virtio_input.cml"
}

fuchsia_driver_package("package") {
  package_name = "virtio_input"
  driver_components = [ ":virtio_input" ]
}

test("virtio-input-test") {
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "input_kbd.cc",
    "input_mouse.cc",
    "input_test.cc",
    "input_touch.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.input.report:fuchsia.input.report_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/bus/lib/virtio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/no_ddk",
    "//src/ui/input/lib/hid-input-report",
    "//src/ui/input/lib/hid-parser",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest",
  ]

  # TODO(https://fxbug.dev/42176699): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

fuchsia_unittest_package("virtio-input-test-package") {
  package_name = "virtio-input-test"
  deps = [ ":virtio-input-test" ]
}

group("tests") {
  testonly = true
  deps = [
    ":virtio-input-test-package",
    ":virtio_input_bind_test",
  ]
}
