# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/bootfs_files_for_assembly.gni")
import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("hid_bind") {
  rules = "hid.bind"
  bind_output = "hid.bindbc"
  tests = "tests.json"
  deps = [ "//src/devices/bind/fuchsia.hid" ]
}

fuchsia_driver("hid-driver") {
  output_name = "hid"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "device-report-reader.cc",
    "hid-fifo.c",
    "hid-instance.cc",
    "hid.cc",
  ]
  deps = [
    ":hid_bind",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hiddevice:fuchsia.hardware.hiddevice_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.input:fuchsia.hardware.input_cpp_wire",
    "//src/devices/bind/fuchsia.hid:fuchsia.hid_cpp",
    "//src/devices/lib/driver",
    "//src/devices/lib/driver:driver_runtime",
    "//src/lib/ddk",
    "//src/lib/listnode",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/hid-parser",
    "//zircon/system/ulib/trace",
  ]
}

fuchsia_driver_component("hid") {
  visibility = [ ":*" ]
  component_name = "hid"
  deps = [ ":hid-driver" ]
  info = "hid-info.json"
  manifest = "meta/hid.cml"
}

bootfs_files_for_assembly("bootfs") {
  deps = [ ":hid" ]
}

test("hid-test") {
  sources = [
    "device-report-reader.cc",
    "hid-fifo.c",
    "hid-instance.cc",
    "hid-test.cc",
    "hid.cc",
  ]
  deps = [
    ":hid_bind",
    "//sdk/banjo/fuchsia.hardware.hidbus:fuchsia.hardware.hidbus_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.hiddevice:fuchsia.hardware.hiddevice_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.input:fuchsia.hardware.input_cpp_wire",
    "//sdk/lib/driver/runtime/testing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bind/fuchsia.hid:fuchsia.hid_cpp",
    "//src/devices/lib/driver:driver_runtime",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddk",
    "//src/lib/listnode",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hid",
    "//zircon/system/ulib/hid-parser",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("hid-test-package") {
  package_name = "hid-test"
  deps = [ ":hid-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":hid-test-package",
    ":hid_bind_test",
  ]
}
