# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/fidl/fidl.gni")
import("//build/test.gni")

driver_bind_rules("vim3-pwm-backlight-bind") {
  rules = "vim3-pwm-backlight.bind"
  bind_output = "vim3-pwm-backlight.bindbc"
  tests = "vim3-pwm-backlight-bind-test.json"

  # TODO(fxbug.dev/115757): Remove this once we no longer need to generate
  # fragments for composite bind rules.
  disable_fragment_gen = true

  deps = [
    "//src/devices/bind/fuchsia.amlogic.platform.a311d",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.hardware.pwm",
    "//src/devices/bind/fuchsia.khadas.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

source_set("common") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "vim3-pwm-backlight.cc" ]
  public_deps =
      [ "//sdk/fidl/fuchsia.hardware.pwm:fuchsia.hardware.pwm_cpp_wire" ]
  deps = [
    ":vim3-pwm-backlight-bind",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp_wire",
    "//sdk/lib/fidl",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_driver("vim3-pwm-backlight-driver") {
  output_name = "vim3-pwm-backlight"
  deps = [ ":common" ]
}

fuchsia_driver_component("vim3-pwm-backlight") {
  component_name = "vim3-pwm-backlight"
  deps = [ ":vim3-pwm-backlight-driver" ]
  info = "vim3-pwm-backlight-info.json"
  manifest = "meta/vim3-pwm-backlight.cml"
}

test("vim3-pwm-backlight-test") {
  output_name = "vim3-pwm-backlight-test"
  sources = [ "vim3-pwm-backlight-test.cc" ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_cpp_wire",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/component/outgoing/cpp:cpp",
    "//sdk/lib/inspect/testing/cpp:zxtest",
    "//src/devices/gpio/testing/fake-gpio",
    "//src/devices/lib/amlogic:amlogic",
    "//src/devices/lib/mmio",
    "//src/devices/testing/mock-ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/async-testing",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("vim3-pwm-backlight-test-package") {
  package_name = "vim3-pwm-backlight-test"
  deps = [ ":vim3-pwm-backlight-test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":vim3-pwm-backlight-bind_test",
    ":vim3-pwm-backlight-test-package",
  ]
}
