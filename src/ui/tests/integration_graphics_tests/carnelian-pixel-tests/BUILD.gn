# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/lib/vulkan/vulkan.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":carnelian_pixel_test",
    "//src/graphics/bin/vulkan_loader",
  ]
}

executable("carnelian_pixel_test_bin") {
  testonly = true
  sources = [ "carnelian_pixel_tests.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_hlcpp",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_hlcpp",
    "//sdk/fidl/fuchsia.ui.input:fuchsia.ui.input_hlcpp",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_hlcpp",
    "//sdk/fidl/fuchsia.ui.views:fuchsia.ui.views_hlcpp",
    "//sdk/fidl/fuchsia.vulkan.loader:fuchsia.vulkan.loader_hlcpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/testing/util",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_test_component("carnelian_pixel_test_component") {
  manifest = "meta/carnelian_pixel_test.cml"
  deps = [
    ":carnelian_pixel_test_bin",
    "carnelian_pixel_client:static_square_component",

    # Include some default config (it doesn't matter which, since we're going to override it).
    "//src/ui/testing/test_ui_stack:flatland_scene_manager_config",
    "//src/ui/testing/test_ui_stack:test-ui-stack-component",
  ]
  test_type = "system"
}

fuchsia_test_package("carnelian_pixel_test") {
  test_components = [ ":carnelian_pixel_test_component" ]

  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = vulkan_envs
  }
}
