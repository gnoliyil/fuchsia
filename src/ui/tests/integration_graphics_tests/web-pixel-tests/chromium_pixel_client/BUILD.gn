# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

executable("bin") {
  testonly = true
  output_name = "chromium_pixel_client_bin"
  sources = [ "chromium_pixel_client.cc" ]
  deps = [
    ":chromium_pixel_client_config",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_hlcpp",
    "//sdk/fidl/fuchsia.web:fuchsia.web_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/chromium/web_engine/meta/shards:web_engine_feature_network",
    "//src/chromium/web_engine/meta/shards:web_engine_view",
    "//src/lib/fsl",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

# Required for structured config.
fuchsia_component_manifest("chromium_pixel_client") {
  testonly = true
  component_name = "chromium_pixel_client"
  manifest = "meta/chromium_pixel_client.cml"
}

fuchsia_structured_config_cpp_elf_lib("chromium_pixel_client_config") {
  testonly = true
  cm_label = ":chromium_pixel_client"
}

fuchsia_structured_config_values("chromium_pixel_client_default_config") {
  testonly = true
  cm_label = ":chromium_pixel_client"
  values = {
    html = ""

    # Determines whether Chrome should use vulkan icd provided by fuchsia. When false, Chrome falls
    # back to swiftshader.
    use_vulkan = true
  }
}

fuchsia_component("chromium_pixel_client_component") {
  testonly = true
  cm_label = ":chromium_pixel_client"
  deps = [ ":bin" ]
}

fuchsia_package("package") {
  testonly = true
  package_name = "chromium_pixel_client"
  deps = [ ":chromium_pixel_client_component" ]
}
