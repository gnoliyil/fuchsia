# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fidl/fidl.gni")

# Most targets in this file are implementation details. Make them
# inaccessible outside this file.
visibility = [ ":*" ]

###
### The actual test binary, generated from C++ source.
###

executable("starnix-touch-test-bin") {
  testonly = true
  sources = [
    "relay-api.h",
    "starnix-touch-test.cc",
  ]
  output_name = "starnix-touch-test"
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_cpp",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_cpp",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
    "//sdk/fidl/fuchsia.process:fuchsia.process_cpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_cpp",
    "//sdk/fidl/fuchsia.ui.app:fuchsia.ui.app_cpp",
    "//sdk/fidl/fuchsia.ui.composition:fuchsia.ui.composition_cpp",
    "//sdk/fidl/fuchsia.ui.display.singleton:fuchsia.ui.display.singleton_cpp",
    "//sdk/fidl/fuchsia.ui.input:fuchsia.ui.input_cpp",
    "//sdk/fidl/fuchsia.ui.pointer:fuchsia.ui.pointer_cpp",
    "//sdk/fidl/fuchsia.ui.test.input:fuchsia.ui.test.input_cpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/testing/util",  # for portable-ui-test
    "//third_party/googletest:gtest",
  ]
}

###
### The Fuchsia component containing the test binary.
###

fuchsia_test_component("starnix-touch-test-component") {
  testonly = true
  manifest = "meta/starnix-touch-test.cml"
  deps = [ ":starnix-touch-test-bin" ]
  test_type = "system"
}

###
### Targets that are visible outside this file.
###

# The Fuchsia package containing the test component and necessary subpackages.
fuchsia_test_package("starnix-touch-test") {
  test_components = [ ":starnix-touch-test-component" ]

  # Must be visible to universe, since `fx test` will suggest adding this target
  # to universe, if not already present.
  visibility += [ "//:developer_universe_packages" ]
  deps = [
    "//src/cobalt/bin/testing/mock_cobalt",
    "//src/connectivity/network:netstack-for-tests",
    "//src/developer/build_info/testing:fake-build-info-component",
    "//src/developer/memory/monitor:component",
    "//src/fonts:font_provider_hermetic_for_test",
    "//src/fonts:font_provider_hermetic_for_test_config_values",
    "//src/testing/fidl/intl_property_manager:intl_property_manager_component",
    "//src/ui/bin/text:text_manager_comp",

    # Include some default config (it doesn't matter which, since we're going to override it).
    "//src/ui/testing/test_ui_stack:flatland_scene_manager_config",

    # Remains deps are unrelated to the previous comment.
    "ui-client:component",
    "//src/ui/testing/test_ui_stack:test-ui-stack-component",
  ]
  subpackages = [ "//src/starnix/kernel:starnix_kernel_package" ]
  test_specs = {
    log_settings = {
      # TODO(https://fxbug.dev/93081): Investigate removal of allowing ERROR severity.
      max_severity = "ERROR"
    }
    environments = [
      {
        dimensions = {
          # Ensure the device has Vulkan.
          device_type = "AEMU"
        }
      },
    ]
  }
}

# Alias for the test package, which follows the naming convention expected for
# tests referenced from static BUILD files.
group("tests") {
  testonly = true
  visibility += [ "*" ]
  deps = [ ":starnix-touch-test" ]
}
