# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/icu/icu_fuchsia_component.gni")
import("//build/icu/icu_fuchsia_package.gni")
import("//build/icu/icu_rustc_binary.gni")

group("input-pipeline") {
  testonly = true
  deps = [
    ":pkg",
    ":tests",
  ]
}

group("tests") {
  testonly = true
  deps = [ ":input-pipeline-tests" ]
}

icu_rustc_binary("input-pipeline-bin") {
  output_name = "input-pipeline"
  with_unit_tests = true
  edition = "2021"

  icu_deps = [ "//src/ui/lib/input_pipeline:input_pipeline" ]
  deps = [
    ":input_config_lib",
    "//sdk/fidl/fuchsia.factory:fuchsia.factory_rust",
    "//sdk/fidl/fuchsia.input.injection:fuchsia.input.injection_rust",
    "//sdk/fidl/fuchsia.input.interaction:fuchsia.input.interaction_rust",
    "//sdk/fidl/fuchsia.input.interaction.observation:fuchsia.input.interaction.observation_rust",
    "//sdk/fidl/fuchsia.lightsensor:fuchsia.lightsensor_rust",
    "//sdk/fidl/fuchsia.recovery.policy:fuchsia.recovery.policy_rust",
    "//sdk/fidl/fuchsia.recovery.ui:fuchsia.recovery.ui_rust",
    "//sdk/fidl/fuchsia.settings:fuchsia.settings_rust",
    "//sdk/fidl/fuchsia.ui.brightness:fuchsia.ui.brightness_rust",
    "//sdk/fidl/fuchsia.ui.policy:fuchsia.ui.policy_rust",
    "//sdk/fidl/fuchsia.ui.scenic:fuchsia.ui.scenic_rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/syslog/rust:syslog",
    "//src/lib/trace/rust:trace-provider",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde_json",
  ]

  sources = [ "src/main.rs" ]
}

# Declare the component manifest separately for shared
# reference in component and configuration definitions.
fuchsia_component_manifest("manifest") {
  component_name = "input-pipeline"
  manifest = "meta/input-pipeline.cml"
}

icu_fuchsia_component("component") {
  cm_label = ":manifest"
  icu_deps = [ ":input-pipeline-bin" ]
}

# TODO(fxbug.dev/106573): Remove test-only target when
# default config values are available.
group("component-for-test") {
  testonly = true
  deps = [
    ":component",
    ":config_for_test",
  ]
}

# Standard package containing input-pipeline.cm.
icu_fuchsia_package("pkg") {
  package_name = "input-pipeline"
  icu_deps = [ ":component" ]
}

fuchsia_unittest_package("input-pipeline-tests") {
  deps = [ ":input-pipeline-bin_test" ]
}

# Define library used to read runtime configuration
# values from structured configuration, for inclusion
# as a dependency in :input-pipeline-bin.
fuchsia_structured_config_rust_lib("input_config_lib") {
  cm_label = ":manifest"
}

# Define structured configuration values for testing.
fuchsia_structured_config_values("config_for_test") {
  testonly = true
  cm_label = ":manifest"
  values = {
    idle_threshold_minutes = 15
  }
}
