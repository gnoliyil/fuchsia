# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true
  public_deps = [
    ":focus_dispatcher_unittests",
    ":virtual_keyboard_tests",
  ]
}

fuchsia_unittest_component("focus_dispatcher_unittests") {
  deps = [ ":focus_dispatcher_unittests_bin" ]
}

fuchsia_unittest_component("virtual_keyboard_unittests") {
  deps = [ ":virtual_keyboard_unittests_bin" ]
}

fuchsia_test_package("virtual_keyboard_tests") {
  test_components = [
    ":virtual_keyboard_unittests",
    ":focus_dispatcher_unittests",
  ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
    environments = [ nuc_env ]
  }
}

executable("virtual_keyboard_unittests_bin") {
  testonly = true
  output_name = "virtual_keyboard_unittests"

  sources = [
    "virtual_keyboard_controller_unittest.cc",
    "virtual_keyboard_coordinator_unittest.cc",
    "virtual_keyboard_fidl_test.cc",
    "virtual_keyboard_manager_unittest.cc",
  ]

  deps = [
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/bin/root_presenter:lib",
    "//src/ui/bin/root_presenter:virtual_keyboard",
  ]
}

executable("focus_dispatcher_unittests_bin") {
  testonly = true
  output_name = "focus_dispatcher_unittests"

  sources = [ "focus_dispatcher_unittest.cc" ]

  deps = [
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/sys/cpp/testing:integration_fixture",
    "//sdk/lib/sys/cpp/testing:unit",
    "//sdk/lib/syslog/cpp",
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture",
    "//src/ui/bin/root_presenter:lib",
    "//src/ui/bin/root_presenter/tests/fakes",
  ]
}
