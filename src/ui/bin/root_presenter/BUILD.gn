# Copyright 2015 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")

fuchsia_component("virtual_keyboard_component") {
  component_name = "virtual_keyboard_manager"
  manifest = "meta/virtual_keyboard_manager.cml"
  deps = [ ":bin" ]
}

fuchsia_package("virtual_keyboard_manager") {
  deps = [ ":virtual_keyboard_component" ]
}

group("tests") {
  testonly = true

  deps = [ "//src/ui/bin/root_presenter/tests" ]
}

executable("bin") {
  output_name = "virtual_keyboard_manager"

  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//src/lib/fxl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

source_set("lib") {
  sources = [
    "app.cc",
    "app.h",
  ]

  public_deps = [
    ":focus_dispatcher",
    "//sdk/fidl/fuchsia.input.virtualkeyboard:fuchsia.input.virtualkeyboard_hlcpp",
    "//sdk/fidl/fuchsia.ui.keyboard.focus:fuchsia.ui.keyboard.focus_hlcpp",
    "//sdk/lib/sys/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
  ]

  deps = [
    ":virtual_keyboard",
    "//sdk/lib/fidl/cpp",
  ]
}

source_set("focus_dispatcher") {
  sources = [
    "focus_dispatcher.cc",
    "focus_dispatcher.h",
    "focus_listener.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.ui.focus:fuchsia.ui.focus_hlcpp",
    "//sdk/fidl/fuchsia.ui.keyboard.focus:fuchsia.ui.keyboard.focus_hlcpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
  ]
}

source_set("virtual_keyboard") {
  sources = [
    "virtual_keyboard_controller.cc",
    "virtual_keyboard_controller.h",
    "virtual_keyboard_coordinator.cc",
    "virtual_keyboard_coordinator.h",
    "virtual_keyboard_manager.cc",
    "virtual_keyboard_manager.h",
  ]

  deps = [
    ":focus_dispatcher",
    "//sdk/fidl/fuchsia.input.virtualkeyboard:fuchsia.input.virtualkeyboard_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl",
  ]
}
