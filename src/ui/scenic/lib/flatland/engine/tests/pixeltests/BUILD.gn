# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/board.gni")

group("pixeltests") {
  testonly = true

  public_deps = [
    ":pixeltests_with_fake_display",
    ":pixeltests_with_real_display",
  ]
}

config("engine_pixeltest_config") {
  defines = []
  if (board_name == "astro") {
    defines += [ "PLATFORM_ASTRO" ]
  } else if (board_name == "sherlock") {
    defines += [ "PLATFORM_SHERLOCK" ]
  } else if (board_name == "nelson") {
    defines += [ "PLATFORM_NELSON" ]
  } else if (board_name == "vim3") {
    defines += [ "PLATFORM_VIM3" ]
  }
}

executable("pixeltests_with_real_display") {
  output_name = "flatland_display_compositor_pixeltests"
  testonly = true
  deps = [ ":pixeltest_lib" ]
  sources = [ "display_compositor_pixeltest.cc" ]
  public_configs = [ ":engine_pixeltest_config" ]
}

config("engine_pixeltest_with_fake_display_config") {
  defines = [ "FAKE_DISPLAY" ]
}

executable("pixeltests_with_fake_display") {
  output_name = "flatland_display_compositor_pixeltests_with_fake_display"
  testonly = true
  deps = [ ":pixeltest_lib" ]
  sources = [ "display_compositor_pixeltest.cc" ]
  public_configs = [ ":engine_pixeltest_with_fake_display_config" ]
}

source_set("pixeltest_lib") {
  testonly = true

  sources = [
    "//src/ui/scenic/lib/flatland/engine/tests/common.cc",
    "//src/ui/scenic/lib/flatland/engine/tests/common.h",

    # Registers a global Escher environment for testing.
    "run_all_unittests.cc",
  ]

  public_deps = [
    "//sdk/lib/ui/scenic/cpp:cpp",
    "//src/graphics/display/lib/coordinator-getter:client",
    "//src/lib/fsl:fsl",
    "//src/lib/testing/loop_fixture:gtest",
    "//src/ui/lib/escher/test/common:gtest_escher",
    "//src/ui/scenic/lib/display:display",
    "//src/ui/scenic/lib/flatland/engine:engine_host_accessible_framebuffers",
    "//src/ui/scenic/lib/scheduling:frame_scheduler",
    "//third_party/googletest:gmock",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/zircon-internal",
  ]
}
