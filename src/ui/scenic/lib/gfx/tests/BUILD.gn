# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("tests") {
  testonly = true
  public_deps = [ ":unittests" ]
}

config("vulkan_no_nodiscard") {
  defines = [ "VULKAN_HPP_NO_NODISCARD_WARNINGS" ]
}

source_set("gfx_tests_main") {
  testonly = true
  sources = [ "run_all_unittests.cc" ]
  deps = [ "//src/ui/lib/escher/test/common:gtest_escher" ]
  public_deps = [ "//third_party/googletest:gtest" ]
}

source_set("testing_deps") {
  testonly = true
  sources = [
    "error_reporting_test.cc",
    "error_reporting_test.h",
  ]
  public_deps = [
    "//sdk/lib/ui/scenic/cpp",
    "//src/lib/fxl",
    "//src/lib/testing/loop_fixture",
    "//src/ui/scenic/lib/gfx",
    "//src/ui/scenic/lib/scenic",
    "//src/ui/scenic/lib/scenic/util",
    "//src/ui/scenic/lib/utils",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]
}

executable("unittests") {
  output_name = "gfx_unittests"

  testonly = true
  sources = [
    "escher_vulkan_smoke_test.cc",
    "object_linker_unittest.cc",
  ]
  deps = [
    ":gfx_tests_main",
    ":testing_deps",
    "//src/lib/fsl",
    "//src/ui/lib/escher/test/common:gtest_vulkan",
    "//src/ui/scenic/lib/gfx:object_linker",
  ]

  include_dirs = [ "//src/ui/lib/escher" ]

  # TODO(https://fxbug.dev/58162): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-conversion" ]

  configs += [ ":vulkan_no_nodiscard" ]
}
