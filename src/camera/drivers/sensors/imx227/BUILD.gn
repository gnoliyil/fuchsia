# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")

group("tests") {
  testonly = true
  deps = [
    ":imx227_bind_test",
    ":imx227_unittest",
  ]
}

driver_bind_rules("imx227_bind") {
  rules = "imx227.bind"
  tests = "bind_tests.json"

  # TODO(fxb/115757): Remove this once we no longer need to generate
  # fragments for composite bind rules.
  disable_fragment_gen = true
  deps = [
    "//src/devices/bind/fuchsia.hardware.clock",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.i2c",
    "//src/devices/bind/fuchsia.mipicsi",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sony.platform",
  ]
}

source_set("lib") {
  sources = [
    "imx227.cc",
    "imx227_otp.cc",
    "imx227_protocol.cc",
  ]

  deps = [
    ":imx227_bind",
    "//sdk/banjo/fuchsia.hardware.camera:fuchsia.hardware.camera_banjo_cpp",
    "//sdk/lib/syslog/cpp",
    "//src/devices/lib/driver",
    "//src/lib/ddk:ddk-metadata-headers",
    "//zircon/system/ulib/fzl",
    "//zircon/third_party/ulib/safemath",
  ]

  public_deps = [
    "//sdk/banjo/fuchsia.hardware.camera.sensor:fuchsia.hardware.camera.sensor_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.mipicsi:fuchsia.hardware.mipicsi_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.clock:fuchsia.hardware.clock_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.gpio:fuchsia.hardware.gpio_cpp_wire",
    "//sdk/lib/fit-promise",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/i2c/lib/device-protocol-i2c-channel",
    "//zircon/system/ulib/driver-unit-test",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_driver("fuchsia_driver") {
  output_name = "imx227"

  sources = [ "imx227_hardware_test.cc" ]

  deps = [
    ":lib",
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_hlcpp",
    "//sdk/lib/syslog/cpp:backend",
  ]
}

fuchsia_driver_component("driver") {
  component_name = "imx227"
  deps = [ ":fuchsia_driver" ]
  info = "driver-info.json"
  manifest = "meta/imx227.cml"
}

fuchsia_driver_package("imx227") {
  driver_components = [ ":driver" ]
}

executable("imx227_unittest_bin") {
  testonly = true
  sources = [ "imx227_unittest.cc" ]
  deps = [
    ":lib",
    "//sdk/banjo/fuchsia.hardware.mipicsi:fuchsia.hardware.mipicsi_banjo_cpp_mock",
    "//sdk/fidl/fuchsia.hardware.i2c:fuchsia.hardware.i2c_cpp_wire",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/gpio/testing/fake-gpio",
    "//src/devices/i2c/testing/mock-i2c",
    "//src/devices/testing/mock-ddk",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("imx227_unittest") {
  deps = [ ":imx227_unittest_bin" ]
}
