# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

import("//build/components.gni")
import("//build/rust/rustc_test.gni")
import("//src/storage/testing/driver_test_realm.gni")

group("storage_manager_integration") {
  testonly = true
  deps = [ ":storage-manager-integration-test-pkg" ]
}

rustc_test("test") {
  name = "storage_manager_integration_test"
  edition = "2018"

  source_root = "tests/lib.rs"

  sources = [
    "tests/fxfs.rs",
    "tests/lib.rs",
    "tests/storage_manager.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.identity.account:fuchsia.identity.account_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/identity/lib/account_common",
    "//src/identity/lib/storage_manager",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-fs",
    "//src/lib/storage/fs_management/rust:fs_management",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:rand",
  ]
  configs += [ "//build/config/rust/lints:clippy_warn_all" ]
}

storage_driver_test_realm_v2_component("storage_manager_integration_test") {
  component_name = "storage_manager_integration_test"
  manifest = "meta/storage_manager_integration_test.cml"
  deps = [
    ":test",
    "//src/storage/fxfs:fxfs_component",
    "//src/storage/fxfs-crypt",
  ]
}

fuchsia_test_package("storage-manager-integration-test-pkg") {
  test_components = [ ":storage_manager_integration_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
