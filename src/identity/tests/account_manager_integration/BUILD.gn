# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
#

import("//build/components.gni")
import("//build/rust/rustc_test.gni")

group("account_manager_integration") {
  testonly = true
  deps = [ ":account_manager_integration_test" ]
}

rustc_test("test") {
  name = "account_manager_integration_test"
  edition = "2021"

  source_root = "tests/lib.rs"

  deps = [
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_rust",
    "//sdk/fidl/fuchsia.hardware.block.partition:fuchsia.hardware.block.partition_rust",
    "//sdk/fidl/fuchsia.identity.account:fuchsia.identity.account_rust",
    "//sdk/fidl/fuchsia.identity.authentication:fuchsia.identity.authentication_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.logger:fuchsia.logger_rust",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2_rust",
    "//sdk/fidl/fuchsia.tracing.provider:fuchsia.tracing.provider_rust",
    "//sdk/lib/driver_test_realm/realm_builder/rust",
    "//src/identity/testing/ramdisk_common",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component-test",
    "//src/lib/storage/ramdevice_client",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:futures",
  ]

  sources = [
    "tests/account.rs",
    "tests/lib.rs",
  ]
}

fuchsia_test_component("account_manager_integration_test_component") {
  testonly = true
  manifest = "meta/account_manager_integration_test.cml"
  deps = [
    ":test",
    "//src/devices/bus/drivers/platform",
    "//src/storage/testing:storage_drivers",
  ]
}

fuchsia_test_package("account_manager_integration_test") {
  # isolated devmgr sometimes logs at error level when its processes are torn
  # down in unlucky orders, and there's no way to tear them down
  # deterministically, so I guess we tolerate ERROR level log messages
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }

  deps = [
    "//src/identity/bin/account_handler:account_handler_ephemeral_for_test",
    "//src/identity/bin/account_handler:account_handler_persistent_for_test",
    "//src/identity/bin/account_manager:account_manager_for_test_component",
    "//src/identity/bin/account_manager:test-config",
    "//src/identity/bin/test_authenticator:test_authenticator_component",
    "//src/storage/bin/minfs",
  ]
  test_components = [ ":account_manager_integration_test_component" ]
}
