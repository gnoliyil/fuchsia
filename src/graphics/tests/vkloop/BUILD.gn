# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

executable("vkloop_bin") {
  testonly = true
  output_name = "vkloop"

  sources = [
    "main.cc",
    "vkloop.cc",
  ]

  deps = [
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp",
    "//sdk/lib/magma_client:magma_headers",
    "//src/graphics/lib/magma/src/libmagma",
    "//src/graphics/magma/lib/magma_client/test_util:test_device_helper",
    "//src/graphics/tests/common",
  ]
  public_deps = [
    "//src/lib/fxl/test:test_settings",
    "//third_party/googletest:gtest",
  ]

  public_deps += [ "//src/lib/vulkan" ]
}

resource("shaders") {
  sources = [ "./vkloop.spv" ]
  outputs = [ "data/{{source_file_part}}" ]
}

fuchsia_test_component("vkloop_cmp") {
  manifest = "meta/vkloop.cml"
  component_name = "vkloop_test"
  deps = [
    ":shaders",
    ":vkloop_bin",
    "//src/lib/vulkan/validation_layers",
  ]
  test_type = "device"
}

fuchsia_test_package("vkloop-pkg") {
  package_name = "vkloop_test"
  test_components = [ ":vkloop_cmp" ]
  test_specs = {
    environments = magma_libvulkan_hardware_envs
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("vkloop") {
  testonly = true
  deps = [ ":vkloop-pkg" ]
}
