# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

executable("msd_arm_mali_nonhardware_tests") {
  testonly = true

  sources = []

  deps = []

  public_deps = [
    ":tests_not_requiring_hardware",
    "//src/devices/testing/no_ddk",
    "//src/graphics/lib/magma/tests/helper:logger_init_helper",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-vthread",
  ]
}

source_set("tests_not_requiring_hardware") {
  testonly = true

  sources = [
    "non_hardware_test_device.cc",
    "test_address_manager.cc",
    "test_address_space.cc",
    "test_buffer.cc",
    "test_connection.cc",
    "test_job_scheduler.cc",
    "test_performance_counters.cc",
    "test_power_manager.cc",
    "test_region.cc",
    "test_timestamp.cc",
  ]

  deps = [
    ":test_deps",
    "//sdk/lib/driver/testing/cpp",
    "//sdk/lib/fdio",
    "//src/graphics/lib/magma/tests/helper:msd_stubs",
    "//src/graphics/lib/magma/tests/mock:bus_mapper",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

executable("msd_arm_mali_dfv2_tests") {
  testonly = true
  sources = [ "dfv2_tests.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/testing/fake-resource",
    "//src/graphics/drivers/msd-arm-mali:msd_arm_entry_dfv2",
    "//src/graphics/drivers/msd-arm-mali/src",
    "//src/graphics/lib/magma/src/magma_util:registers",
    "//src/lib/fxl/test:gtest_main",
  ]
}

# These tests require ARM Mali graphics hardware to run.  They can be
# run inside the test build of the driver.
source_set("tests_requiring_hardware") {
  testonly = true

  sources = [
    "test_command_buffer.cc",
    "test_device.cc",
  ]

  deps = [ ":test_deps" ]
}

group("test_deps") {
  testonly = true

  public_deps = [
    "../../include",
    "//src/graphics/drivers/msd-arm-mali/src",
    "//src/graphics/lib/magma/include:msd",
    "//src/graphics/lib/magma/src/magma_util:macros",
    "//src/graphics/lib/magma/src/magma_util:registers",
    "//src/graphics/lib/magma/src/magma_util/platform:port",
    "//src/graphics/lib/magma/src/magma_util/platform:semaphore",
    "//src/graphics/lib/magma/src/sys_driver",
    "//src/graphics/lib/magma/tests/helper:platform_msd_device_helper",
    "//src/graphics/lib/magma/tests/mock:mmio",
    "//third_party/googletest:gtest",
  ]
}
