# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

executable("msd_arm_mali_nonhardware_tests") {
  testonly = true

  sources = []

  deps = []

  public_deps = [
    ":tests_not_requiring_hardware",
    "//src/devices/testing/no_ddk",
    "//src/graphics/magma/lib/magma/test_util:logger_init_helper",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-vthread",
  ]
}

# Tests that run in both DFv1 and DFv2
source_set("common_non_hardware_tests") {
  testonly = true
  sources = [
    "driver_logger_harness.h",
    "non_hardware_test_device.cc",
    "test_address_manager.cc",
    "test_address_space.cc",
    "test_buffer.cc",
    "test_connection.cc",
    "test_job_scheduler.cc",
    "test_performance_counters.cc",
    "test_power_manager.cc",
    "test_region.cc",
    "test_timestamp.cc",
  ]
  deps = [
    "../../include",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/runtime/testing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/testing/fake-resource",
    "//src/graphics/drivers/msd-arm-mali/src",
    "//src/graphics/magma/lib/magma/platform:buffer",
    "//src/graphics/magma/lib/magma/platform:port",
    "//src/graphics/magma/lib/magma_service/mock:bus_mapper",
    "//src/graphics/magma/lib/magma_service/mock:mmio",
    "//src/graphics/magma/lib/magma_service/test_util:msd_stubs",
    "//src/graphics/magma/lib/magma_service/util:registers",
    "//third_party/googletest:gtest",
  ]
}

source_set("tests_not_requiring_hardware") {
  testonly = true

  sources = [ "driver_logger_harness_stub.cc" ]

  deps = [
    ":common_non_hardware_tests",
    ":test_deps",
    "//sdk/lib/driver/testing/cpp",
    "//sdk/lib/fdio",
    "//src/graphics/magma/lib/magma_service/mock:bus_mapper",
    "//src/graphics/magma/lib/magma_service/test_util:msd_stubs",
    "//zircon/system/ulib/sync:sync-cpp",
  ]
}

executable("msd_arm_mali_lifecycle_tests") {
  testonly = true
  sources = [
    "driver_logger_harness_dfv2.cc",
    "lifecycle_tests.cc",
  ]
  deps = [
    ":common_non_hardware_tests",
    "../../include",
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_cpp_testing",
    "//sdk/lib/async_patterns/testing/cpp",
    "//sdk/lib/driver/runtime/testing/cpp",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/bus/testing/fake-pdev",
    "//src/devices/testing/fake-resource",
    "//src/graphics/drivers/msd-arm-mali:msd_arm_entry",
    "//src/graphics/drivers/msd-arm-mali/src",
    "//src/graphics/magma/lib/magma/platform/zircon:logger_for_dfv2",
    "//src/lib/fxl/test:gtest_main",
  ]
}

# These tests require ARM Mali graphics hardware to run.  They can be
# run inside the test build of the driver.
source_set("tests_requiring_hardware") {
  testonly = true

  sources = [
    "test_command_buffer.cc",
    "test_device.cc",
  ]

  deps = [ ":test_deps" ]
}

group("test_deps") {
  testonly = true

  public_deps = [
    "../../include",
    "//src/graphics/drivers/msd-arm-mali/src",
    "//src/graphics/magma:msd",
    "//src/graphics/magma/lib/magma/platform:port",
    "//src/graphics/magma/lib/magma/platform:semaphore",
    "//src/graphics/magma/lib/magma/util:macros",
    "//src/graphics/magma/lib/magma_service/mock:mmio",
    "//src/graphics/magma/lib/magma_service/sys_driver",
    "//src/graphics/magma/lib/magma_service/test_util:platform_msd_device_helper",
    "//src/graphics/magma/lib/magma_service/util:registers",
    "//third_party/googletest:gtest",
  ]
}
