# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

fuchsia_driver("msd_arm-driver") {
  output_name = "libmsd_arm"

  deps = [
    ":msd_arm_entry",
    "src",
    "//src/graphics/lib/magma/src/magma_util/platform:logger_for_system_drivers",
  ]
}

fuchsia_driver_component("msd_arm") {
  component_name = "libmsd_arm"
  deps = [ ":msd_arm-driver" ]
  info = "msd_arm-info.json"
  colocate = true
  root_resource = true
  uses_profiles = true
}

fuchsia_driver("msd_arm_test-driver") {
  testonly = true
  output_name = "libmsd_arm_test"
  assert_no_deps = [ "//src/graphics/lib/magma/src/sys_driver" ]

  deps = [
    ":msd_arm_test_entry",
    "src",
    "tests/unit_tests:tests_requiring_hardware",
    "//src/graphics/lib/magma/src/magma_util/platform:logger_for_system_drivers",
    "//src/graphics/lib/magma/tests/unit_tests:magma_platform_hardware_tests",
    "//src/graphics/lib/magma/tests/unit_tests:msd_conformance_tests_cc",
  ]
}

fuchsia_driver_component("msd_arm_test") {
  testonly = true
  component_name = "libmsd_arm_test"
  deps = [ ":msd_arm_test-driver" ]
  info = "msd_arm_test-info.json"
  colocate = true
}

driver_bind_rules("msd_arm_bind") {
  rules = "msd_arm.bind"
  header_output = "bind.h"
  tests = "tests/bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.arm.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("msd_arm_test_bind") {
  rules = "msd_arm_test.bind"
  header_output = "test_bind.h"
  disable_autobind = true
  deps = [
    "//src/devices/bind/fuchsia.arm.platform",
    "//src/devices/bind/fuchsia.compat",
    "//src/devices/bind/fuchsia.platform",
  ]
}

entry_deps = [
  "//sdk/banjo/fuchsia.hardware.platform.device:fuchsia.hardware.platform.device_banjo_cpp",
  "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
  "//src/devices/lib/driver",
  "//src/graphics/lib/magma/src/magma_util:macros",
  "//src/graphics/lib/magma/src/magma_util/platform:logger",
  "//src/graphics/lib/magma/src/magma_util/platform:trace",
  "//src/graphics/lib/magma/src/magma_util/platform:trace_provider",
  "//src/graphics/lib/magma/src/magma_util/platform:trace_provider_with_fdio",
  "//src/graphics/lib/magma/src/magma_util/platform/zircon:status",
  "//src/graphics/lib/magma/src/magma_util/platform/zircon:thread_profile",
  "//src/graphics/lib/magma/src/sys_driver_cpp",
  "//src/graphics/lib/magma/src/sys_driver_cpp:device_impl",
  "//src/lib/ddk",
  "//src/lib/ddktl",
  "//zircon/system/ulib/zx",
]

source_set("msd_arm_entry") {
  sources = [ "src/driver_entry.cc" ]
  deps = entry_deps + [ ":msd_arm_bind" ]
}

source_set("msd_arm_test_entry") {
  assert_no_deps = [ "//src/graphics/lib/magma/src/sys_driver" ]
  testonly = true
  sources = [ "src/driver_entry.cc" ]
  defines = [ "MAGMA_TEST_DRIVER=1" ]
  deps = entry_deps + [
           ":msd_arm_test_bind",
           "//src/graphics/lib/magma/src/magma_util/platform/zircon:pdev_driver_gtest",
         ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":msd_arm_bind_test",
    "tests",
  ]
}

group("msd-system") {
  deps = [ ":msd-arm-mali" ]
}

group("msd-config") {
  deps = [ "//src/diagnostics/config/triage:mali-detect" ]
}

fuchsia_driver_package("msd-arm-mali") {
  driver_components = [ ":msd_arm" ]
}

fuchsia_driver_package("msd-arm-mali-test") {
  testonly = true
  driver_components = [ ":msd_arm_test" ]
  deps = [ "//src/graphics/lib/magma/tests/unit_tests:test_firmware" ]
}

group("test-driver") {
  testonly = true
  deps = [ ":msd-arm-mali-test" ]
}
