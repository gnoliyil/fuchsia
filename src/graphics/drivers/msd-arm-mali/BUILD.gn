# Copyright 2017 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

fuchsia_driver("msd_arm-driver") {
  output_name = "libmsd_arm"
  assert_no_deps = [ "//zircon/system/ulib/trace-engine:trace-engine-static" ]

  deps = [
    ":msd_arm_entry",
    "src",
    "//src/graphics/lib/magma/src/magma_util/platform:logger_for_system_drivers",
  ]
}

fuchsia_driver_component("msd_arm") {
  component_name = "libmsd_arm"
  deps = [
    ":msd_arm-driver",
    ":msd_arm_bind",
  ]
  info = "msd_arm-info.json"
  manifest = "meta/msd_arm.cml"
}

# The driver framework can't use the initial composite bind rules when rebinding a driver, so the
# "rebind" driver has different bind rules so it can be bound the second time.
# TODO(fxbug.dev/124976): Unify rebind and production drivers.
fuchsia_driver_component("msd_arm_rebind") {
  component_name = "libmsd_arm_rebind"
  deps = [
    ":msd_arm-driver",
    ":msd_arm_rebind_bind",
  ]
  info = "msd_arm-info.json"
  manifest = "meta/msd_arm_rebind.cml"
}

fuchsia_driver("msd_arm_test-driver") {
  testonly = true
  output_name = "libmsd_arm_test"
  assert_no_deps = [
    "//src/graphics/lib/magma/src/sys_driver",
    "//zircon/system/ulib/trace-engine:trace-engine-static",
  ]

  deps = [
    ":msd_arm_test_entry",
    "src",
    "tests/unit_tests:tests_requiring_hardware",
    "//src/graphics/lib/magma/src/magma_util/platform:logger_for_system_drivers",
    "//src/graphics/lib/magma/tests/unit_tests:msd_conformance_tests_cc",
  ]
}

fuchsia_driver_component("msd_arm_test") {
  testonly = true
  component_name = "libmsd_arm_test"
  deps = [
    ":msd_arm_test-driver",
    ":msd_arm_test_bind",
  ]
  info = "msd_arm_test-info.json"
  manifest = "meta/msd_arm_test.cml"
}

driver_bind_rules("msd_arm_bind") {
  rules = "msd_arm.bind"
  tests = "tests/bind_tests.json"
  deps = [
    "//src/devices/bind/fuchsia.arm.platform",
    "//src/devices/bind/fuchsia.platform",
  ]
}

driver_bind_rules("msd_arm_rebind_bind") {
  rules = "msd_arm_rebind.bind"
}

driver_bind_rules("msd_arm_test_bind") {
  rules = "msd_arm_test.bind"
  disable_autobind = true
  disable_fragment_gen = true
}

entry_deps = [
  "src:parent_device_dfv1",
  "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
  "//sdk/fidl/fuchsia.hardware.gpu.mali:fuchsia.hardware.gpu.mali_cpp_driver",
  "//sdk/fidl/fuchsia.hardware.gpu.mali:fuchsia.hardware.gpu.mali_cpp_wire",
  "//src/devices/bin/driver_runtime",
  "//src/devices/lib/driver",
  "//src/graphics/lib/magma/src/magma_util:macros",
  "//src/graphics/lib/magma/src/magma_util/platform:logger",
  "//src/graphics/lib/magma/src/magma_util/platform:trace",
  "//src/graphics/lib/magma/src/magma_util/platform/zircon:status",
  "//src/graphics/lib/magma/src/magma_util/platform/zircon:thread_profile",
  "//src/graphics/lib/magma/src/sys_driver_cpp",
  "//src/graphics/lib/magma/src/sys_driver_cpp:device_impl",
  "//src/lib/ddk",
  "//src/lib/ddktl",
  "//zircon/system/ulib/trace",
  "//zircon/system/ulib/trace-vthread",
  "//zircon/system/ulib/zx",
]

source_set("msd_arm_entry") {
  sources = [ "src/driver_entry.cc" ]
  deps = entry_deps + [ "src" ]
}

source_set("msd_arm_test_entry") {
  assert_no_deps = [ "//src/graphics/lib/magma/src/sys_driver" ]
  testonly = true
  sources = [
    "src/driver_entry.cc",
    "src/driver_test_gtest.cc",
  ]
  defines = [ "MAGMA_TEST_DRIVER=1" ]
  deps = entry_deps + [
           "src",
           "//src/graphics/lib/magma/tests/helper:platform_msd_device_helper",
           "//third_party/googletest:gtest",
         ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":msd_arm_bind_test",
    "tests",
  ]
}

group("msd-system") {
  deps = [ ":msd-arm-mali" ]
}

group("msd-config") {
  deps = [ "//src/diagnostics/config/triage:mali-detect" ]
}

fuchsia_driver_package("msd-arm-mali") {
  driver_components = [ ":msd_arm" ]
}

fuchsia_driver_package("msd-arm-mali-test") {
  testonly = true
  driver_components = [
    ":msd_arm_test",
    ":msd_arm_rebind",
  ]
  deps = [ "//src/graphics/lib/magma/tests/unit_tests:test_firmware" ]
}

group("test-driver") {
  testonly = true
  deps = [ ":msd-arm-mali-test" ]
}
