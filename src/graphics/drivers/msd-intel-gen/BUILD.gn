# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

# The gpu device
fuchsia_driver("msd_intel-driver") {
  output_name = "libmsd_intel"

  deps = [
    ":msd_intel_entry",
    "$magma_build_root/src/magma_util/platform:logger_for_system_drivers",
    "src",
  ]

  assert_no_deps = [ "//zircon/system/ulib/trace-engine:trace-engine-static" ]
}

fuchsia_driver_component("msd_intel") {
  component_name = "libmsd_intel"
  deps = [ ":msd_intel-driver" ]
  info = "msd_intel-info.json"
  manifest = "meta/msd_intel.cml"
}

# The gpu device; runs gtests at startup
fuchsia_driver("msd_intel_test-driver") {
  testonly = true

  output_name = "libmsd_intel_test"

  deps = [
    ":msd_intel_test_entry",
    "$magma_build_root/src/magma_util/platform:logger_for_system_drivers",
    "src",
  ]

  assert_no_deps = [ "//zircon/system/ulib/trace-engine:trace-engine-static" ]
}

fuchsia_driver_component("msd_intel_test") {
  testonly = true
  component_name = "libmsd_intel_test"
  deps = [ ":msd_intel_test-driver" ]
  info = "msd_intel_test-info.json"
  manifest = "meta/msd_intel_test.cml"
}

driver_bind_rules("msd_intel_bind") {
  rules = "msd_intel.bind"
  tests = "tests/bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.intel.gpucore" ]
}

driver_bind_rules("msd_intel_test_bind") {
  rules = "msd_intel_test.bind"
  disable_autobind = true
  deps = [ "//src/devices/bind/fuchsia.intel.gpucore" ]
}

source_set("msd_intel_entry") {
  public_configs = [ "$msd_intel_gen_build_root:msd_src_include_config" ]
  sources = [ "src/driver_entry.cc" ]

  deps = [
    ":msd_intel_bind",
    "src",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
    "//src/devices/lib/driver",
    "//src/graphics/lib/magma/src/magma_util:macros",
    "//src/graphics/lib/magma/src/magma_util/platform:trace",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:performance_counter_device",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:status",
    "//src/graphics/lib/magma/src/sys_driver",
    "//src/graphics/lib/magma/src/sys_driver:device_impl",
    "//src/lib/ddk",
    "//src/zircon/lib/zircon",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-vthread",
    "//zircon/system/ulib/zx",
  ]
}

source_set("msd_intel_test_entry") {
  testonly = true

  public_configs = [ "$msd_intel_gen_build_root:msd_src_include_config" ]
  sources = [ "src/driver_entry.cc" ]
  defines = [ "MAGMA_TEST_DRIVER=1" ]

  deps = [
    ":msd_intel_test_bind",
    "src",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
    "//src/devices/lib/driver",
    "//src/graphics/lib/magma/src/magma_util:macros",
    "//src/graphics/lib/magma/src/magma_util/platform:trace",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:status",
    "//src/graphics/lib/magma/src/sys_driver",
    "//src/graphics/lib/magma/src/sys_driver:device_impl",
    "//src/lib/ddk",
    "//src/zircon/lib/zircon",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-vthread",
    "//zircon/system/ulib/zx",
  ]
  deps += [
    "tests/unit_tests:hardware_tests",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:pci_driver_gtest",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:performance_counter_device",
    "//src/graphics/lib/magma/tests/unit_tests:magma_platform_pci_device_tests",
    "//src/graphics/lib/magma/tests/unit_tests:msd_conformance_tests_cc",
  ]
}

# This config allows inclusion of headers via relative paths
config("msd_tests_include_config") {
  include_dirs = [ "tests" ]
}

# This config allows inclusion of headers via relative paths
config("msd_src_include_config") {
  include_dirs = [ "src" ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":msd_intel_bind_test",
    "tests",
  ]
}

fuchsia_driver_package("msd-intel-gen") {
  driver_components = [ ":msd_intel" ]
}

fuchsia_driver_package("msd-intel-gen-test") {
  testonly = true
  driver_components = [ ":msd_intel_test" ]
  deps = [ "//src/graphics/lib/magma/tests/unit_tests:test_firmware" ]
}

group("test-driver") {
  testonly = true
  deps = [ ":msd-intel-gen-test" ]
}
