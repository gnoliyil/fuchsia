# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/compute/gn/build_rules.gni")

source_set("spinel_vk_tests_sources") {
  testonly = true
  sources = [
    "env.cc",
    "env.h",
    "env_spinel_vk_target.cc",
    "env_spinel_vk_target.h",
    "env_vk_device.cc",
    "env_vk_device.h",
    "env_vk_instance.cc",
    "env_vk_instance.h",
    "fxt_spinel_vk.cc",
    "fxt_spinel_vk.h",
    "fxt_spinel_vk_render.cc",
    "fxt_spinel_vk_render.h",
  ]
  deps = [
    "${graphics_compute_dir}/common",
    "${graphics_compute_dir}/common/vk",
    graphics_compute_gtest_target,
  ]
  public_deps = [
    "${graphics_compute_dir}/spinel/ext/svg2spinel",
    "${graphics_compute_dir}/spinel/platforms/vk/ext/find_target",
  ]
}

graphics_compute_unittests_package("spinel-vk-tests-api") {
  needs_vulkan = true
  testonly = true
  sources = [ "spinel_vk_api_unittests.cc" ]
  deps = [ ":spinel_vk_tests_sources" ]
}

graphics_compute_unittests_package("spinel-vk-tests-simple") {
  needs_vulkan = true
  testonly = true
  sources = [ "spinel_vk_simple_unittests.cc" ]
  deps = [ ":spinel_vk_tests_sources" ]
}

graphics_compute_unittests_package("spinel-vk-tests-svg") {
  needs_vulkan = true
  testonly = true
  sources = [ "spinel_vk_svg_unittests.cc" ]
  deps = [ ":spinel_vk_tests_sources" ]
}

group("spinel_vk_tests") {
  testonly = true
  public_deps = [
    ":spinel-vk-tests-api",
    ":spinel-vk-tests-simple",
    ":spinel-vk-tests-svg",
  ]

  #
  # Ensure that a host executable is built on Linux hosts
  #
  if (is_fuchsia && host_os == "linux") {
    data_deps = [
      ":spinel-vk-tests-api($host_toolchain)",
      ":spinel-vk-tests-simple($host_toolchain)",
      ":spinel-vk-tests-svg($host_toolchain)",
    ]
  }
}
