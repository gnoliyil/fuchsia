# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

visibility_list = [
  "//src/graphics/lib/magma/*",
  "//src/graphics/drivers/*",
  "//third_party/imgtec-pvr-rgx-km/*",
]

declare_args() {
  magma_debug = is_debug
}

config("magma_util_config") {
  if (magma_debug) {
    defines = [ "MAGMA_DEBUG_INTERNAL_USE_ONLY=1" ]
  } else {
    defines = [ "MAGMA_DEBUG_INTERNAL_USE_ONLY=0" ]
  }
}

# Deprecated
group("magma_util") {
  public_deps = [ ":macros" ]
}

source_set("address_space") {
  visibility = visibility_list

  sources = [
    "accessor.h",
    "address_space.h",
    "gpu_mapping.h",
  ]
  public_deps = [
    ":macros",
    "platform:buffer",
    "platform:bus_mapper",
  ]
}

source_set("macros") {
  visibility = visibility_list

  public_configs = [
    ":magma_util_config",
    "$magma_build_root:magma_src_include_config",
  ]
  public_deps = [ "platform:logger" ]

  sources = [
    "dlog.h",
    "macros.h",
  ]
}

source_set("status") {
  visibility = visibility_list

  public_configs = [ "$magma_build_root:magma_src_include_config" ]

  sources = [ "status.h" ]

  public_deps = [ "$magma_build_root/include:magma_common" ]
}

source_set("mapped_batch") {
  visibility = visibility_list

  public_configs = [
    ":magma_util_config",
    "$magma_build_root:magma_src_include_config",
  ]

  sources = [
    "command_buffer.h",
    "mapped_batch.h",
  ]

  public_deps = [
    ":address_space",
    "$magma_build_root/include:msd",
    "platform:semaphore",
    "platform:trace",
  ]
}

source_set("thread") {
  visibility = visibility_list

  public_configs = [ "$magma_build_root:magma_src_include_config" ]

  sources = [ "thread.h" ]

  deps = [ "platform:thread" ]
}

source_set("allocator") {
  visibility = visibility_list

  public_configs = [ "$magma_build_root:magma_src_include_config" ]

  sources = [
    "address_space_allocator.h",
    "simple_allocator.cc",
    "simple_allocator.h",
  ]

  public_deps = [
    ":macros",
    "//sdk/lib/fit",
  ]
}

source_set("registers") {
  visibility = visibility_list

  public_configs = [
    ":magma_util_config",
    "$magma_build_root:magma_src_include_config",
  ]

  sources = [
    "register_io.cc",
    "register_io.h",
  ]

  public_deps = [
    ":macros",
    "platform:mmio_header",
  ]
}

source_set("ringbuffer") {
  visibility = visibility_list

  public_configs = [ "$magma_build_root:magma_src_include_config" ]

  sources = [
    "instruction_writer.h",
    "ringbuffer.h",
  ]

  public_deps = [
    ":address_space",
    ":macros",
  ]
}

source_set("short_macros") {
  # The short macros should only be used in leaf code that isn't included into other consumers.
  visibility = [
    "//src/graphics/drivers/*",
    "//src/graphics/lib/magma/src/libmagma:*",
    "//src/graphics/lib/magma/src/libmagma_virt:*",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:*",
    "//src/graphics/lib/magma/src/sys_driver/tests:*",
    "//src/graphics/lib/magma/tests/mock:*",
    "//src/graphics/lib/magma/tests/unit_tests:*",
    "//third_party/imgtec-pvr-rgx-km/*",
  ]
  sources = [ "short_macros.h" ]
  public_deps = [ ":macros" ]
}
