# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/components.gni")
import("//build/testing/environments.gni")

source_set("sources") {
  sources = [
    "mali-performance-counters.cc",
    "mali-performance-counters.h",
  ]

  public_deps = [ "//src/lib/fxl" ]
  deps = [
    "//third_party/github.com/ARM-software/HWCPipe:mali_profiler",
    "//zircon/system/ulib/async-loop",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/trace-engine",
    "//zircon/system/ulib/trace-provider",
  ]

  configs -= [ "//build/config:no_exceptions" ]
}

executable("mali-performance-counters-bin") {
  sources = [ "main.cc" ]
  output_name = "mali-performance-counters"

  deps = [ ":sources" ]

  configs -= [ "//build/config:no_exceptions" ]
}

executable("mali-performance-counters-test-bin") {
  testonly = true
  output_name = "mali-performance-counters-test"
  sources = [ "test.cc" ]

  deps = [
    ":sources",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/fbl",
  ]

  configs -= [ "//build/config:no_exceptions" ]
}

fuchsia_test_component("mali-performance-counters-test-cmp") {
  component_name = "mali-performance-counters-test"
  deps = [ ":mali-performance-counters-test-bin" ]
  manifest = "meta/mali_performance_counters_test.cml"
  test_type = "system"
}

fuchsia_test_package("mali-performance-counters-test") {
  test_components = [ ":mali-performance-counters-test-cmp" ]
  test_specs = {
    environments = [
      sherlock_env,
      astro_env,
      vim3_env,
    ]
  }
}

fuchsia_shell_package("mali-performance-counters-pkg") {
  deps = [ ":mali-performance-counters-bin" ]
}

group("mali-performance-counters") {
  testonly = true
  deps = [ ":mali-performance-counters-pkg" ]
}
