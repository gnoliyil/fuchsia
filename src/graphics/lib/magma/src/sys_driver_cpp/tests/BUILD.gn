# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/graphics/lib/magma/gnbuild/magma.gni")

source_set("magma_system_tests") {
  testonly = true

  sources = [
    "test_magma_driver.cc",
    "test_magma_system_buffer.cc",
    "test_magma_system_connection.cc",
    "test_magma_system_context.cc",
    "test_magma_system_device.cc",
    "test_notification_handler.cc",
    "test_zircon_connection.cc",
  ]
  assert_no_deps = [ "//src/graphics/lib/magma/src/sys_driver" ]

  deps = [
    "$magma_build_root/src/magma_util:macros",
    "$magma_build_root/src/magma_util:short_macros",
    "$magma_build_root/src/magma_util/platform:connection_client",
    "$magma_build_root/src/magma_util/platform:event",
    "$magma_build_root/src/sys_driver_cpp",
    "$magma_build_root/tests/helper:command_buffer_helper_cpp",
    "$magma_build_root/tests/helper:logger_init_helper",
    "$magma_build_root/tests/helper:platform_device_helper",
    "$magma_build_root/tests/mock:msd_cpp",
    "//sdk/lib/fit",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/sync:sync-cpp",
  ]

  if (is_fuchsia) {
    deps += [ "$magma_build_root/src/magma_util/platform/zircon:buffer" ]
  }
}

executable("magma_system_dfv2_test_executable") {
  testonly = true
  output_name = "magma_system_dfv2_test"
  sources = [ "test_magma_driver_base.cc" ]

  deps = [
    "$magma_build_root/src/sys_driver_cpp:magma_driver_base",
    "$magma_build_root/tests/mock:msd_cpp",
    "//sdk/lib/driver/runtime/testing/loop_fixture:gtest",
    "//sdk/lib/driver/testing/cpp",
    "//src/devices/testing/driver-runtime-main:gtest",
    "//zircon/system/ulib/trace",
  ]
}

executable("magma_sys_driver_cpp_test_executable") {
  testonly = true
  output_name = "magma_sys_driver_cpp_test"
  deps = [
    ":magma_system_tests",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("magma_sys_driver_cpp_test") {
  deps = [ ":magma_sys_driver_cpp_test_executable" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_unittest_package("magma_system_dfv2_test") {
  deps = [ ":magma_system_dfv2_test_executable" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

group("tests") {
  testonly = true
  deps = [
    ":magma_sys_driver_cpp_test",
    ":magma_system_dfv2_test",
  ]
}
