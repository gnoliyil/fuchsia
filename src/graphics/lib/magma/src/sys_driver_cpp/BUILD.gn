# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/graphics/lib/magma/gnbuild/magma.gni")

source_set("sys_driver_cpp") {
  public_deps = [
    ":connection",
    "$magma_build_root/include:magma",
    "$magma_build_root/include:msd_cc",
    "$magma_build_root/src/magma_util:macros",
    "$magma_build_root/src/magma_util/platform:buffer",
    "//sdk/lib/fit",
  ]

  sources = [
    "magma_driver.h",
    "magma_system_buffer.cc",
    "magma_system_buffer.h",
    "magma_system_connection.cc",
    "magma_system_connection.h",
    "magma_system_context.cc",
    "magma_system_context.h",
    "magma_system_device.cc",
    "magma_system_device.h",
    "magma_system_semaphore.cc",
    "magma_system_semaphore.h",
  ]

  deps = [
    "$magma_build_root/src/magma_util/platform:device",
    "$magma_build_root/src/magma_util/platform:event",
    "$magma_build_root/src/magma_util/platform:semaphore",
    "$magma_build_root/src/magma_util/platform:thread",
    "$magma_build_root/src/magma_util/platform:trace",
  ]
}

source_set("connection") {
  configs += [
    "//src/graphics/lib/magma/src/magma_util/platform:platform_include_config",
  ]

  sources = [
    "zircon_connection.cc",
    "zircon_connection.h",
  ]

  public_deps = [
    "$magma_build_root/include:msd",
    "$magma_build_root/include:msd_cc",
    "$magma_build_root/src/magma_util:macros",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
    "//sdk/lib/stdcompat",
    "//src/graphics/lib/magma/src/magma_util/platform:thread",
    "//src/graphics/lib/magma/src/magma_util/platform:trace",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:status",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:thread_profile",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/zx",
  ]

  deps = [ "//zircon/system/ulib/async:async-cpp" ]

  # Fidl bindings should not introduce a dep on libasync-default.so
  assert_no_deps = [ "//zircon/system/ulib/async-default" ]
}

source_set("device_impl") {
  sources = [ "magma_device_impl.h" ]

  public_deps = [
    ":sys_driver_cpp",
    "//sdk/fidl/fuchsia.gpu.magma:fuchsia.gpu.magma_cpp_wire",
    "//src/devices/lib/driver",
    "//src/graphics/lib/magma/src/magma_util:macros",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:dependency_injection_device",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:performance_counter_device",
    "//src/graphics/lib/magma/src/magma_util/platform/zircon:status",
    "//src/lib/ddk",
    "//src/lib/ddktl",
  ]
}
