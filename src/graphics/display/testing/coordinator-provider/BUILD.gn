# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//tools/cmc/build/expect_includes.gni")

group("coordinator-provider") {
  testonly = true
  public_deps = [
    ":display-coordinator-connector",
    ":fake-display-coordinator-connector",
  ]
}

group("tests") {
  testonly = true
  public_deps = [ "fake/tests" ]
}

fuchsia_component("fake-display-coordinator-connector-component") {
  testonly = true
  manifest = "meta/fake_display_coordinator_provider.cml"
  component_name = "display-coordinator-connector"
  deps = [ "fake:bin" ]
}

fuchsia_package("fake-display-coordinator-connector") {
  testonly = true
  package_name = "fake-display-coordinator-connector"
  deps = [ ":fake-display-coordinator-connector-component" ]
}

fuchsia_component("display-coordinator-connector-component") {
  manifest = "meta/display_coordinator_provider.cml"
  component_name = "display-coordinator-connector"
  deps = [ "real:bin" ]
}

fuchsia_package("display-coordinator-connector") {
  package_name = "display-coordinator-connector"
  deps = [ ":display-coordinator-connector-component" ]
}

expect_includes("fake-display-coordinator-connector-shard-includes") {
  includes = [ "meta/fake_display_coordinator_provider.shard.cml" ]
}

expect_includes("display-coordinator-connector-shard-includes") {
  includes = [ "meta/display_coordinator_provider.shard.cml" ]
}

# Includes `fake-display-coordinator-connector-component` as a child component
# and performs "includes" check for the fake display coordinator provider
# shard in the component manifest.
#
# Only supports statically defined components; do not use this on realms built
# by Realm Builder.
group("fake-display-coordinator-connector-child-component") {
  testonly = true
  deps = [
    ":fake-display-coordinator-connector-component",
    ":fake-display-coordinator-connector-shard-includes",
  ]
}

# Includes `display-coordinator-connector-component` as a child component
# and performs "includes" check for the real display coordinator provider
# shard in the component manifest.
#
# Only supports statically defined components; do not use this on realms built
# by Realm Builder.
group("display-coordinator-connector-child-component") {
  deps = [
    ":display-coordinator-connector-component",
    ":display-coordinator-connector-shard-includes",
  ]
}
