# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("amlogic_display_bind") {
  rules = "amlogic-display.bind"
  bind_output = "amlogic-display.bindbc"
  tests = "bind_tests.json"

  # TODO(fxb/115757): Remove this once we no longer need to generate
  # fragments for composite bind rules.
  disable_fragment_gen = true

  deps = [
    "//sdk/fidl/fuchsia.hardware.amlogiccanvas:fuchsia.hardware.amlogiccanvas_bindlib",
    "//src/devices/bind/fuchsia.amlogic.platform",
    "//src/devices/bind/fuchsia.hardware.dsi",
    "//src/devices/bind/fuchsia.hardware.gpio",
    "//src/devices/bind/fuchsia.hardware.hdmi",
    "//src/devices/bind/fuchsia.platform",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

source_set("common_srcs") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [
    "clock.cc",
    "dsi-host.cc",
    "hdmi-host.cc",
    "hdmitx-clk.cc",
    "lcd.cc",
    "mipi-phy.cc",
    "osd.cc",
    "rdma.cc",
    "vout.cc",
    "vpu.cc",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.display.clamprgb:fuchsia.hardware.display.clamprgb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.dsiimpl:fuchsia.hardware.dsiimpl_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.gpio:fuchsia.hardware.gpio_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.amlogiccanvas:fuchsia.hardware.amlogiccanvas_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.hdmi:fuchsia.hardware.hdmi_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp_wire",
    "//src/devices/bus/lib/device-protocol-pdev",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/graphics/display/lib/api-types-cpp",
    "//src/graphics/display/lib/device-protocol-display",
    "//src/graphics/display/lib/mipi-dsi",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/ddktl",
    "//src/lib/fsl",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/fzl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/sysmem-version",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

source_set("amlogic_display_driver_src") {
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  public_deps = [
    ":amlogic_display_bind",
    ":common_srcs",
  ]
  sources = [ "amlogic-display.cc" ]
}

fuchsia_driver("amlogic-display-driver") {
  output_name = "amlogic-display"
  deps = [
    ":amlogic_display_driver_src",
    ":common_srcs",
    "//sdk/lib/syslog/cpp:backend",
  ]
}

fuchsia_driver_component("amlogic-display") {
  component_name = "amlogic-display"
  deps = [ ":amlogic-display-driver" ]
  info = "amlogic-display-info.json"
  manifest = "meta/amlogic-display.cml"
}

test("amlogic-display-test") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "amlogic-display-test.cc",
    "vout-test.cc",
  ]
  public_deps = [
    ":amlogic_display_driver_src",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire_testing",
    "//sdk/lib/async_patterns/testing/cpp",
    "//src/devices/testing/no_ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fidl-async:fidl-async-cpp",
    "//zircon/system/ulib/fzl",
  ]
}

test("clock-test") {
  configs += [ "//build/config:all_source" ]
  sources = [ "clock-test.cc" ]
  deps = [
    ":amlogic_display_driver_src",
    ":common_srcs",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":amlogic-clock-test-package",
    ":amlogic-display-test-package",
    ":amlogic_display_bind_test",
  ]
}

fuchsia_unittest_package("amlogic-display-test-package") {
  package_name = "amlogic-display-test"
  deps = [ ":amlogic-display-test" ]
}

fuchsia_unittest_package("amlogic-clock-test-package") {
  package_name = "amlogic-clock-test"
  deps = [ ":clock-test" ]
}
