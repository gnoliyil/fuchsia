# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("virtio_gpu_bind") {
  rules = "virtio_gpu.bind"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.pci" ]
}

group("tests") {
  testonly = true
  deps = [
    ":virtio-gpu-test-package",
    ":virtio_gpu_bind_test",
  ]
}

source_set("common_srcs") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "gpu.cc",
    "virtio-abi.cc",
  ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire",
    "//src/devices/bus/lib/virtio",
    "//src/graphics/display/lib/api-types-cpp",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/fsl",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/sysmem-version",
  ]
}

fuchsia_driver("virtio_gpu-driver") {
  output_name = "virtio_gpu"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  sources = [ "gpu_driver.cc" ]
  deps = [
    ":common_srcs",
    ":virtio_gpu_bind",
    "//sdk/lib/syslog/cpp:backend",
    "//src/devices/bus/lib/virtio",
    "//src/devices/lib/driver",
    "//src/lib/ddktl",
  ]
}

fuchsia_driver_component("virtio_gpu") {
  info = "virtio_gpu-info.json"
  component_name = "virtio_gpu"
  manifest = "meta/virtio_gpu.cml"
  deps = [ ":virtio_gpu-driver" ]
}

test("virtio-gpu-test") {
  output_name = "virtio-gpu-test-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "gpu_test.cc",
    "virtio-abi-test.cc",
  ]
  deps = [
    ":common_srcs",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire_testing",
    "//sdk/lib/async-loop-testing/cpp",
    "//src/devices/bus/lib/virtio",
    "//src/devices/testing/fake-bti",
    "//src/devices/testing/no_ddk",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_unittest_package("virtio-gpu-test-package") {
  package_name = "virtio-gpu-test"
  deps = [ ":virtio-gpu-test" ]
}
