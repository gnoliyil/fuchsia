# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

source_set("shared") {
  visibility = [ ":*" ]
  sources = [ "simple-display.cc" ]
  public_deps = [
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.pci:fuchsia.hardware.pci_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_wire",
    "//sdk/fidl/fuchsia.sysmem2:fuchsia.sysmem2_cpp_wire",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/pci/lib/pci",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/sysmem-version",
    "//zircon/system/ulib/zircon-internal",
  ]

  # TODO(fxbug.dev/94768): This target uses mutable tables which are deprecated,
  # rather than builders.
  configs += [ "//build/cpp:fidl-wire-deprecated-mutable-tables" ]
}

test("simple-display-test-bin") {
  sources = [ "simple-display-test.cc" ]
  deps = [
    ":shared",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/no_ddk",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/predicates",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_unittest_package("simple-display-test") {
  package_name = "simple-display-test"
  deps = [ ":simple-display-test-bin" ]
}

# Creates a Fuchsia driver assuming the driver's files start with the same
# naming scheme: "simple-${target_name}" where `target_name` is the name of the
# driver.
#
# This template assumes that the following files exist for the driver:
# * simple-${target_name}.bind - the driver's bind rules
# * ${target_name}_bind_tests.json - the driver's bind tests
# * simple-${target_name}.cc - the driver's C++ source code
# * simple.${target_name}-info.json - the driver's info
#
# Parameters
#
#   is_fallback (optional)
#     Whether or not the driver is a fallback driver
#     Type: boolean
#     Default: false
template("simple_fuchsia_driver") {
  not_needed(invoker, "*")
  driver_name = target_name
  driver_bind_rules("simple-${driver_name}-bind") {
    rules = "simple-${driver_name}.bind"
    tests = "${driver_name}_bind_tests.json"

    deps = [
      "//src/devices/bind/fuchsia.hardware.sysmem",
      "//src/devices/bind/fuchsia.pci",
      "//src/devices/bind/fuchsia.sysmem",
    ]
  }

  fuchsia_driver("simple.${driver_name}-driver") {
    output_name = "simple.${driver_name}"
    configs += [
      "//build/config:all_source",
      "//build/config/fuchsia:enable_zircon_asserts",
    ]
    sources = [ "simple-${driver_name}.cc" ]
    deps = [
      ":shared",
      ":simple-${driver_name}-bind",
      "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
      "//src/devices/pci/lib/device-protocol-pci",
      "//src/devices/pci/lib/pci",
      "//src/lib/ddk",
    ]
  }

  fuchsia_driver_component("simple.${driver_name}") {
    component_name = "simple.${driver_name}"
    deps = [ ":simple.${driver_name}-driver" ]
    info = "simple.${driver_name}-info.json"
    manifest = "meta/${driver_name}.cml"
  }
}

simple_fuchsia_driver("amd-kaveri") {
}
simple_fuchsia_driver("bochs") {
}
simple_fuchsia_driver("gga") {
}
simple_fuchsia_driver("intel") {
}
simple_fuchsia_driver("nv") {
}
simple_fuchsia_driver("vmware") {
}

group("tests") {
  testonly = true
  deps = [
    ":simple-amd-kaveri-bind_test",
    ":simple-bochs-bind_test",
    ":simple-display-test",
    ":simple-gga-bind_test",
    ":simple-intel-bind_test",
    ":simple-vmware-bind_test",
  ]
}
