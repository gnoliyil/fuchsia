# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/components.gni")
import("//build/drivers.gni")
import("//build/test.gni")

driver_bind_rules("intel-i915-bind") {
  rules = "intel-i915.bind"
  tests = "bind_tests.json"

  deps = [
    "//src/devices/bind/fuchsia.acpi",
    "//src/devices/bind/fuchsia.pci",
    "//src/devices/bind/fuchsia.sysmem",
  ]
}

source_set("common_srcs") {
  configs += [ "//build/config:all_source" ]
  public_configs = [ "//build/config/fuchsia:enable_zircon_asserts" ]

  sources = [
    "acpi-memory-region-util.h",
    "acpi-memory-region.cc",
    "acpi-memory-region.h",
    "clock/cdclk.cc",
    "clock/cdclk.h",
    "ddi-aux-channel.cc",
    "ddi-aux-channel.h",
    "ddi-physical-layer-manager.cc",
    "ddi-physical-layer-manager.h",
    "ddi-physical-layer.cc",
    "ddi-physical-layer.h",
    "ddi.cc",
    "ddi.h",
    "display-device.cc",
    "display-device.h",
    "dp-display.cc",
    "dp-display.h",
    "dpcd.h",
    "dpll-config.cc",
    "dpll-config.h",
    "dpll.cc",
    "dpll.h",
    "firmware-bridge.cc",
    "firmware-bridge.h",
    "fuse-config.cc",
    "fuse-config.h",
    "gtt.cc",
    "gtt.h",
    "hardware-common.h",
    "hdmi-display.cc",
    "hdmi-display.h",
    "i2c/gmbus-gpio.h",
    "i2c/gmbus-i2c.cc",
    "i2c/gmbus-i2c.h",
    "igd.cc",
    "igd.h",
    "intel-i915.cc",
    "intel-i915.h",
    "interrupts.cc",
    "interrupts.h",
    "pch-engine.cc",
    "pch-engine.h",
    "pci-ids.h",
    "pipe-manager.cc",
    "pipe-manager.h",
    "pipe.cc",
    "pipe.h",
    "poll-until.h",
    "power-controller.cc",
    "power-controller.h",
    "power.cc",
    "power.h",
    "registers-ddi-phy-tiger-lake.h",
    "registers-ddi.h",
    "registers-dpll.h",
    "registers-gmbus.h",
    "registers-gt-mailbox.h",
    "registers-pch.h",
    "registers-pipe-scaler.h",
    "registers-pipe.h",
    "registers-transcoder.h",
    "registers-typec.h",
    "registers.h",
    "scoped-value-change.cc",
    "scoped-value-change.h",
    "tiling.h",
  ]
  public_deps = [
    ":intel-i915-bind",
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.intelgpucore:fuchsia.hardware.intelgpucore_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.backlight:fuchsia.hardware.backlight_cpp",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//sdk/lib/stdcompat",
    "//sdk/lib/zbi-format",
    "//src/devices/lib/driver",
    "//src/devices/lib/mmio",
    "//src/devices/pci/lib/device-protocol-pci",
    "//src/devices/pci/lib/pci",
    "//src/graphics/display/lib/api-types-cpp",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/fxl",
    "//src/lib/zbitl",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/region-alloc",
    "//zircon/system/ulib/zircon-internal",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/42085293): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]
}

fuchsia_driver("intel-i915-driver") {
  output_name = "intel-i915"
  deps = [ ":common_srcs" ]
}

fuchsia_driver_component("intel-i915") {
  component_name = "intel-i915"
  deps = [ ":intel-i915-driver" ]
  info = "intel-i915-info.json"
  manifest = "meta/intel-i915.cml"
}

fuchsia_driver_package("package") {
  package_name = "intel-i915"
  driver_components = [ ":intel-i915" ]
}

test("intel-i915-test") {
  configs += [ "//build/config:all_source" ]
  sources = [
    "acpi-memory-region-unittest.cc",
    "acpi-memory-region-util-unittest.cc",
    "ddi-aux-channel-unittest.cc",
    "ddi-physical-layer-manager-unittest.cc",
    "ddi-physical-layer-unittest.cc",
    "dp-capabilities-unittest.cc",
    "dp-display-unittest.cc",
    "dpll-config-unittest.cc",
    "dpll-unittest.cc",
    "fake-dpcd-channel.cc",
    "fake-dpcd-channel.h",
    "firmware-bridge-unittest.cc",
    "fuse-config-unittest.cc",
    "gtt-unittest.cc",
    "hardware-common-unittest.cc",
    "i2c/gmbus-gpio-unittest.cc",
    "intel-i915-test.cc",
    "interrupts-unittest.cc",
    "pch-engine-unittest.cc",
    "pipe-manager-unittest.cc",
    "pipe-unittest.cc",
    "poll-until-unittest.cc",
    "power-controller-unittest.cc",
    "power-unittest.cc",
    "registers-arbiter-unittest.cc",
    "registers-arbiter.h",
    "registers-ddi-phy-tiger-lake-unittest.cc",
    "registers-ddi-unittest.cc",
    "registers-dpll-unittest.cc",
    "registers-gmbus-unittest.cc",
    "registers-memory-controller-unittest.cc",
    "registers-memory-controller.h",
    "registers-pipe-scaler-unittest.cc",
    "registers-pipe-unittest.cc",
    "registers-transcoder-unittest.cc",
    "registers-typec-unittest.cc",
    "registers-unittest.cc",
    "scoped-value-change-unittest.cc",
  ]
  deps = [
    ":common_srcs",
    "//sdk/fidl/fuchsia.hardware.sysmem:fuchsia.hardware.sysmem_cpp_testing",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp_testing",
    "//sdk/lib/async-loop-testing/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//src/devices/lib/mmio:test_helper",
    "//src/devices/pci/testing:pci-protocol-fake",
    "//src/devices/testing/fake-mmio-reg",
    "//src/devices/testing/mock-ddk",
    "//src/devices/testing/mock-mmio-range",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
    "//third_party/googletest:gtest",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/mmio-ptr:mmio-ptr-fake",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":intel-i915-bind_test",
    ":intel-i915-test-package",
  ]
}

fuchsia_unittest_package("intel-i915-test-package") {
  package_name = "intel-i915-test"
  deps = [ ":intel-i915-test" ]
  manifest = "meta/intel-i915-test.cml"
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
