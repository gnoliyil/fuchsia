# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bind/bind.gni")
import("//build/drivers.gni")

driver_bind_rules("coordinator_bind") {
  rules = "coordinator.bind"
  bind_output = "display-coordinator.bindbc"
  tests = "bind_tests.json"
  deps = [ "//src/devices/bind/fuchsia.display" ]
}

fuchsia_driver("coordinator-driver") {
  output_name = "display-coordinator"
  configs += [
    "//build/config:all_source",
    "//build/config/fuchsia:enable_zircon_asserts",
  ]
  deps = [
    ":shared",
    "//src/devices/lib/driver",
  ]
}

fuchsia_driver_component("coordinator") {
  visibility = [ ":*" ]
  component_name = "display-coordinator"
  deps = [ ":coordinator-driver" ]
  info = "coordinator-info.json"
  manifest = "meta/display-coordinator.cml"
}

fuchsia_driver_package("package") {
  package_name = "display-coordinator"
  driver_components = [ ":coordinator" ]
}

# Also used by test/.
source_set("shared") {
  visibility = [
    "../fake:fake-display-stack",
    "./*",
  ]
  configs += [ "//build/config:all_source" ]
  sources = [
    "capture-image.cc",
    "capture-image.h",
    "client-id.h",
    "client-priority.cc",
    "client-priority.h",
    "client.cc",
    "client.h",
    "controller.cc",
    "controller.h",
    "display-info.cc",
    "display-info.h",
    "driver.cc",
    "driver.h",
    "eld.cc",
    "eld.h",
    "fence.cc",
    "fence.h",
    "id-map.h",
    "image.cc",
    "image.h",
    "layer.cc",
    "layer.h",
    "migration-util.cc",
    "migration-util.h",
  ]
  public_deps = [
    ":coordinator_bind",
    "//sdk/banjo/fuchsia.hardware.audiotypes:fuchsia.hardware.audiotypes_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.display.clamprgb:fuchsia.hardware.display.clamprgb_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.display.controller:fuchsia.hardware.display.controller_banjo_cpp",
    "//sdk/banjo/fuchsia.hardware.i2cimpl:fuchsia.hardware.i2cimpl_banjo_cpp",
    "//sdk/fidl/fuchsia.hardware.display:fuchsia.hardware.display_cpp",
    "//sdk/fidl/fuchsia.hardware.display.types:fuchsia.hardware.display.types_cpp",
    "//sdk/fidl/fuchsia.sysmem:fuchsia.sysmem_cpp",
    "//sdk/lib/fidl",
    "//sdk/lib/zbi-format",
    "//src/devices/lib/audio",
    "//src/devices/lib/mmio",
    "//src/graphics/display/lib/api-types-cpp",
    "//src/graphics/display/lib/edid",
    "//src/lib/async-watchdog",
    "//src/lib/ddk",
    "//src/lib/ddktl",
    "//src/lib/eld",
    "//src/media/audio/drivers/lib/audio-proto-utils",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/hwreg",
    "//zircon/system/ulib/image-format",
    "//zircon/system/ulib/inspect",
    "//zircon/system/ulib/pretty",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/trace",
    "//zircon/system/ulib/zx",
  ]

  # TODO(https://fxbug.dev/135694): delete the below and fix compiler warnings
  configs += [ "//build/config:Wno-vla-cxx-extension" ]
}

source_set("preferred-scanout-image-type") {
  sources = [ "preferred-scanout-image-type.h" ]
}

group("tests") {
  testonly = true
  deps = [
    ":coordinator_bind_test",
    "tests",
  ]
}
