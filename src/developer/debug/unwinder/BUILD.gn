# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/sdk_source_set.gni")

sdk_source_set("unwinder") {
  category = "internal"
  build_as_static = true

  public = [
    "dwarf_cfi.h",
    "dwarf_cfi_parser.h",
    "dwarf_expr.h",
    "error.h",
    "memory.h",
    "registers.h",
    "unwind.h",
  ]
  sdk_headers_for_internal_use = [
    "dwarf_cfi.h",
    "dwarf_cfi_parser.h",
    "dwarf_expr.h",
    "dwarf_unwinder.h",
    "scs_unwinder.h",
  ]
  sources = [
    "dwarf_cfi.cc",
    "dwarf_cfi_parser.cc",
    "dwarf_expr.cc",
    "dwarf_unwinder.cc",
    "memory.cc",
    "registers.cc",
    "scs_unwinder.cc",
    "unwind.cc",
  ]
  public_deps = [ "//src/lib/fxl" ]
  deps = [ "//sdk/lib/syslog/cpp" ]
  friend = [ ":tests" ]

  if (is_mac) {
    deps += [ "//src/developer/debug/shared:elf_h_on_mac" ]
  } else {
    # UnwindLocal() is only available on ELF systems.
    public += [
      "third_party/libunwindstack/context.h",
      "unwind_local.h",
    ]
    sdk_headers_for_internal_use += [ "third_party/libunwindstack/context.h" ]
    sources += [ "unwind_local.cc" ]
    if (current_cpu == "x64") {
      sources += [ "third_party/libunwindstack/context_x86_64.S" ]
    }
  }

  if (is_fuchsia) {
    public += [ "fuchsia.h" ]
    sources += [ "fuchsia.cc" ]
  }
}

source_set("tests") {
  testonly = true

  sources = [
    "dwarf_expr_unittest.cc",
    "memory_unittest.cc",
  ]
  if (!is_mac) {
    sources += [ "unwind_local_unittest.cc" ]
  }

  deps = [
    ":unwinder",
    "//third_party/googletest:gtest",
  ]
}
