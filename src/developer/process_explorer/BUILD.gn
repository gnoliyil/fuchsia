# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//src/sys/core/build/core_shard.gni")

group("process_explorer") {
  testonly = true
  deps = [
    ":process_explorer_package",
    "tests:tests",
  ]
}

source_set("lib") {
  sources = [
    "process_explorer.cc",
    "process_explorer.h",
    "utils.cc",
    "utils.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_hlcpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fsl",
    "//third_party/rapidjson",
    "//zircon/system/ulib/task-utils",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.process.explorer:fuchsia.process.explorer_hlcpp",
    "//sdk/lib/sys/cpp",
    "//src/lib/fxl",
  ]
}

executable("bin") {
  output_name = "process_explorer"

  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

fuchsia_component("process_explorer_component") {
  component_name = "process_explorer"
  manifest = "meta/process_explorer.cml"
  deps = [ ":bin" ]
}

fuchsia_package("process_explorer_package") {
  package_name = "process_explorer"
  deps = [
    ":process_explorer_component",
    "//build/validate:non_production_tag",
  ]
}

core_shard("process_explorer_core_shard") {
  shard_file = "meta/process_explorer.core_shard.cml"
}
