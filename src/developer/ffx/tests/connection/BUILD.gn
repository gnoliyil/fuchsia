# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/testing/environments.gni")

if (is_host) {
  rustc_test("ffx_connection_test") {
    testonly = true
    edition = "2021"
    source_root = "src/tests.rs"

    sources = [
      "src/tests.rs",
      "src/util.rs",
    ]
    deps = [
      "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol_rust",
      "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
      "//src/connectivity/overnet/lib/hoist",
      "//src/developer/ffx/config:lib",
      "//src/developer/ffx/daemon:lib",
      "//src/developer/ffx/lib/isolate:lib",
      "//src/developer/ffx/lib/target:lib",
      "//src/lib/fidl/rust/fidl",
      "//src/lib/fuchsia",
      "//src/lib/fuchsia-async",
      "//src/lib/testing/fixture",
      "//src/testing/fidl/proxy_stressor/fidl:test.proxy.stress_rust",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:async-lock",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:tracing",
    ]

    data_deps = [
      "//src/developer/ffx:test_data",
      "//src/developer/ffx/plugins/component:ffx_component_tool_test_data",
      "//src/developer/ffx/plugins/target:ffx_target_tool_test_data",
    ]

    test_environments = all_fuchsia_envs

    # TODO(https://fxbug.dev/64897): The arm64 emulator is run from an arm64 host but we only
    # support running tests on x64 hosts.
    if (target_cpu == "arm64") {
      test_environments -= [ emu_env ]
    }
  }
}

fuchsia_package("ffx_connection_test_components") {
  deps = [ "//src/testing/fidl/proxy_stressor" ]
}

group("tests") {
  deps = [
    ":ffx_connection_test($host_toolchain)",
    ":ffx_connection_test_components($target_toolchain)",
  ]
  testonly = true
}
