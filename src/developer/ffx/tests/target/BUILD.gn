# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components/fuchsia_package.gni")
import("//build/host.gni")
import("//build/rust/rustc_binary.gni")

test_deps = []

# The qemu-x64 board does not build a UEFI partition. Switch to x64 to include
# this test.
# Test is disabled under ASAN builds because Gigaboot built w/ ASAN suffers from
# memory corruption.
if (is_host && target_cpu == "x64" && build_uefi_disk && !is_asan) {
  rustc_test("ffx_target_test") {
    testonly = true
    edition = "2021"
    source_root = "src/tests.rs"

    sources = [ "src/tests.rs" ]

    deps = [
      "//src/developer/ffx/testing:ffx_testing",
      "//src/lib/fuchsia",
      "//src/lib/fuchsia-async",
      "//src/lib/testing/fixture",
      "//third_party/rust_crates:futures",
    ]

    # Only one emulator can be spawned at a time.
    args = [ "--test-threads=1" ]
  }

  test_deps += [ ":ffx_target_test($host_toolchain)" ]
}

group("tests") {
  deps = test_deps
  testonly = true
}
