# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")
import("//build/testing/host_test_data.gni")

if (is_host) {
  host_test_data("empty-repo") {
    sources = [
      "//src/sys/pkg/tests/pkg-resolver/template/repository/1.root.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/1.snapshot.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/1.targets.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/root.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/snapshot.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/targets.json",
      "//src/sys/pkg/tests/pkg-resolver/template/repository/timestamp.json",
    ]
    outputs = [ "$root_out_dir/test_data/ffx_daemon_protocol_repo/empty-repo/repository/{{source_file_part}}" ]
  }

  rustc_library("ffx_daemon_protocol_repo") {
    version = "0.1.0"
    edition = "2021"
    with_unit_tests = true

    deps = [
      "//sdk/fidl/fuchsia.developer.ffx:fuchsia.developer.ffx_rust",
      "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol_rust",
      "//sdk/fidl/fuchsia.pkg:fuchsia.pkg_rust",
      "//sdk/fidl/fuchsia.pkg.rewrite:fuchsia.pkg.rewrite_rust",
      "//src/developer/ffx/daemon/core:lib",
      "//src/developer/ffx/daemon/events:lib",
      "//src/developer/ffx/daemon/target:lib",
      "//src/developer/ffx/lib/fidl-fuchsia-developer-ffx-ext",
      "//src/developer/ffx/lib/pkg",
      "//src/developer/ffx/lib/protocols:lib",
      "//src/lib/analytics/rust:lib",
      "//src/lib/fidl/rust/fidl",
      "//src/lib/fuchsia-async",
      "//src/lib/fuchsia-hyper",
      "//src/lib/fuchsia-url",
      "//src/lib/network/fidl_fuchsia_net_ext",
      "//src/lib/zircon/rust:fuchsia-zircon-status",
      "//src/sys/lib/fidl-fuchsia-pkg-rewrite-ext",
      "//src/sys/pkg/lib/fuchsia-repo",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:async-lock",
      "//third_party/rust_crates:async-trait",
      "//third_party/rust_crates:futures",
      "//third_party/rust_crates:itertools",
      "//third_party/rust_crates:tracing",
      "//third_party/rust_crates:tuf",
      "//third_party/rust_crates:url",
    ]
    test_deps = [
      ":empty-repo",
      "//src/developer/ffx/config:lib",
      "//src/developer/ffx/lib/addr:lib",
      "//third_party/rust_crates:assert_matches",
      "//third_party/rust_crates:lazy_static",
      "//third_party/rust_crates:pretty_assertions",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:simplelog",
    ]

    sources = [
      "src/lib.rs",
      "src/metrics.rs",
      "src/tunnel.rs",
    ]
  }
}

group("tests") {
  testonly = true
  deps = [ ":ffx_daemon_protocol_repo_test($host_toolchain)" ]
}
