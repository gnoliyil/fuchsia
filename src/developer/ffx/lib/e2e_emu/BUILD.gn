# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/rust/rustc_library.gni")

if (is_host) {
  group("lib") {
    testonly = true
    deps = [ ":ffx_e2e_emu" ]
  }

  group("host_tests") {
    testonly = true
    deps = [ ":ffx_e2e_emu_test" ]
  }

  rustc_library("ffx_e2e_emu") {
    testonly = true
    edition = "2021"
    with_unit_tests = true
    sources = [ "src/lib.rs" ]
    deps = [
      "//src/developer/ffx/config:lib",
      "//src/developer/ffx/lib/isolate:lib",
      "//src/lib/diagnostics/data/rust",
      "//third_party/rust_crates:anyhow",
      "//third_party/rust_crates:serde",
      "//third_party/rust_crates:serde_json",
      "//third_party/rust_crates:tempfile",
      "//third_party/rust_crates:tracing",
    ]
    test_deps = [ "//src/lib/fuchsia" ]
    data_deps = [ ":test_data" ]
    configs += [ ":test_data_path_config" ]
  }

  # Product bundle containing emulator image.
  _pb_label = "//build/images/fuchsia:product_bundle($default_toolchain)"
  _pb_outdir = get_label_info(_pb_label, "target_out_dir")
  _product_bundle = "$_pb_outdir/product_bundle"

  # Exported SDK with ffx binary.
  _sdk_label = "//sdk:core($default_toolchain)"
  _sdk_outdir = get_label_info(_sdk_label, "root_out_dir")
  _sdk_path = _sdk_outdir + "/sdk/exported/core"

  host_test_data("test_data") {
    sources = [
      _product_bundle,
      _sdk_path,
    ]
    deps = [
      _pb_label,
      _sdk_label,
    ]
  }

  config("test_data_path_config") {
    rustenv = [
      "PRODUCT_BUNDLE_PATH=" + rebase_path(_product_bundle, root_build_dir),
      "SDK_ROOT_DIR=" + rebase_path(_sdk_path, root_build_dir),
    ]
  }
}
