# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//src/developer/ffx/config.gni")

# The list of tools under this directory
_tools = [
  "echo",
  "starnix",
  "target-package",
  "log-ng",
]

group("tools") {
  public_deps = _tools

  # Any plugins that can be built in dual mode should be listed here, and that
  # will cause them to be built as host tools that ffx can invoke.
  if (ffx_build_dual_mode_plugins_as_subtools) {
    public_deps += dual_mode_subtools  # from config.gni
  }
}

group("bins") {
  public_deps = []
  foreach(tool, _tools) {
    public_deps += [ "$tool:bin" ]
  }

  if (ffx_build_dual_mode_plugins_as_subtools) {
    public_deps += dual_mode_subtool_bins  # from config.gni
  }
}

group("tests") {
  testonly = true
  deps = []
  foreach(tool, _tools) {
    deps += [ "$tool:tests" ]
  }

  if (ffx_build_dual_mode_plugins_as_subtools) {
    deps += dual_mode_subtool_tests  # from config.gni
  }
}

group("host_tests") {
  testonly = true
  deps = [ "target-package:host_tests" ]
}
