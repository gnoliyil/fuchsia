# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/rust/rustc_library.gni")
import("//src/developer/ffx/build/ffx_tool.gni")
import("//src/developer/ffx/lib/version/build/ffx_apply_version.gni")

rustc_library("lib") {
  # This is named as such to avoid a conflict with an existing ffx log-ng command.
  name = "ffx_tool_log-ng"
  edition = "2021"
  with_unit_tests = true

  deps = [
    "//sdk/fidl/fuchsia.developer.ffx:fuchsia.developer.ffx_rust",
    "//sdk/fidl/fuchsia.developer.remotecontrol:fuchsia.developer.remotecontrol_rust",
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_rust",
    "//sdk/fidl/fuchsia.diagnostics.host:fuchsia.diagnostics.host_rust",
    "//src/developer/ffx/config:lib",
    "//src/developer/ffx/lib/fho:lib",
    "//src/developer/ffx/lib/rcs:lib",
    "//src/diagnostics/lib/log-command",
    "//src/diagnostics/lib/log_symbolizer",
    "//src/lib/diagnostics/data/rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-channel",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:regex",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]

  sources = [
    "src/error.rs",
    "src/lib.rs",
    "src/spam_filter.rs",
    "src/symbolizer.rs",
    "src/testing_utils.rs",
  ]

  test_deps = [
    "//src/developer/ffx/lib/writer:lib",
    "//src/lib/diagnostics/selectors",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:async-channel",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:tracing",
  ]
}

# TODO(https://fxbug.dev/883811): Rename directory to match target.
ffx_tool("ffx_log-ng") {
  edition = "2021"
  output_name = "ffx-log-ng"
  deps = [
    ":lib",
    "//src/developer/ffx/lib/fho:lib",
    "//src/lib/fuchsia-async",
  ]
  sources = [ "src/main.rs" ]
}

group("log-ng") {
  public_deps = [
    ":ffx_log-ng",
    ":ffx_log-ng_host_tool",
  ]
}

group("bin") {
  public_deps = [ ":ffx_log-ng_versioned" ]
}

group("tests") {
  testonly = true
  deps = [ ":lib_test($host_toolchain)" ]
}
