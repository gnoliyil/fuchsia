# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")
import("//src/sys/core/build/core_shard.gni")

group("common") {
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.adb:fuchsia.hardware.adb_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_cpp_wire",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/component/outgoing/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/fbl",
  ]
}

executable("adb-reboot") {
  sources = [
    "adb-reboot.cc",
    "main.cc",
  ]
  deps = [ ":common" ]
}

core_shard("adb-reboot-shard") {
  shard_file = "meta/adb-reboot.core_shard.cml"
}

fuchsia_component("adb-reboot-component") {
  manifest = "meta/adb-reboot.cml"
  component_name = "adb-reboot"
  deps = [ ":adb-reboot" ]
}

test("adb-reboot-test-bin") {
  sources = [
    "adb-reboot-test.cc",
    "adb-reboot.cc",
  ]
  deps = [
    ":common",
    "//sdk/fidl/fuchsia.hardware.adb:fuchsia.hardware.adb_hlcpp",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_cpp_wire_testing",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_hlcpp",
    "//sdk/lib/sys/component/cpp/testing:cpp",
    "//src/lib/fxl/test:gtest_main",
    "//src/lib/testing/loop_fixture:gtest",
    "//third_party/googletest:gtest",
  ]
}

fuchsia_component("adb-reboot-test-component") {
  testonly = true
  component_name = "adb_reboot_test"
  manifest = "meta/adb-reboot-test.cml"
  deps = [ ":adb-reboot-test-bin" ]
}

fuchsia_test_package("adb-reboot-test") {
  test_components = [ ":adb-reboot-test-component" ]
  deps = [ ":adb-reboot-component" ]
}

group("tests") {
  testonly = true
  deps = [ ":adb-reboot-test" ]
}
