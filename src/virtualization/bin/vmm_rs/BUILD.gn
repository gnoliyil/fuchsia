# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true
  deps = [ ":vmm_rs_unittests" ]
}

rustc_binary("vmm_rs") {
  edition = "2021"
  with_unit_tests = true

  sources = [
    "src/address.rs",
    "src/fidl_server.rs",
    "src/hypervisor.rs",
    "src/main.rs",
    "src/memory.rs",
    "src/vcpu.rs",
    "src/virtual_machine.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.kernel:fuchsia.kernel_rust",
    "//sdk/fidl/fuchsia.virtualization:fuchsia.virtualization_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-runtime",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:lazy_static",
    "//third_party/rust_crates:static_assertions",
    "//third_party/rust_crates:thiserror",
    "//third_party/rust_crates:tracing",
  ]
  test_deps = [
    "//src/lib/fuchsia-async",
    "//third_party/rust_crates:parking_lot",
  ]
}

fuchsia_component("component") {
  component_name = "vmm_rs"
  manifest = "meta/vmm.cml"
  deps = [ ":vmm_rs" ]
}

fuchsia_unittest_package("vmm_rs_unittests") {
  deps = [ ":vmm_rs_test" ]
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
