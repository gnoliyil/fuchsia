# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_test.gni")
import("//src/sys/core/build/core_shard.gni")

group("tests") {
  testonly = true
  deps = [ ":vmm_launcher_tests" ]
}

fuchsia_component_manifest("manifest") {
  component_name = "vmm_launcher"
  manifest = "meta/vmm_launcher.cml"
}

fuchsia_structured_config_rust_lib("vmm_launcher_config") {
  cm_label = ":manifest"
}

rustc_binary("vmm_launcher_bin") {
  edition = "2021"
  name = "vmm_launcher"

  sources = [
    "src/main.rs",
    "src/vmm_launcher.rs",
  ]

  deps = [
    ":vmm_launcher_config",
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.virtualization:fuchsia.virtualization_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon-status",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

rustc_test("vmm_launcher_test_bin") {
  edition = "2021"
  source_root = "tests/integration.rs"
  sources = [ "tests/integration.rs" ]
  deps = [
    "//sdk/fidl/fuchsia.component:fuchsia.component_rust",
    "//sdk/fidl/fuchsia.component.decl:fuchsia.component.decl_rust",
    "//sdk/fidl/fuchsia.virtualization:fuchsia.virtualization_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component-test",
    "//third_party/rust_crates:anyhow",
  ]
}

fuchsia_test_component("vmm_launcher_tests_component") {
  component_name = "vmm_launcher_tests"
  manifest = "meta/vmm_launcher_tests.cml"
  deps = [
    ":vmm_launcher",
    ":vmm_launcher_test_bin",
  ]
  test_type = "system"
}

fuchsia_test_package("vmm_launcher_tests") {
  test_components = [ ":vmm_launcher_tests_component" ]
  deps = [
    ":default_config",
    ":vmm_launcher_component",
    "//src/virtualization/bin/vmm_rs:component",
  ]
}

fuchsia_structured_config_values("default_config") {
  cm_label = ":manifest"
  values = {
    vmm_component_url = "#meta/vmm.cm"
  }
}

fuchsia_component("vmm_launcher_component") {
  cm_label = ":manifest"
  deps = [
    ":vmm_launcher_bin",
    "//src/virtualization/bin/vmm:vmm_component",
  ]
}

fuchsia_package("vmm_launcher") {
  package_name = "vmm_launcher"
  deps = [
    ":default_config",
    ":vmm_launcher_component",
  ]
}

core_shard("core_shard") {
  shard_file = "meta/vmm_launcher.core_shard.cml"
}
