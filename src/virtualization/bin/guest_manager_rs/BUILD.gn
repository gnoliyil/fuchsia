# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

group("packages") {
  deps = [ ":debian_guest_manager_rs" ]
}

group("tests") {
  testonly = true
  deps = [ ":guest_manager_rs_test" ]
}

rustc_binary("guest_manager_bin") {
  name = "guest_manager_bin"
  edition = "2021"
  with_unit_tests = true

  sources = [
    "src/guest_config.rs",
    "src/guest_manager.rs",
    "src/main.rs",
  ]

  deps = [
    "//sdk/fidl/fuchsia.hardware.network:fuchsia.hardware.network_rust",
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//sdk/fidl/fuchsia.net:fuchsia.net_rust",
    "//sdk/fidl/fuchsia.net.interfaces:fuchsia.net.interfaces_rust",
    "//sdk/fidl/fuchsia.virtualization:fuchsia.virtualization_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-fs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:serde",
    "//third_party/rust_crates:serde_json",
    "//third_party/rust_crates:static_assertions",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/lib/async-utils",
    "//third_party/rust_crates:tempfile",
  ]
}

fuchsia_unittest_package("guest_manager_rs_test") {
  deps = [ ":guest_manager_bin_test" ]
  manifest = "meta/guest_manager_rs_test.cml"
  test_specs = {
    log_settings = {
      max_severity = "ERROR"
    }
  }
}

fuchsia_component("debian_guest_manager_cmp") {
  visibility = [ ":*" ]
  manifest = "meta/debian_guest_manager.cml"
  deps = [ ":guest_manager_bin" ]
  component_name = "debian_guest_manager_rs"
}

fuchsia_package("debian_guest_manager_rs") {
  deps = [ ":debian_guest_manager_cmp" ]
  subpackages = [ "//src/virtualization/packages/debian_guest" ]
}
