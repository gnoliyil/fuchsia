# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Add documentation to README.md if updating this file with
# more configurations.

import("//build/components.gni")
import("//build/dist/renamed_binary.gni")
import("//build/icu/icu_executable.gni")
import("//build/icu/icu_fuchsia_component.gni")
import("//build/icu/icu_fuchsia_package.gni")
import("//build/icu/icu_renamed_binary.gni")
import("//build/icu/icu_source_set.gni")
import("//src/sys/core/build/core_shard.gni")

group("intl_services") {
  testonly = true
  public_deps = [
    ":component",
    ":component_small",
    ":pkg",
    ":pkg_small",
    ":tests",
  ]
}

group("small") {
  deps = [ ":pkg_small" ]
}

group("tests") {
  testonly = true
  public_deps = []
}

icu_source_set("lib") {
  sources = [
    "run.cc",
    "run.h",
  ]

  icu_deps = [
    "//src/lib/intl/intl_property_provider_impl",
    "//src/lib/intl/time_zone_info:time-zone-info-service-lib",
  ]

  deps = [
    "//sdk/fidl/fuchsia.settings:fuchsia.settings_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl:fxl",
    "//src/lib/icu:data",
    "//src/lib/icu:lib",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

icu_executable("intl_services_bin") {
  output_name = "intl_services"
  sources = [ "main.cc" ]

  icu_deps = [ ":lib" ]
  deps = [ "//sdk/lib/syslog/cpp" ]
}

icu_fuchsia_component("component") {
  component_name = "intl"
  manifest = "meta/intl.cml"
  icu_deps = [
    ":intl_services_bin",
    "//src/lib/icu:icudtl",
  ]
}

icu_fuchsia_package("pkg") {
  package_name = "intl_services"
  icu_deps = [ ":component" ]
}

# The a11y_manager and intl_services binaries have been fused together for
# storage space reasons, busybox style.
icu_renamed_binary("renamed_a11y_manager") {
  source = "$root_out_dir/a11y_manager"
  icu_source_deps = [ "//src/ui/a11y/bin/a11y_manager:bin" ]
  dest = "bin/intl_services"
}

icu_fuchsia_component("component_small") {
  component_name = "intl"
  manifest = "meta/intl.cml"
  icu_deps = [
    ":renamed_a11y_manager",
    "//src/lib/icu:icudtl",
  ]
}

icu_fuchsia_package("pkg_small") {
  package_name = "intl-services-small"
  icu_deps = [ ":component_small" ]
}

# TODO: fxbug.dev/117974 - Remove these targets, they are not needed anymore.
group("intl_services_shard") {
}

group("intl_services_small_shard") {
}

group("intl_services_small_timezones_shard") {
}
