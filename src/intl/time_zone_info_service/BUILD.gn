# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/config.gni")
import("//build/icu/icu_executable.gni")
import("//build/icu/icu_fuchsia_component.gni")
import("//build/icu/icu_fuchsia_package.gni")
import("//src/sys/core/build/core_shard.gni")

group("time_zone_info_service") {
  testonly = true
  deps = [
    ":pkg",
    ":tests",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":pkg",
    "tests",
  ]
}

icu_executable("bin") {
  output_name = "time-zone-info-service"
  sources = [ "main.cc" ]
  icu_deps = [ "//src/lib/intl/time_zone_info:time-zone-info-service-lib" ]
  deps = [
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl:fxl_cli",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

icu_fuchsia_component("component") {
  component_name = "time-zone-info-service"
  manifest = "meta/time-zone-info-service.cml"
  icu_deps = [ ":bin" ]
}

icu_fuchsia_package("pkg") {
  package_name = "time-zone-info-service"
  icu_deps = [
    ":component",
    "//src/lib/icu:icudtl",
  ]
}

core_shard("time-zone-info-service-core-shard") {
  shard_file = "meta/time-zone-info-service.core_shard.cml"
}

# Alias
group("core-shard") {
  deps = [ ":time-zone-info-service-core-shard" ]
}
