# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/images/args.gni")
import("//build/testing/environments.gni")
import("//build/testing/host_test_data.gni")

group("tests") {
  testonly = true

  deps = []

  # //tools/emulator only works on linux.
  # The installer only works on x64 targets which use FVM.
  if (host_os == "linux" && target_cpu == "x64" && fvm_partition != "") {
    deps += [ ":installer_test($host_toolchain)" ]
  }
}

if (is_linux) {
  go_library("lib") {
    testonly = true
    sources = [ "installer_test.go" ]
    deps = [ "//tools/emulator/emulatortest" ]
  }

  host_test_data("installer_image") {
    sources = [ "$root_build_dir/obj/build/images/installer/installer.img" ]

    deps = [ "//build/images/installer($target_toolchain)" ]
  }

  go_test("installer_test") {
    library = ":lib"
    deps = [
      "//tools/virtual_device",
      "//tools/virtual_device:proto",
    ]
    non_go_deps = [
      ":installer_image",
      "//build/images/recovery:test_data",
    ]
  }
}
