# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fuzz.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":fcrypto-fuzzers",
    ":fcrypto-test-pkg",
  ]
}

test("fcrypto") {
  output_name = "fcrypto-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "aead.cc",
    "bytes.cc",
    "cipher.cc",
    "hkdf.cc",
    "hmac.cc",
    "secret.cc",
    "utils.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//src/security/lib/fcrypto",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_library_fuzzer("hmac-fuzzer") {
  sources = [ "hmac-fuzzer.cc" ]
  deps = [
    "//src/security/lib/fcrypto",
    "//zircon/system/ulib/fbl",
  ]
}

fuchsia_fuzzer_component("hmac-fuzzer-component") {
  manifest = "meta/hmac-fuzzer.cml"
  deps = [ ":hmac-fuzzer" ]
}

fuchsia_fuzzer_package("fcrypto-fuzzers") {
  cpp_fuzzer_components = [ ":hmac-fuzzer-component" ]
}

fuchsia_unittest_package("fcrypto-test-pkg") {
  package_name = "fcrypto-test"
  deps = [ ":fcrypto" ]
}
