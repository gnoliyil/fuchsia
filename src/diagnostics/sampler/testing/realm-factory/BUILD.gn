# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

rustc_binary("bin") {
  testonly = true
  edition = "2021"
  name = "sampler_realm_factory"
  sources = [
    "src/main.rs",
    "src/mocks.rs",
    "src/realm_factory.rs",
  ]
  deps = [
    "../fidl:test.sampler_rust",
    "//sdk/fidl/fuchsia.hardware.power.statecontrol:fuchsia.hardware.power.statecontrol_rust",
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_rust",
    "//src/diagnostics/sampler/tests/fidl:fuchsia.mockrebootcontroller_rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-component",
    "//src/lib/fuchsia-component-test",
    "//src/testing/realm_proxy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component("realm-factory-component") {
  testonly = true
  component_name = "realm-factory"
  manifest = "meta/default.cml"
  deps = [ ":bin" ]
}

fuchsia_package("realm-factory") {
  package_name = "sampler-realm-factory"
  testonly = true
  deps = [
    ":realm-factory-component",
    "../../tests:sampler-test-config",
    "../../tests:validation-fire-components",
    "../../tests:validation-fire-projects",
    "../../tests:validation-sampler-resources",

    # Mock deps.
    "//src/cobalt/bin/testing/mock_cobalt:component_v2",
    "//src/diagnostics/archivist:archivist-for-embedding-bundle",

    # Component under test deps.
    "//src/diagnostics/sampler:sampler-component",
    "//src/diagnostics/sampler/tests/test_component:single_counter_test_component",
  ]
}
