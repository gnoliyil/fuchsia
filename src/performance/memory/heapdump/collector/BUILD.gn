# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

shared_library("elf-search-wrapper") {
  sources = [ "src/elf-search.cc" ]
  public_deps = [ "//zircon/system/ulib/elf-search" ]
}

rustc_binary("bin") {
  output_name = "heapdump-collector"
  with_unit_tests = true

  edition = "2021"
  sources = [
    "src/main.rs",
    "src/process.rs",
    "src/process_v1.rs",
    "src/registry.rs",
    "src/snapshot_storage.rs",
    "src/utils.rs",
  ]

  deps = [
    ":elf-search-wrapper",
    "//sdk/fidl/fuchsia.memory.heapdump.client:fuchsia.memory.heapdump.client_rust",
    "//sdk/fidl/fuchsia.memory.heapdump.process:fuchsia.memory.heapdump.process_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/performance/memory/heapdump/lib/heapdump_snapshot",
    "//src/performance/memory/heapdump/lib/heapdump_vmo",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
    "//third_party/rust_crates:tracing",
  ]

  test_deps = [
    "//src/lib/fuchsia-async",
    "//src/lib/fuchsia-runtime",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:itertools",
    "//third_party/rust_crates:test-case",
  ]
}

fuchsia_package_with_single_component("collector") {
  package_name = "heapdump-collector"
  manifest = "meta/collector.cml"
  deps = [ ":bin" ]
}

fuchsia_unittest_package("tests") {
  package_name = "heapdump-collector-tests"
  deps = [ ":bin_test" ]
}
