# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.
import("//build/components.gni")

executable("profiler_integration_test_bin") {
  testonly = true
  output_name = "profiler_integration_test"
  sources = [ "integration_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.cpu.profiler:fuchsia.cpu.profiler_cpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fidl/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fsl",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/system/ulib/zx",
  ]
}

fuchsia_test_component("profiler_integration_test_component") {
  manifest = "meta/profiler_integration_test.cml"
  deps = [ ":profiler_integration_test_bin" ]
  test_type = "system"
}

fuchsia_test_package("profiler_integration_test") {
  test_components = [ ":profiler_integration_test_component" ]
  deps = [ "//src/performance/experimental/profiler:profiler_component" ]

  test_specs = {
    parallel = 1
  }
}

executable("profiler_unittest_bin") {
  sources = [
    "process_watcher_test.cc",
    "targets_test.cc",
  ]
  deps = [
    "//src/lib/fxl/test:gtest_main",
    "//src/performance/experimental/profiler:lib",
  ]
  testonly = true
}

fuchsia_unittest_package("profiler_unittests") {
  deps = [ ":profiler_unittest_bin" ]
}

group("tests") {
  testonly = true
  public_deps = [
    ":profiler_integration_test",
    ":profiler_unittests",
  ]
}
