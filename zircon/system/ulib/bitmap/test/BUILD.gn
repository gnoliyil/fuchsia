# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/fuzz.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":bitmap-fuzzers",
    ":bitmap-test-pkg",
  ]
}

test("bitmap") {
  output_name = "bitmap-test"
  if (is_fuchsia) {
    fdio_config = [ "//build/config/fuchsia:fdio_config" ]
    if (configs + fdio_config - fdio_config != configs) {
      configs -= fdio_config
    }
  }
  sources = [
    "raw-bitmap-tests.cc",
    "rle-bitmap-tests.cc",
  ]
  deps = [
    "//sdk/lib/fdio",
    "//zircon/system/ulib/bitmap",
    "//zircon/system/ulib/fbl",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_package("bitmap-test-pkg") {
  package_name = "bitmap-test"
  deps = [ ":bitmap" ]
}

fuchsia_library_fuzzer("raw-bitmap-fuzzer") {
  sources = [ "raw-bitmap-fuzzer.cc" ]
  deps = [ "//zircon/system/ulib/bitmap" ]
}

fuchsia_fuzzer_component("raw-bitmap-fuzzer-component") {
  manifest = "meta/raw-bitmap-fuzzer.cml"
  deps = [ ":raw-bitmap-fuzzer" ]
}

fuchsia_fuzzer_package("bitmap-fuzzers") {
  cpp_fuzzer_components = [ ":raw-bitmap-fuzzer-component" ]
}
