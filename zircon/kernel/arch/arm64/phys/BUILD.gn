# Copyright 2020 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/toolchain/toolchain_environment.gni")
import("//build/toolchain/variant.gni")
import("//zircon/system/ulib/hwreg/hwreg_asm_header.gni")

# This has to be variant() rather than config() to be used in the toolchain
# configs list.
variant("strict-align") {
  cflags = [ "-mstrict-align" ]
}

if (toolchain_environment == "kernel.phys" ||
    toolchain_environment == "kernel.efi") {
  # arm64-specific definitions for things in <phys/address-space.h>.
  static_library("address-space") {
    visibility = [ "//zircon/kernel/phys:address-space" ]
    sources = [ "address-space.cc" ]
    include_dirs = [ "//." ]
    deps = [
      "//src/lib/zbitl",
      "//zircon/kernel/lib/arch",
      "//zircon/kernel/lib/boot-options",
      "//zircon/kernel/phys:address-space.header",
      "//zircon/kernel/phys:allocation",
      "//zircon/kernel/phys/lib/memalloc",
      "//zircon/kernel/phys/lib/page-table",
    ]
  }

  static_library("phys") {
    sources = [
      "exception.S",
      "psci.S",
      "psci.cc",
      "reset.cc",
      "setup.cc",
      "start.S",
    ]
    include_dirs = [ "//zircon/.." ]
    deps = [
      ":arch-phys-info",
      ":arch-phys-info-asm",
      "//sdk/lib/zbi-format",
      "//src/lib/zbitl",
      "//zircon/kernel/lib/arch",
      "//zircon/kernel/lib/boot-options",
      "//zircon/kernel/lib/ktl",
      "//zircon/kernel/lib/libc",
      "//zircon/kernel/phys:main",
      "//zircon/kernel/phys:stdio",
      "//zircon/system/ulib/zircon-internal",
    ]
  }

  config("qemu.config") {
    # Like ZBI executables, direct QEMU phys executables can be loaded
    # anywhere, so they are linked at 0.
    configs = [ "//zircon/kernel/phys:load-pic" ]
  }

  source_set("linuxboot") {
    sources = [ "linuxboot-header.S" ]
    deps = [ "//zircon/kernel/lib/arch" ]
    libs = [ "./linuxboot.ld" ]
  }

  source_set("arch-handoff-prep") {
    sources = [ "arch-handoff-prep.cc" ]
    deps = [
      "//sdk/lib/zbi-format",
      "//zircon/kernel/lib/ktl",
      "//zircon/kernel/phys:handoff",
      "//zircon/kernel/phys:handoff-prep.h",
    ]
    include_dirs = [ "//zircon/kernel/phys" ]
  }

  source_set("arch-on-physload-handoff") {
    sources = [ "arch-on-physload-handoff.cc" ]
    include_dirs = [ "//zircon/kernel/phys" ]
    deps = [ "//zircon/kernel/phys:physload.header" ]
  }

  source_set("arch-boot-zbi") {
    sources = [ "arch-boot-zbi.cc" ]
    deps = [
      "//zircon/kernel/lib/arch",
      "//zircon/kernel/phys:boot-zbi.deps",
    ]
  }
}

config("includes") {
  include_dirs = [ "include" ]
}

source_set("arch-phys-info") {
  visibility = [
    "./*",
    "//zircon/kernel/phys/*",
  ]
  public = [ "include/phys/arch/arch-phys-info.h" ]
  public_deps = [ "//zircon/kernel/lib/arch" ]
  public_configs = [ ":includes" ]
}

hwreg_asm_header("arch-phys-info-asm") {
  visibility = [ ":*" ]
  output_name = "arch-phys-info-asm.h"
  sources = [ "gen-arch-phys-info-asm.cc" ]
  deps = [ ":arch-phys-info" ]
}

group("tests") {
  testonly = true
  deps = []
}
