# Copyright 2023 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/images/args.gni")
import("//build/testing/boot_tests/zbi_test.gni")
import("//build/toolchain/toolchain_environment.gni")
import("//zircon/kernel/phys/phys_executable.gni")
import("//zircon/kernel/phys/qemu.gni")
import("//zircon/kernel/phys/shim_executable.gni")

group("boot-shim") {
  deps = [ ":linux-riscv64-boot-shim" ]
}

shim_executable("linux-riscv64-boot-shim") {
  sources = [ "linux-riscv64-boot-shim.cc" ]
  deps = [
    "//zircon/kernel/lib/boot-options",
    "//zircon/kernel/lib/libc",
    "//zircon/kernel/phys:address-space",
    "//zircon/kernel/phys:allocation",
    "//zircon/kernel/phys:arch-phys-info",
    "//zircon/kernel/phys:boot-zbi",
    "//zircon/kernel/phys:exception",
    "//zircon/kernel/phys:main",
    "//zircon/kernel/phys:new",
    "//zircon/kernel/phys:stdio",
    "//zircon/kernel/phys:symbolize",
    "//zircon/kernel/phys:uart",
    "//zircon/kernel/phys/boot-shim:devicetree",
    "//zircon/kernel/phys/lib/boot-shim",
  ]
}

group("tests") {
  testonly = true
}

qemu_shim_phys_tests = [
  "physload-handoff-test",
  "zbi-phys-unittests",
  "zbi-memory-test",
]

qemu_shim_tests = qemu_shim_phys_tests
if (current_cpu == target_cpu) {
  # The userland core-tests binary is built in $default_toolchain, which only
  # supports the "primary" CPU of the build, not additional CPUs for which the
  # kernel and phys tests can be built.
  qemu_shim_tests += [ "core-tests" ]
}

template("qemu_shim_test") {
  zbi_test("linux-riscv64-boot-shim-$target_name") {
    foreach(qemu, qemu_boot_formats) {
      if (qemu.name == qemu_boot_format) {
        qemu_kernel = ":linux-riscv64-boot-shim(${qemu.environment})"
        forward_variables_from(qemu, [ "timeout" ])
      }
    }
    assert(defined(qemu_kernel), "$qemu_boot_format not in $qemu_boot_formats")

    device_types = [ "QEMU" ]

    forward_variables_from(invoker, "*")
  }
}

group("boot_tests") {
  testonly = true
  deps = [ ":boot-shim" ]
  foreach(test, qemu_shim_tests) {
    deps += [ ":linux-riscv64-boot-shim-$test" ]
  }
}

foreach(test, qemu_shim_phys_tests) {
  qemu_shim_test(test) {
    deps = [ "//zircon/kernel/phys/test:$test.zbi($phys_toolchain)" ]
  }
}

qemu_shim_test("core-tests") {
  deps = [ "//zircon/system/utest/core:core-tests.zbi($default_toolchain)" ]

  # This runs the real kernel with full hardware reboot/shutdown support and it
  # isn't necessarily really quick, so don't impose the short timeout used for
  # raw phys tests.
  timeout = false
}
