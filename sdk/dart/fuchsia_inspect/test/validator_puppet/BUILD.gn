# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/dart/dart_component.gni")
import("//build/dart/dart_library.gni")
import("//build/dart/dart_test_component.gni")
import("//build/dart/test.gni")
import("//build/testing/environments.gni")  # Do we need this?

group("tests") {
  testonly = true
  deps = [ ":inspect_validator_test_dart" ]
}

dart_library("lib") {
  package_name = "validator_puppet"
  sources = [ "main.dart" ]
  source_dir = "lib"

  deps = [
    "//sdk/dart/fidl",
    "//sdk/dart/fuchsia_inspect",
    "//sdk/dart/fuchsia_logger",
    "//sdk/dart/fuchsia_services",
    "//src/diagnostics/validator/inspect/fidl:validate_dart",
    "//third_party/dart-pkg/pub/meta",  # Do we need this?
  ]
}

dart_component("puppet_component") {
  # `component_name` must be "puppet" so `children: url: "#meta/puppet.cm"` in
  # //src/diagnostics/validator/inspect/meta/validator.shard.cml can find it.
  component_name = "puppet"
  main_dart = "main.dart"
  main_package = "validator_puppet"
  manifest = "meta/dart-inspect-validator-puppet.cml"
  deps = [ ":lib" ]
}

# The parent component of #validator and #puppet.
fuchsia_test_component("inspect_validator_test_component") {
  component_name = "validator_test"
  manifest = "meta/test.cml"
}

fuchsia_package("dart") {
  testonly = true
  package_name = "dart_puppet"
  deps = [ ":puppet_component" ]
}

fuchsia_test_package("inspect_validator_test_dart") {
  subpackages = [ "//src/diagnostics/validator/inspect:validator" ]
  renameable_subpackages = [
    {
      name = "puppet"
      package = "//sdk/dart/fuchsia_inspect/test/validator_puppet:dart"
    },
  ]
  test_components = [ ":inspect_validator_test_component" ]
}
