# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

group("cpp") {
  testonly = true
  deps = [
    ":archive_reader",
    ":read_visitor",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":inspect_contrib_cpp_tests",
    "tests",
  ]
}

source_set("read_visitor") {
  sources = [ "read_visitor.h" ]

  public_deps = [
    "//sdk/lib/fit",
    "//sdk/lib/fit-promise",
    "//sdk/lib/stdcompat",
    "//zircon/system/ulib/inspect",
  ]
}

source_set("archive_reader") {
  sources = [
    "archive_reader.cc",
    "archive_reader.h",
    "constants.h",
    "inspect.cc",
    "inspect.h",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.diagnostics:fuchsia.diagnostics_hlcpp",
    "//third_party/rapidjson",
    "//zircon/system/ulib/inspect",
  ]

  deps = [
    "//src/lib/fsl",
    "//src/lib/fxl",
  ]
}

source_set("bounded_list_node") {
  sources = [
    "bounded_list_node.cc",
    "bounded_list_node.h",
  ]

  public_deps = [ "//zircon/system/ulib/inspect" ]
  public_configs = [ "//sdk/config" ]
}

executable("bounded_list_node_test") {
  testonly = true
  output_name = "bounded_list_node_test"
  sources = [ "bounded_list_node_unittest.cc" ]
  deps = [
    ":bounded_list_node",
    "//zircon/system/ulib/zxtest",
  ]
}

executable("read_visitor_test") {
  testonly = true
  output_name = "read_visitor_test"
  sources = [ "read_visitor_unittest.cc" ]
  deps = [
    ":read_visitor",
    "//zircon/system/ulib/zxtest",
  ]
}

executable("archive_reader_test") {
  testonly = true
  output_name = "archive_reader_test"
  sources = [ "archive_reader_unittest.cc" ]
  deps = [
    ":archive_reader",
    "//zircon/system/ulib/zxtest",
  ]
}

fuchsia_unittest_component("archive_reader_unittest") {
  deps = [ ":archive_reader_test" ]
}

fuchsia_unittest_component("read_visitor_unittest") {
  deps = [ ":read_visitor_test" ]
}

fuchsia_unittest_component("bounded_list_node_unittest") {
  deps = [ ":bounded_list_node_test" ]
}

fuchsia_test_package("inspect_contrib_cpp_tests") {
  test_components = [
    ":archive_reader_unittest",
    ":read_visitor_unittest",
    ":bounded_list_node_unittest",
  ]
}
