# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/cpp/sdk_source_set.gni")

#  ________  _________  ________  ________
# |\   ____\|\___   ___\\   __  \|\   __  \
# \ \  \___|\|___ \  \_\ \  \|\  \ \  \|\  \
#  \ \_____  \   \ \  \ \ \  \\\  \ \   ____\
#   \|____|\  \   \ \  \ \ \  \\\  \ \  \___|
#     ____\_\  \   \ \__\ \ \_______\ \__\
#    |\_________\   \|__|  \|_______|\|__|
#    \|_________|
#
# This is an allowlist of targets that use the deprecated `inspect` C++ library which relies
# on the diagnostics directory (instead of InspectSink) as well as the deprecated HLCPP bindings.
#
# Developers are encouraged to migrate to use the `//sdk/lib/inspect/component/cpp` library instead.
#
# The policy at this time is:
# 1. Pre-existing use of this library in fuchsia.git is allowlisted.
# 2. Use of this library in new libraries and binaries is strongly discouraged and only allowed
#    under special circumstances under which the new library isn't covering the needs.
#
# To add items to the allowlist, please send a change to one of the OWNERS of
# this file to add an element to the visibility list below.
# Please allowlist entire directories rather than individual targets as it
# requires less allowlist churn over time.
#
# To regenerate:
# fx gn refs $(fx get-build-dir) //sdk/lib/inspect/service/cpp //sdk/lib/inspect/service/cpp:cpp | sed 's|\(.*:\).*|"\1*",|' | sort | uniq
inspect_service_hlcpp_allowlist = [
  "//build/sdk:*",
  "//sdk/lib/inspect/service/cpp:*",
  "//sdk/lib/inspect/service/cpp/tests:*",
  "//sdk/lib/sys/inspect/cpp:*",
  "//sdk/lib/sys/inspect/cpp/tests:*",
  "//src/devices/bin/driver_host2:*",
  "//src/devices/bin/driver_manager:*",
  "//src/devices/bin/driver_manager/v1:*",
  "//src/diagnostics/validator/inspect/lib/cpp:*",
  "//src/storage/minfs:*",
  "//src/ui/scenic/lib/scheduling/tests:*",
]

sdk_source_set("cpp") {
  category = "partner"

  sdk_name = "inspect_service_cpp"

  include_base = "//sdk"
  sources = [
    "reader.cc",
    "reader.h",
    "service.cc",
    "service.h",
    "tree_handler_settings.h",
  ]

  public_deps = [
    "//sdk/fidl/fuchsia.inspect:fuchsia.inspect_hlcpp",
    "//sdk/lib/fit-promise",
    "//zircon/system/ulib/async:async-cpp",
    "//zircon/system/ulib/inspect",
  ]

  public_configs = [ "//sdk/config" ]

  data_deps = [ "//sdk/lib/inspect:client_includes" ]

  visibility = inspect_service_hlcpp_allowlist
}

group("tests") {
  testonly = true
  deps = [ "tests:tests" ]
}
