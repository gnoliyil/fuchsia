# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":registers-visitor-test" ]
  testonly = true
}

devicetree_visitor("registers-visitor") {
  deps = [ ":registers" ]
}

source_set("registers") {
  sources = [
    "registers-visitor.cc",
    "registers-visitor.h",
  ]
  deps = [
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:common-types",
    "//sdk/lib/driver/devicetree/visitors:multivisitor",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.register:fuchsia.register_cpp",
    "//src/lib/ddk",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.registers:fuchsia.hardware.registers_cpp",
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
  ]
}

test("registers-visitor-test-bin") {
  sources = [ "test/registers-visitor-test.cc" ]
  deps = [
    ":registers",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.register:fuchsia.register_cpp",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("registers-dtb") {
  sources = [ "test/dts/registers.dts.S" ]
}

resource("registers-dtb-file") {
  sources = get_target_outputs(":registers-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":registers-dtb" ]
}

fuchsia_unittest_package("registers-visitor-test") {
  component_name = "registers-visitor-test"
  deps = [
    ":registers-dtb-file",
    ":registers-visitor-test-bin",
  ]
}
