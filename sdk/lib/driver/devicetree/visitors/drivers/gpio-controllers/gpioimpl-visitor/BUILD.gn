# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":gpioimpl-visitor-test" ]
  testonly = true
}

devicetree_visitor("gpioimpl-visitor") {
  deps = [ ":gpioimpl" ]
}

source_set("gpioimpl") {
  sources = [
    "gpioimpl-visitor.cc",
    "gpioimpl-visitor.h",
  ]
  deps = [
    "//sdk/lib/driver/component/cpp:cpp",
    "//sdk/lib/driver/devicetree/manager",
    "//sdk/lib/driver/devicetree/visitors:registration",
    "//sdk/lib/driver/logging/cpp",
    "//src/devices/bind/fuchsia:fuchsia_cpp",
    "//src/devices/bind/fuchsia.gpio:fuchsia.gpio_cpp",
    "//src/lib/ddk",
    "//src/lib/ddk:ddk-metadata-headers",
  ]
  public_deps = [
    "//sdk/fidl/fuchsia.hardware.gpioimpl:fuchsia.hardware.gpioimpl_cpp",
    "//sdk/lib/driver/devicetree/visitors:driver-visitor",
  ]
}

test("gpioimpl-visitor-test-bin") {
  sources = [ "test/gpioimpl-visitor-test.cc" ]
  deps = [
    ":gpioimpl",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/ddk:ddk-metadata-headers",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("gpio-dtb") {
  sources = [ "test/dts/gpio.dts.S" ]
}

resource("gpio-dtb-file") {
  sources = get_target_outputs(":gpio-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":gpio-dtb" ]
}

fuchsia_unittest_package("gpioimpl-visitor-test") {
  component_name = "gpioimpl-visitor-test"
  deps = [
    ":gpio-dtb-file",
    ":gpioimpl-visitor-test-bin",
  ]
}
