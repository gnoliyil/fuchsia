# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":mmio-visitor-test" ]
  testonly = true
}

source_set("mmio") {
  sources = [
    "mmio.cc",
    "mmio.h",
  ]
  deps = [ "//sdk/lib/driver/logging/cpp" ]

  public_deps = [
    "//sdk/lib/driver/devicetree/manager:headers",
    "//zircon/system/ulib/zx",
  ]
}

test("mmio-visitor-test-bin") {
  sources = [ "test/mmio-test.cc" ]
  deps = [
    ":mmio",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("mmio-dtb") {
  sources = [ "test/dts/mmio.dts.S" ]
}

resource("mmio-dtb-file") {
  sources = get_target_outputs(":mmio-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":mmio-dtb" ]
}

dtb("ranges-dtb") {
  sources = [ "test/dts/ranges.dts.S" ]
}

resource("ranges-dtb-file") {
  sources = get_target_outputs(":ranges-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":ranges-dtb" ]
}

dtb("not-mmio-dtb") {
  sources = [ "test/dts/not-mmio.dts.S" ]
}
resource("not-mmio-dtb-file") {
  sources = get_target_outputs(":not-mmio-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":not-mmio-dtb" ]
}

# can the two targets below be combined?
fuchsia_unittest_package("mmio-visitor-test") {
  component_name = "mmio-visitor-test"
  deps = [
    ":mmio-dtb-file",
    ":mmio-visitor-test-bin",
    ":not-mmio-dtb-file",
    ":ranges-dtb-file",
  ]
}
