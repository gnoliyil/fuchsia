# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/devicetree/devicetree.gni")
import("//build/test.gni")

group("tests") {
  deps = [ ":bti-visitor-test" ]
  testonly = true
}

source_set("bti") {
  sources = [
    "bti.cc",
    "bti.h",
  ]
  deps = [ "//sdk/lib/driver/logging/cpp" ]

  public_deps = [
    "//sdk/lib/driver/devicetree/manager:headers",
    "//zircon/system/ulib/zx",
  ]
}

test("bti-visitor-test-bin") {
  sources = [ "test/bti-test.cc" ]
  deps = [
    ":bti",
    "//sdk/lib/driver/devicetree/testing:visitor-test-helper",
    "//sdk/lib/driver/devicetree/visitors:registry",
    "//sdk/lib/driver/devicetree/visitors/default/bind-property",
    "//src/lib/fxl/test:gtest_main",
  ]
}

dtb("bti-dtb") {
  sources = [ "test/dts/iommu.dts.S" ]
}

resource("bti-dtb-file") {
  sources = get_target_outputs(":bti-dtb")
  outputs = [ "test-data/{{source_file_part}}" ]
  deps = [ ":bti-dtb" ]
}

# can the two targets below be combined?
fuchsia_unittest_package("bti-visitor-test") {
  component_name = "bti-visitor-test"
  deps = [
    ":bti-dtb-file",
    ":bti-visitor-test-bin",
  ]
}
