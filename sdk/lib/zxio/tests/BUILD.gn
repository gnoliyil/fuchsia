# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/test.gni")

group("tests") {
  testonly = true
  deps = [
    ":zxio-standalone-package",
    ":zxio-unittest-package",
  ]
}

test("zxio") {
  output_name = "zxio-test"
  sources = [
    "c-compilation-test.c",
    "create-test.cc",
    "debuglog-test.cc",
    "directory-test.cc",
    "dirent-test.cc",
    "file-test.cc",
    "file_test_suite.cc",
    "file_test_suite.h",
    "inception-test.cc",
    "null-test.cc",
    "pipe-test.cc",
    "posix-mode-test.cc",
    "remote-test.cc",
    "rights-const-test.cc",
    "socket-test.cc",
    "test_directory_server_base.h",
    "test_file_server_base.h",
    "test_node_server.h",
    "test_socket_server.h",
    "tty-test.cc",
    "vmo-test.cc",
    "watcher-test.cc",
    "zxio-test.cc",
  ]
  deps = [
    "//sdk/fidl/fuchsia.boot:fuchsia.boot_cpp_wire",
    "//sdk/fidl/fuchsia.hardware.pty:fuchsia.hardware.pty_cpp_wire",
    "//sdk/fidl/fuchsia.io:fuchsia.io_cpp_wire_testing",
    "//sdk/fidl/fuchsia.posix.socket:fuchsia.posix.socket_cpp_wire_testing",
    "//sdk/fidl/fuchsia.posix.socket.packet:fuchsia.posix.socket.packet_cpp_wire_testing",
    "//sdk/fidl/fuchsia.posix.socket.raw:fuchsia.posix.socket.raw_cpp_wire_testing",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/fdio",
    "//sdk/lib/fit",
    "//sdk/lib/zxio",
    "//sdk/lib/zxio:create_with_type",
    "//sdk/lib/zxio:inception",
    "//zircon/system/ulib/async-default",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/sync",
    "//zircon/system/ulib/zx",
    "//zircon/system/ulib/zxtest:zxtest-streams",
  ]
}

test("standalone") {
  sources = [ "standalone_main.c" ]

  disable_syslog_backend = true

  deps = [ "//sdk/lib/zxio:standalone" ]
  configs -= [ "//build/config/fuchsia:fdio_config" ]
  configs += [ "//build/config/fuchsia:no_cpp_standard_library" ]
}

rustc_test("zxio_rust_test") {
  edition = "2021"
  sources = [ "zxio-test.rs" ]
  source_root = "zxio-test.rs"
  deps = [
    "//sdk/fidl/fuchsia.io:fuchsia.io_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/storage/vfs/rust:vfs",
    "//src/lib/zircon/rust:fuchsia-zircon",
    "//src/starnix/lib/syncio",
    "//src/storage/fxfs/platform:fxfs-testing",
    "//third_party/rust_crates:assert_matches",
    "//third_party/rust_crates:async-trait",
    "//third_party/rust_crates:futures",
  ]
}

fuchsia_unittest_component("zxio-test") {
  manifest = "meta/zxio_test.cml"
  deps = [ ":zxio" ]
}

fuchsia_unittest_component("zxio-rust-test") {
  manifest = "meta/zxio-rust-test.cml"
  deps = [ ":zxio_rust_test" ]
}

fuchsia_test_package("zxio-unittest-package") {
  test_components = [
    ":zxio-test",
    ":zxio-rust-test",
  ]
}

fuchsia_unittest_component("zxio-standalone") {
  deps = [
    ":standalone",
    "//src/sys/testing/elftest",
  ]
}

fuchsia_test_package("zxio-standalone-package") {
  test_components = [ ":zxio-standalone" ]
}
