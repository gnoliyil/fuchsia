# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# [START build]

import("//sdk/ctf/build/ctf.gni")

group("tests") {
  testonly = true
  deps = [ ":fuchsia-examples-cpp-tests-package" ]
}

ctf_executable("bin") {
  output_name = "ctf_fuchsia_examples_cpp_test"
  sources = [ "main.cc" ]
  deps = [
    "//examples/fidl/fuchsia.examples:fuchsia.examples_hlcpp",
    "//sdk/ctf/tests/examples/fidl/fuchsia.examples/testing/fidl:test.example_hlcpp",
    "//sdk/lib/component/incoming/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp:cpp-macros",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]
  testonly = true
}

ctf_fuchsia_component("test-suite") {
  manifest = "meta/test-suite.cml"
  deps = [ ":bin" ]
  testonly = true
}

ctf_fuchsia_package("fuchsia-examples-cpp-tests") {
  package_name = "fuchsia-examples-cpp-tests"
  testonly = true
  deps = [ ":test-suite" ]
}

fuchsia_test_component("test-root") {
  manifest = "meta/test-root.cml"
  test_type = "ctf"
}

fuchsia_test_package("fuchsia-examples-cpp-tests-package") {
  test_components = [ ":test-root" ]
  subpackages = [
    "//sdk/ctf/tests/examples/fidl/fuchsia.examples/testing/realm-factory:echo-realm-factory",
    ":fuchsia-examples-cpp-tests",
  ]
}
# [END build]
