# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//sdk/ctf/build/ctf.gni")

group("tests") {
  testonly = true
  deps = [ ":fuchsia.driver.test_test" ]
}

ctf_executable("bin") {
  output_name = "driver_test_realm_cts_test"
  testonly = true
  sources = [ "test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

ctf_fuchsia_component("test") {
  component_name = "test"
  manifest = "meta/fuchsia.driver.test_test.cml"
  deps = [ ":bin" ]
  testonly = true
}

ctf_fuchsia_test_package("fuchsia.driver.test_test") {
  enable_ctf_test_realms = true
  plasa_element = "fidl/fuchsia.driver.test/Realm"
  package_name = "fuchsia.driver.test_test"
  test_components = [ ":test" ]
  test_specs = {
    # Allow error logging for absent driver manifest files
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
