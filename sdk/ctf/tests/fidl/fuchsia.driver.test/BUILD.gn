# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//sdk/ctf/build/ctf.gni")

group("tests") {
  testonly = true
  deps = [
    ":fuchsia-driver-test_tests-package",
    # TODO(https://fxbug.dev/116940): Uncomment to include this test in CTF when it no longer
    # depends on the experimental //sdk/lib/device_watcher API.
    # ":fuchsia-driver-test_tests_archive",
  ]
}

ctf_executable("bin") {
  output_name = "driver_test_realm_ctf_test"
  testonly = true
  sources = [ "test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.driver.test:fuchsia.driver.test_hlcpp",
    "//sdk/fidl/fuchsia.io:fuchsia.io_hlcpp",
    "//sdk/lib/device-watcher/cpp",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//zircon/system/ulib/zxtest",
  ]
}

ctf_fuchsia_component("test-suite") {
  manifest = "meta/test-suite.cml"
  deps = [ ":bin" ]
  testonly = true
}

ctf_fuchsia_package("fuchsia-driver-test_tests") {
  testonly = true
  package_name = "fuchsia-driver-test_tests"
  deps = [ ":test-suite" ]
}

fuchsia_test_component("test-root") {
  testonly = true
  manifest = "meta/test-root.cml"
  test_type = "ctf"
}

fuchsia_test_package("fuchsia-driver-test_tests-package") {
  test_components = [ ":test-root" ]
  subpackages = [ ":fuchsia-driver-test_tests" ]
  deps = [
    "//sdk/lib/driver_test_realm",
    "//src/devices/misc/drivers/test-parent",
  ]
  test_specs = {
    # Allow error logging for absent driver manifest files
    log_settings = {
      max_severity = "ERROR"
    }
  }
}
