# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")
import("//build/testing/fuchsia_test_realm_proxy.gni")
import("//sdk/ctf/build/ctf.gni")

ctf_executable("element-manager-test-bin") {
  output_name = "element-manager-test"
  testonly = true
  sources = [ "element_manager_test.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.element:fuchsia.element_hlcpp",
    "//sdk/fidl/fuchsia.testing.harness:fuchsia.testing.harness_hlcpp",
    "//sdk/lib/sys/cpp",
    "//sdk/testing/fidl:fidl_test_hlcpp",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
    "//zircon/system/ulib/zxtest",
  ]
}

ctf_fuchsia_component("default") {
  manifest = "meta/default.cml"
  testonly = true
  deps = [ ":element-manager-test-bin" ]
}

ctf_fuchsia_package("fuchsia-element-tests") {
  package_name = "fuchsia-element-tests"
  testonly = true
  deps = [ ":default" ]
}

fuchsia_component("test-root") {
  manifest = "meta/test-root.cml"
}

fuchsia_test_realm_proxy("test-realm-proxy") {
  proxied_component = "session:reference-session"
  proxied_component_name = "reference-session"
  proxied_protocols = [ "fuchsia.element.Manager" ]
}

fuchsia_test_package("fuchsia-element-tests-latest") {
  test_components = [ ":test-root" ]
  subpackages = [
    ":test-realm-proxy",
    ":fuchsia-element-tests",
  ]
}

group("tests") {
  testonly = true
  deps = [
    ":fuchsia-element-tests-latest",
    ":fuchsia-element-tests_archive",
  ]
}
