# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/components.gni")

# Defines a test realm component that provides capabilities for one or more CTF tests.
#
# The test realm can be accessed at runtime using the URL: '$target_name#meta/default.cm'
#
# Parameters
#
#   manifest (required)
#     The path to the component manifest that describes this test realm.
#     Type: string
#
#   deps
template("ctf_test_realm") {
  assert(defined(invoker.manifest), "A component manifest is required")

  fuchsia_package_with_single_component(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "manifest",
                           ])
    testonly = true
    component_name = "default"
  }
}
