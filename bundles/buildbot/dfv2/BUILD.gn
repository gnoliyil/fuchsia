# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# The dependencies of this group should be the same as //src/buildbot/core. If
# there is a target in //src/buildbot/core's dependencies that is not in this
# group then that either means the target includes a test that is failing and
# has been explicitly omitted or a new target was added to core's deps and this
# assign needs to be updated. Explicitly omitted targets may have been expanded
# into their dependencies and added further down in this group's creation and
# this may be done recursively with the dependencies of those dependencies. A
# target that has been explicitly omitted should also have a TODO comment within
# this group template invocation.
# TODO(fxbug.dev/108563): Remove when `:core` is no longer referenced.
group("core") {
  testonly = true

  # Assign should contain all deps of //src/buildbot/core.
  # TODO(fxbug.dev/108563): Add //src:e2e_tests to this add-assign when all
  # tests in //src:e2e_tests pass with DFv2 enabled.
  deps = [
    "//bundles/buildbot/core:no_e2e",
    "//bundles/fidl:tests",
    "//sdk:tests",
  ]

  # Ensure paths to known e2e test libs are always up-to-date.
  deps += e2e_test_libs

  # Add-assign should contain all deps of //src:e2e_tests.
  # TODO(fxbug.dev/108563): Remove this add-assign when all tests in
  # //src:e2e_tests pass with DFv2 enabled.
  # TODO(fxbug.dev/108563): Add //src/tests to this add-assign when all tests in
  # //src/tests pass with DFv2 enabled
  deps += [
    "//src/connectivity:e2e_tests",
    "//src/recovery:e2e_tests",
    "//src/storage/lib/paver/e2e_tests",
    "//src/sys/pkg:e2e_tests",
  ]

  # Add-assign should contain all deps of //src/tests.
  # TODO(fxbug.dev/108563): Remove this add-assign when all tests in //src/tests
  # pass with DFv2 enabled.
  # TODO(fxbug.dev/108569): Add //src/tests/oom:tests when all tests in
  # //src/tests/oom:tests pass with DFv2 enabled.
  # TODO(fxbug.dev/108571): Add //src/tests/reboot:tests when all tests in
  # //src/tests/reboot:tests pass with DFv2 enabled.
  # TODO(fxbug.dev/108572): Add //src/tests/shutdown:tests when all tests in
  # //src/tests/shutdown:tests pass with DFv2 enabled.
  deps += [
    "//src/tests/assembly:tests",
    "//src/tests/coverage:tests",
    "//src/tests/dfv2:tests",
    "//src/tests/diagnostics:tests",
    "//src/tests/disable_shell:tests",
    "//src/tests/disable_syscalls:tests",
    "//src/tests/dlog:tests",
    "//src/tests/end_to_end/fidlcat:tests",
    "//src/tests/end_to_end/package_manager:tests",
    "//src/tests/end_to_end/snapshot:tests",
    "//src/tests/femu:tests",
    "//src/tests/intl:tests",
    "//src/tests/kernel_cprng:tests",
    "//src/tests/kernel_crashlog:tests",
    "//src/tests/kernel_lockup_detector:tests",
    "//src/tests/kernel_panic:tests",
    "//src/tests/limited_netsvc:tests",
    "//src/tests/microbenchmarks:tests",
  ]
}

# The dependencies of this group should be the same as :host-tests. If there is a
# target in :host-tests's dependencies that is not in this group then that
# either means the target includes a test that is failing and has been
# explicitly omitted or a new target was added to core's deps and this assign
# needs to be updated. Explicitly omitted targets may have been expanded into
# its dependencies and added further down in this group's creation and this may
# be done recursively with the dependencies of those dependencies. A target that
# has been explicitly omitted should also have a TODO comment within this
# group template invocation.
# TODO(fxbug.dev/108563): Remove when `:host-tests` is no longer referenced.
group("host-tests") {
  testonly = true

  deps = [ "//bundles/buildbot/host" ]
}

# The dependencies of this group should be the same as
# //src/buildbot/workstation_host_tests. If there is a target in
# //src/buildbot/workstation_host_tests's dependencies that is not in this group
# then that either means the target includes a test that is failing and has been
# explicitly omitted or a new target was added to workstation_host_tests's deps
# and this assign needs to be updated. Explicitly omitted targets may have been
# expanded into their dependencies and added further down in this group's
# creation and this may be done recursively with the dependencies of those
# dependencies. A target that has been explicitly omitted should also have a
# TODO comment within this group template invocation.
group("workstation_host_tests") {
  testonly = true

  # TODO(fxbug.dev/110053): Add
  # //src/experiences/tests/chrome:test($host_toolchain) to deps when all tests
  # in //src/experiences/tests/chrome:test($host_toolchain) pass with DFv2
  # enabled.
  deps = [
    "//src/experiences/tests/e2e:test($host_toolchain)",
    "//src/experiences/tests/performance:test($host_toolchain)",
  ]
}

# The dependencies of this group should be the same as
# //src/buildbot/workstation. If there is a target in
# //src/buildbot/workstation's dependencies that is not in this group then that
# either means the target includes a test that is failing and has been
# explicitly omitted or a new target was added to workstation's deps and this
# assign needs to be updated. Explicitly omitted targets may have been expanded
# into their dependencies and added further down in this group's creation and
# this may be done recursively with the dependencies of those dependencies. A
# target that has been explicitly omitted should also have a TODO comment within
# this group template invocation.
# TODO(fxbug.dev/105182): Remove when all workstation builder bots with DFv2
# enabled include //bundles/buildbot/workstation instead of :workstation into
# their base package set.
group("workstation") {
  testonly = true
  deps = [
    # Only for products in which Flutter is supported.
    "//src/tests/intl/timezone:tests-for-flutter",

    # CFv2 test framework.
    "//src/sys/run_test_suite",

    # Used for Chromium tests
    "//src/testing/fidl/intl_property_manager",

    # E2E tests w/ default toolchain that do not produce Fuchsia packages, only host bins.
    "//src/tests/end_to_end/sl4f:test",
    "//src/tests/end_to_end/time_sync:test",

    # Other e2e dep bundles
    "//src/experiences/tests/e2e:end_to_end_deps",
    "//src/experiences/tests/performance:performance_deps",
    "//src/tests/end_to_end/bundles:end_to_end_deps",

    # Integration tests
    "//src/ui/tests:workstation_tests",

    # FHCP device tests
    # TODO(fxb/102262): Remove when we are ready.
    "//src/devices/tests/fhcp:device-tests",

    # FIDL example tests
    "//examples/fidl:tests",
  ]

  deps += [ ":workstation_host_tests" ]
}
