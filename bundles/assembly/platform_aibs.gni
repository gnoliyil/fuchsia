# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/icu/config.gni")  # icu_git_info

platform_aibs_dir = get_label_info("//bundles/assembly", "target_out_dir")

# The names of all of the platform's 'testonly=false' Assembly Input Bundles
# LINT.IfChange
user_platform_aib_names = [
  ###
  # The common platform bundles

  # The bootstrap feature-set-level
  "bootstrap",

  ## The core realm bundles

  # `/core` itself
  "core_realm",
  "core_realm_user_and_userdebug",

  # The additional children of core we add when we have networking enabled
  "core_realm_networking",

  # The minimal feature-set-level
  "common_minimal",
  "common_minimal_userdebug",

  # Emulator Support
  "emulator_support",

  ###
  # Feature-level / Subsystem-level bundles

  # Graphics
  "virtcon",

  # SWD (Software Delivery)
  "omaha_client",

  # Kernel args
  "kernel_args_user",
  "kernel_args_userdebug",

  # Netstack
  "netstack2",
  "netstack3",

  # WLAN
  "wlan_base",
  "wlan_legacy_privacy_support",
  "wlan_contemporary_privacy_only_support",
  "wlan_fullmac_support",
  "wlan_softmac_support",

  # Storage
  "fshost_fxfs",

  # Session
  "session_manager",

  "starnix_support",
  "virtualization_support",
  "empty_live_usb",
  "live_usb",

  # ICU-specific version of intl_services
  "intl_services.icu_default_${icu_git_info.default}",
  "intl_services.icu_latest_${icu_git_info.latest}",
  "intl_services.icu_stable_${icu_git_info.stable}",

  # Driver Framework
  "driver_framework_v1",
  "driver_framework_v2",

  # UI
  "ui",
  "ui_legacy",
  "ui_user_and_userdebug",
  "ui_package_user_and_userdebug",
  "ui_legacy_package_user_and_userdebug",
]

# LINT.ThenChange(platform_aib_names.bzl)

# The names of all userdebug-allowed platform Assembly Input Bundles
#LINT.IfChange
_userdebug_aib_names = [
  # Needed by both eng and userdebug products, but contains things disallowed in
  # user.
  "core_realm_development_access",
]

# LINT.ThenChange(platform_aib_names.bzl)
userdebug_platform_aib_names = user_platform_aib_names + _userdebug_aib_names

# The names of all of the platform's Assembly Input Bundles.
# LINT.IfChange
_eng_aib_names = [
  # the core realm additions for eng build-type assemblies
  "core_realm_eng",

  "common_minimal_eng",

  # SWD (Software Delivery)
  "system_update_checker",

  # Kernel args
  "kernel_args_eng",

  # Testing Support
  "testing_support",

  # UI
  "ui_eng",
  "ui_package_eng",
  "ui_legacy_package_eng",

  # Example AIB
  "example_assembly_bundle",
]
eng_platform_aib_names = userdebug_platform_aib_names + _eng_aib_names

# LINT.ThenChange(platform_aib_names.bzl)

user_platform_aib_labels = []
user_platform_aib_files = []
foreach(name, user_platform_aib_names) {
  label = "//bundles/assembly:${name}"
  _target_out_dir = get_label_info(label, "target_out_dir")

  user_platform_aib_labels += [ label ]
  user_platform_aib_files +=
      [ "${_target_out_dir}/${name}/assembly_config.json" ]
}

userdebug_platform_aib_labels = []
userdebug_platform_aib_files = []
foreach(name, userdebug_platform_aib_names) {
  label = "//bundles/assembly:${name}"
  _target_out_dir = get_label_info(label, "target_out_dir")

  userdebug_platform_aib_labels += [ label ]
  userdebug_platform_aib_files +=
      [ "${_target_out_dir}/${name}/assembly_config.json" ]
}

eng_platform_aib_labels = []
eng_platform_aib_files = []
foreach(name, eng_platform_aib_names) {
  label = "//bundles/assembly:${name}"
  _target_out_dir = get_label_info(label, "target_out_dir")

  eng_platform_aib_labels += [ label ]
  eng_platform_aib_files +=
      [ "${_target_out_dir}/${name}/assembly_config.json" ]
}
