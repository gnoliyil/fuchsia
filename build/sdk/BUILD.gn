# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/python/python_host_test.gni")

if (is_host) {
  python_host_test("sdk_common_unittests") {
    main_source = "sdk_common_unittest.py"
    sources = [ "sdk_common.py" ]
  }

  python_host_test("sdk_element_adapter_test") {
    main_source = "sdk_element_adapter_test.py"
    sources = [ "sdk_element_adapter.py" ]
  }
}

group("tests") {
  testonly = true

  deps = [
    ":sdk_common_unittests($host_toolchain)",
    ":sdk_element_adapter_test($host_toolchain)",
    "meta:tests($host_toolchain)",
    "tests",
    "tools/merge:merge_sdk_test($host_toolchain)",
  ]
}

# Contains all the source sets redistributed with the partner or public SDK that
# should be compatible with all the currently supported API levels.
_sdk_versioned_source_sets_list = [
  "//sdk/lib/driver/runtime:driver_runtime_env_cpp",
  "//sdk/lib/fidl/cpp:cpp",
  "//sdk/lib/fidl/cpp:cpp_sync",
  "//sdk/lib/fit:fit",
  "//sdk/lib/fit-promise:fit-promise",
  "//sdk/lib/fidl/cpp:cpp_base",
  "//sdk/lib/images/cpp:cpp",
  "//sdk/lib/inspect/service/cpp:cpp",
  "//sdk/lib/media/cpp:cpp",
  "//sdk/lib/media/cpp:no_converters",
  "//sdk/lib/modular/cpp:cpp",
  "//sdk/lib/sys/cpp:cpp",
  "//sdk/lib/sys/inspect/cpp:cpp",
  "//sdk/lib/sys/service/cpp:cpp",
  "//sdk/lib/syslog/structured_backend",
  "//sdk/lib/ui/scenic/cpp:cpp",
  "//sdk/lib/ui/scenic/cpp/testing:testing",
  "//sdk/lib/vfs/cpp:cpp",
  "//src/lib/fidl/cpp:hlcpp_conversion",
  "//src/lib/fidl/cpp:natural_ostream",
]

group("sdk_versioned_source_sets") {
  deps = _sdk_versioned_source_sets_list
}

group("sdk_versioned_source_sets_allowlist") {
  visibility =
      _sdk_versioned_source_sets_list + [ ":sdk_versioned_source_sets" ]
}
