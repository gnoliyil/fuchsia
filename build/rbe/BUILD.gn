# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/python/python_host_test.gni")
import("//build/python/python_library.gni")

group("tests") {
  testonly = true

  deps = [
    ":cl_utils_test($host_toolchain)",
    ":cxx_remote_wrapper_test($host_toolchain)",
    ":cxx_test($host_toolchain)",
    ":relativize_args_test($host_toolchain)",
    ":remote_action_test($host_toolchain)",
    ":rustc_remote_wrapper_test($host_toolchain)",
  ]
}

if (is_host) {
  # TODO: python_library() doesn't work with the current source structure.
  # It wants one __init__.py per library with one library per directory.
  # As a workaround, we expand lists of sources instead of a proper
  # dependency tree of libraries.  :(
  _fuchsia_sources = [ "fuchsia.py" ]
  _cl_utils_sources = [ "cl_utils.py" ]
  _remote_action_sources =
      [ "remote_action.py" ] + _fuchsia_sources + _cl_utils_sources
  _cxx_sources = [ "cxx.py" ] + _cl_utils_sources
  _cxx_remote_wrapper_sources =
      [ "cxx_remote_wrapper.py" ] + _cxx_sources + _remote_action_sources
  _rustc_remote_wrapper_sources = [ "rustc_remote_wrapper.py" ]

  python_library("cl_utils") {
    sources = _cl_utils_sources
  }
  python_host_test("cl_utils_test") {
    main_source = "cl_utils_test.py"
    sources = _cl_utils_sources
  }

  python_library("cxx") {
    sources = _cxx_sources
  }
  python_host_test("cxx_test") {
    main_source = "cxx_test.py"
    sources = _cxx_sources
  }

  python_library("cxx_remote_wrapper") {
    sources = _cxx_remote_wrapper_sources
  }
  python_host_test("cxx_remote_wrapper_test") {
    main_source = "cxx_remote_wrapper_test.py"
    sources = _cxx_remote_wrapper_sources
  }

  python_library("fuchsia") {
    sources = _fuchsia_sources
  }

  python_library("relativize_args") {
    sources = [ "relativize_args.py" ]
  }
  python_host_test("relativize_args_test") {
    main_source = "relativize_args_test.py"
    sources = [ "relativize_args.py" ]
  }

  python_library("remote_action") {
    sources = _remote_action_sources
  }
  python_host_test("remote_action_test") {
    main_source = "remote_action_test.py"
    sources = _remote_action_sources
  }

  python_library("rustc_remote_wrapper") {
    sources = _rustc_remote_wrapper_sources
  }
  python_host_test("rustc_remote_wrapper_test") {
    main_source = "rustc_remote_wrapper_test.py"
    sources = _rustc_remote_wrapper_sources
    libraries = [ "//third_party/parameterized" ]
  }
}
