# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dev.gni")
import("//src/developer/ffx/build/ffx_action.gni")

# Validates that all use statements for protocols & directories has a valid
# expose-offer-use chain and in the case of directories that the correct
# rights are provided.
#
# Parameters
#
#   product_bundle_path (required)
#     [path] Path to product bundle.
#
#   allowlist (required)
#     [list of strings] paths to allowlist files to be passed to
#     `ffx scrutiny verify routes`.
#
#   component_tree_config (optional)
#     [string] paths to component tree configuration file to be passed to
#     `ffx scrutiny verify routes`.
#
#   recovery (optional)
#     [bool] Analyze recovery images rather than default fuchsia images.
#
#   testonly, deps, public_deps, data_deps, visibility (optional)
#     Usual GN meaning.

template("verify_routes") {
  assert(defined(invoker.product_bundle_path),
         "verify_bootfs_filelist() must specify product_bundle_path")
  assert(defined(invoker.allowlist), "verify_routes() must specify allowlist")

  ffx_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])

    depfile = "$target_out_dir/$target_name.d"
    stamp_file = "$target_gen_dir/$target_name.verified"
    tmp_dir = "$target_out_dir/$target_name/tmp"
    pb_manifest = "${invoker.product_bundle_path}/product_bundle.json"

    hermetic_action_ignored_prefixes = [ tmp_dir ]

    inputs = [ pb_manifest ] + invoker.allowlist
    outputs = [ stamp_file ]

    # The target below is generated as a part of the `ffx_tool` action at
    # `//src/developer/ffx/plugins/scrutiny:ffx_scrutiny_tool`. See there
    # for more information.
    ffx_tool =
        "//src/developer/ffx/plugins/scrutiny:ffx_scrutiny_tool_versioned"
    ffx_tool_output_name = "ffx-scrutiny"

    args = [
      "scrutiny",
      "verify",
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--stamp",
      rebase_path(stamp_file, root_build_dir),
      "--tmp-dir",
      rebase_path(tmp_dir, root_build_dir),
    ]

    if (defined(invoker.recovery) && invoker.recovery) {
      args += [ "--recovery" ]
    }

    args += [
      "routes",
      "--product-bundle",
      rebase_path(invoker.product_bundle_path, root_build_dir),

      ## Uncomment the lines below to fail on route verification warnings. Other valid option values
      ## include "all" and "verbose", but non-warning/error messages do not cause a non-zero status
      ## code.
      # "--response-level",
      # "warn",
    ]

    if (defined(invoker.component_tree_config)) {
      inputs += [ invoker.component_tree_config ]
      args += [
        "--component-tree-config",
        rebase_path(invoker.component_tree_config, root_build_dir),
      ]
    }

    foreach(allowlist, invoker.allowlist) {
      args += [
        "--allowlist",
        rebase_path(allowlist, root_build_dir),
      ]
    }
  }
}
