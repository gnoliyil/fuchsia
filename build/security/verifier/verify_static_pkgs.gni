# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dev.gni")
import("//src/developer/ffx/build/ffx_action.gni")

# Check the static pkgs list against a golden file.
#
# The target that generates 'zbi' needs to be added to deps.
#
# Parameters
#
#   product_bundle_path (required)
#     [path] Path to product bundle.
#
#   goldens (required)
#     [list of strings] Path to a list of golden files that contain golden
#     static pkgs list in the format of one static pkg name per line. The actual
#     static pkgs list must match either one of the goldens. There should be
#     only one golden file in this list for normal case and two golden files,
#     one for the old golden file, one for the new golden file during a soft
#     transition.
#
#   recovery (optional)
#     [bool] Analyze recovery images rather than default fuchsia images.
#
#   deps, public_deps, data_deps (optional)
#     Usual GN meaning.

template("verify_static_pkgs") {
  assert(defined(invoker.product_bundle_path),
         "verify_bootfs_filelist() must specify product_bundle_path")
  assert(defined(invoker.goldens), "verify_static_pkgs() must specify goldens")

  ffx_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])

    no_output_dir_leaks = false

    stamp_file = "$target_gen_dir/$target_name.verified"
    depfile = "$target_gen_dir/$target_name.d"
    tmp_dir = "$target_out_dir/$target_name/tmp"
    pb_manifest = "${invoker.product_bundle_path}/product_bundle.json"

    hermetic_action_ignored_prefixes = [ tmp_dir ]

    inputs = [ pb_manifest ] + invoker.goldens
    outputs = [ stamp_file ]

    # The target below is generated as a part of the `ffx_tool` action at
    # `//src/developer/ffx/plugins/scrutiny:ffx_scrutiny_tool`. See there
    # for more information.
    ffx_tool = "//src/developer/ffx/plugins/scrutiny:ffx_scrutiny_tool"
    ffx_tool_output_name = "ffx-scrutiny"

    args = [
      "scrutiny",
      "verify",
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--stamp",
      rebase_path(stamp_file, root_build_dir),
      "--tmp-dir",
      rebase_path(tmp_dir, root_build_dir),
    ]

    if (defined(invoker.recovery) && invoker.recovery) {
      args += [ "--recovery" ]
    }

    args += [
      "static-pkgs",
      "--product-bundle",
      rebase_path(invoker.product_bundle_path, root_build_dir),
    ]

    foreach(golden, invoker.goldens) {
      args += [
        "--golden",
        rebase_path(golden, root_build_dir),
      ]
    }
  }
}
