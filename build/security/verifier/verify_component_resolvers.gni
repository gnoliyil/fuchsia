# Copyright 2021 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dev.gni")
import("//src/developer/ffx/build/ffx_action.gni")

# Validates that privileged component resolvers only resolve allowed static components,
# determined by the provided allowlist.
#
# Parameters
#
#   product_bundle_path (required)
#     [path] Path to product bundle.
#
#   allowlist (required)
#     [string] Path to the allowlist defining which component resolvers are
#     privileged and which components are allowed to be resolved by them.
#
#   recovery (optional)
#     [bool] Analyze recovery images rather than default fuchsia images.
#
#   deps, public_deps, data_deps (optional)
#       Usual GN meaning.

template("verify_component_resolvers") {
  assert(defined(invoker.product_bundle_path),
         "verify_bootfs_filelist() must specify product_bundle_path")
  assert(defined(invoker.allowlist),
         "verify_component_resolvers() must specify image_assembler_target")

  ffx_action(target_name) {
    forward_variables_from(invoker,
                           [
                             "testonly",
                             "deps",
                             "public_deps",
                             "data_deps",
                             "visibility",
                           ])
    depfile = "$target_out_dir/$target_name.d"
    stamp_file = "$target_gen_dir/$target_name.verified"
    tmp_dir = "$target_out_dir/$target_name/tmp"
    pb_manifest = "${invoker.product_bundle_path}/product_bundle.json"

    hermetic_action_ignored_prefixes = [ tmp_dir ]

    inputs = [
      pb_manifest,
      invoker.allowlist,
    ]
    outputs = [ stamp_file ]

    # The target below is generated as a part of the `ffx_tool` action at
    # `//src/developer/ffx/plugins/scrutiny:ffx_scrutiny_tool`. See there
    # for more information.
    ffx_tool = "//src/developer/ffx/plugins/scrutiny:ffx_scrutiny_tool"
    ffx_tool_output_name = "ffx-scrutiny"

    args = [
      "scrutiny",
      "verify",
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--stamp",
      rebase_path(stamp_file, root_build_dir),
      "--tmp-dir",
      rebase_path(tmp_dir, root_build_dir),
    ]

    if (defined(invoker.recovery) && invoker.recovery) {
      args += [ "--recovery" ]
    }

    args += [
      "component-resolvers",
      "--product-bundle",
      rebase_path(invoker.product_bundle_path, root_build_dir),
      "--allowlist",
      rebase_path(invoker.allowlist, root_build_dir),
    ]
  }
}
