# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/assembled_system.gni")
import("//build/assembly/legacy_image_metadata_overrides.gni")
import("//build/assembly/product_assembly_configuration.gni")
import("//build/board.gni")
import("//build/images/args.gni")
import("//build/images/custom_signing.gni")
import("//build/images/vboot/vboot.gni")

assert(current_toolchain == default_toolchain,
       "//build/images/* are only valid in the Fuchsia toolchain")

if (has_board) {
  assert(bringup_images_config_label != false,
         "Need to define bringup_images_config_label")
  assert(bringup_fastboot_images_config_label != false,
         "Need to define bringup_fastboot_images_config_label")
} else {
  bringup_images_config_label = "//boards/images:bringup_default"
  bringup_fastboot_images_config_label =
      "//boards/images:bringup_fastboot_default"
}

product_assembly_configuration("product_assembly_config") {
  platform = {
    feature_set_level = "bootstrap"
    build_type = "eng"
  }

  # The bringup product can't specify anything in base/cache packages, as it
  # doesn't have an fvm with blobfs.
  product = {
  }
}

bringup_base = {
  testonly = true
  output_dir = root_build_dir
  generate_fvm = false

  product_assembly_config_label = ":product_assembly_config"
  use_bringup_platform_bundles_only = true

  include_component_id_index = true
  include_shell_commands = false
  include_config_data = false

  cmdline_deps = [ "//build/input:bootfs" ]

  base_packages = []
  base_driver_packages = [
    "//:developer_base_driver_packages",
    "//:legacy_base_driver_packages",
  ]

  bootfs_labels = base_driver_packages + [
                    "//build/input:bootfs",
                    "//:developer_base_packages",
                  ]

  _ignored = {
    import("//build/dev.gni")
    import("//build/product.gni")
    all_empty = true
    if (dev_system_image_deps != []) {
      print("dev_system_image_deps not empty: ${dev_system_image_deps}")
      all_empty = false
    }
    if (product_system_image_deps != []) {
      print("product_system_image_deps not empty: ${product_system_image_deps}")
      all_empty = false
    }
    assert(all_empty)
  }
}

# This target generates a (potentially signed) bringup ZBI and a corresponding
# Vbmeta. Unfortunately, the resulting ZBI cannot be passed directly into a
# fastboot boot invocation, as this command expects the image it is passed to
# be a concatenation of the ZBI and Vbmeta. The bringup_fastboot target below
# is responsible for generating that image.
assembled_system("bringup_non_fastboot") {
  forward_variables_from(bringup_base, "*")
  assembly_images_config_label = bringup_images_config_label
  image_name = "bringup"
  if (custom_signing_script != "") {
    zbi_signing_script = custom_signing_script
    zbi_signing_script_deps = [ "//build/images/custom_signing:deps" ]
    inputs = custom_signing_script_inputs
  } else if (use_vboot) {
    zbi_signing_script = vboot_action.script
    zbi_signing_args = vboot_action.args
    zbi_signing_script_deps = vboot_action.deps
    inputs = vboot_action.inputs
  }

  image_metadata_overrides = legacy_bringup_image_metadata_overrides
}

# Builds a bringup image that we can RAM boot with fastboot boot.
assembled_system("bringup_fastboot") {
  forward_variables_from(bringup_base, "*")
  assembly_images_config_label = bringup_fastboot_images_config_label
  zbi_signing_script = "concat_zbi_and_vbmeta.sh"
  zbi_signing_args = []
  if (use_vbmeta || use_vboot) {
    assembly_out_dir = rebase_path(target_out_dir, root_build_dir)
    zbi_signing_args = [
      "-v",
      "${assembly_out_dir}/${target_name}/bringup_fastboot.vbmeta",
    ]
  }
  if (custom_signing_script != "") {
    rebased_script = rebase_path(custom_signing_script, root_build_dir)
    zbi_signing_args += [ "-s ${rebased_script}" ]
    zbi_signing_script_deps = [ "//build/images/custom_signing:deps" ]
    inputs = custom_signing_script_inputs
  } else if (use_vboot) {
    rebased_script = rebase_path(vboot_action.script, root_build_dir)
    zbi_signing_args += [ "-s ${rebased_script}" ] + vboot_action.args
    zbi_signing_script_deps = vboot_action.deps
    inputs = vboot_action.inputs
  }

  image_metadata_overrides = legacy_bringup_fastboot_image_metadata_overrides
}

group("bringup") {
  testonly = true
  deps = [ ":bringup_fastboot" ]
  public_deps = [ ":bringup_non_fastboot" ]
}
