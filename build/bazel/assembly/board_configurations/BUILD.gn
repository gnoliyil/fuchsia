# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import(
    "//build/bazel/assembly/board_configurations/verify_board_configuration.gni")
import("//build/python/python_binary.gni")

verification_targets = []
foreach(board,
        [
          "x64",
          "qemu-x64",
          "vim3",
        ]) {
  target_label = "verify_${board}"
  verification_targets += [ ":${target_label}" ]
  verify_board_configuration(target_label) {
    bazel_board_config_target = "//boards:${board}"
    golden_json = "//boards/${board}/board_information.json5"
  }
}

group("verifications") {
  testonly = true
  deps = verification_targets
}

python_binary("verify_board_configuration") {
  main_source = "//build/bazel/assembly/board_configurations/verify_board_configuration.py"
  main_callable = "main"
  deps = [ "//third_party/pyjson5" ]
}
