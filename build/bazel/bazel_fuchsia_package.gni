# Copyright 2022 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/bazel/bazel_build_action.gni")
import("//build/packages/prebuilt_package.gni")

# Wraps a `fuchsia_package()` in a `BUILD.bazel` file.
# Use this template to make a `fuchsia_package()` target in a `BUILD.bazel`
# file be available in the GN build. Depending on the GN target will ensure
# that the corresponding Bazel target is built.
#
# Example:
#   BUILD.bazel:
#     fuchsia_package(
#       name = "pkg",
#       package_name = "example_package",
#       ...
#     )
#   BUILD.gn:
#     bazel_fuchsia_package("pkg") {
#       package_name = "example_package"
#     }
#
# Args:
#   bazel_target: (optional)
#     Bazel target to wrap.
#     This target should be a `fuchsia_package()` target.
#     Default: //<target_dir>:target_name
#     For instance: `bazel_fuchsia_package("foo") {}` in `//bar/BUILD.gn`
#     will wrap the target `//bar:foo` defined in `//bar/BUILD.bazel`.
#
#   package_name: (optional)
#     The `package_name` of the wrapped `fuchsia_test_package`.
#     Default: target_name
#
#   deps:
#   testonly:
#   visibility:
#     Usual GN meaning.
template("bazel_fuchsia_package") {
  if (defined(invoker.bazel_target)) {
    bazel_target = invoker.bazel_target
  } else {
    bazel_target = get_label_info(":$target_name", "label_no_toolchain")
  }

  # We need the `.far` file, but building the package outputs it in `bazel-out/`
  # which is an unpredictable path.
  # Instead, build the `.publish` target and read the runfiles of the
  # `.publish.sh` script under `bazel-bin/`.
  # See: fxbug.dev/115699#c4
  bazel_target += ".publish"

  if (defined(invoker.package_name)) {
    package_name = invoker.package_name
  } else {
    package_name = target_name
  }

  bazel_build_target = "$target_name.bazel_build"
  bazel_build_action(bazel_build_target) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "metadata",
                             "testonly",
                             "visibility",
                           ])
    bazel_targets = [ bazel_target ]
    _bazel_target_dir = rebase_path(get_label_info(bazel_target, "dir"), "//")

    # Read the `.far` from the runfiles of the `.publish.sh` output
    # under `bazel-bin/`.
    # See: fxbug.dev/115699#c4
    bazel_outputs = [ "bazel-bin/$_bazel_target_dir/_${invoker.target_name}.publish.sh.runfiles/main/$_bazel_target_dir/$package_name.far" ]
    ninja_outputs = [ "$package_name.far" ]
  }

  # From the perspective of GN/Ninja, this package is a prebuilt
  # even though we just built it ourselves.
  prebuilt_package(target_name) {
    forward_variables_from(invoker,
                           [
                             "deps",
                             "testonly",
                             "visibility",
                           ])
    package_name = package_name
    archive = "$target_gen_dir/$package_name.far"
    if (!defined(deps)) {
      deps = []
    }
    deps += [ ":$bazel_build_target" ]
  }
}
