# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/zircon/standard.gni")
import("//build/toolchain/zircon/zircon_toolchain_suite.gni")

if (current_toolchain == default_toolchain) {
  foreach(cpu, standard_fuchsia_cpus) {
    zircon_toolchain_suite("user.basic_$cpu") {
      cpu = cpu
      os = "fuchsia"
      strip = "--strip-sections"
      environment = "user.basic"

      configs = [
        "//build/config/zircon:user",
        "//build/config:no-compiler-abi",
      ]

      # Since //build/config/zircon:user-executable uses BFD, we can't use ICF.
      # It's only supported by Gold and LLD.  But we can't use Gold for
      # executables because it doesn't have --no-dynamic-linker for static PIE.
      remove_executable_configs = [ "//build/config/zircon:default_icf" ]
      executable_configs = [ "//build/config/zircon:user-executable" ]

      tags = [
        "no-compiler-abi",
        "standalone",
      ]
      exclude_variant_tags = [ "needs-compiler-abi" ]
    }
  }
}
