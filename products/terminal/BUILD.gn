# Copyright 2022 The Fuchsia Authors.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/product_assembly_configuration.gni")
import("//build/assembly/sshd_config.gni")
import("//build/info/info.gni")
import("//src/sys/core/build/core_shard.gni")

assert(
    is_fuchsia,
    "Product definitions and assemblies should only be defined in the fuchsia toolchain")

core_shard("tiles_flatland_core_shard") {
  shard_file = "tiles_flatland.core_shard.cml"
}

product_assembly_configuration("terminal") {
  platform = {
    build_type = "eng"

    input_groups = {
      group1 = true
    }

    storage = {
      configure_fshost = true
      filesystems = {
        volume = {
          fvm = {
            data = {
            }
            blob = {
            }
          }
        }
      }
    }

    development_support = {
      include_sl4f = true

      # If the 'authorized_ssh_keys_path' GN arg has been set, then pass that path
      # as part of the platform configuration.
      if (authorized_ssh_keys_path != false) {
        authorized_ssh_keys_path =
            rebase_path(authorized_ssh_keys_path, root_build_dir)
      }
    }

    ui = {
      enabled = true
      supported_input_devices = [
        "button",
        "touchscreen",
      ]
    }

    media = {
      audio = {
        full_stack = {
        }
      }
    }

    intl = {
      config_type = "default"
    }

    icu = {
      revision = "default"
    }

    setui = {
      use_icu = "with_icu"
      with_camera = false
    }

    fonts = {
      # See base_packages below.
      font_collection = "open-fonts-collection"
    }

    session = {
      enabled = true

      # avoid impacting existing terminal users like chromium tests
      autolaunch = false
    }
  }
  product = {
    session_url =
        "fuchsia-pkg://fuchsia.com/tiles-session#meta/tiles-session.cm"
    build_info = default_product_build_info
  }

  base_packages = [
    {
      # Provides font assets for `platform.fonts.font_collection` above.
      package_target = "//src/fonts/asset_providers:open-fonts-collection.asset-provider.package"
    },
  ]

  deps = [ "//build/info:build_info_files" ]
}
