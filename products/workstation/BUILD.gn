# Copyright 2022 The Fuchsia Authors.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/assembly/product_assembly_configuration.gni")
import("//build/bazel/bazel_inputs.gni")
import("//build/dist/resource.gni")

assert(
    is_fuchsia,
    "Product definitions and assemblies should only be defined in the fuchsia toolchain")

# LINT.IfChange
workstation_platform_config = {
  build_type = "eng"
  battery = {
    enabled = true
  }
  identity = {
    password_pinweaver = "required"
  }
  input = {
    supported_input_devices = [
      "keyboard",
      "mouse",
      "touchscreen",
    ]
  }
  storage = {
    live_usb_enabled = true
    configure_fshost = false
  }
  ui = {
    enabled = true
    legacy = true
  }
  virtualization = {
    enabled = true
  }
  fonts = {
    enabled = true
    font_collection = "open-fonts-collection"
  }
}

# LINT.ThenChange(//build/bazel/assembly/product_configurations/BUILD.bazel)

_workstation_font_asset_provider_packages = [
  "//src/fonts/asset_providers:open-fonts-collection.asset-provider.package",
]

workstation_product_config = {
  session_url = "fuchsia-pkg://fuchsia.com/tiles-session#meta/tiles-session.cm"
}

product_assembly_configuration("workstation") {
  platform = workstation_platform_config
  product = workstation_product_config
  font_asset_provider_packages = _workstation_font_asset_provider_packages
}

# TODO(https://fxbug.dev/107089) remove this product once workstation_eng can start paused
product_assembly_configuration("workstation_paused") {
  platform = workstation_platform_config
  product = {
    # We want to inherit all of workstation_eng's config except the session URL.
    forward_variables_from(workstation_product_config, "*", [ "session_url" ])

    session_url = ""
  }
  font_asset_provider_packages = _workstation_font_asset_provider_packages
}

group("bootfs") {
  deps = [ ":workstation-config" ]
}

resource("workstation-config") {
  sources = [ "config/workstation.profiles" ]
  outputs = "config/profiles/workstation.profiles"
}
