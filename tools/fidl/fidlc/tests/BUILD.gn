# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test.gni")
import("//build/testing/host_test_data.gni")
import("//tools/fidl/fidlc/testdata/info.gni")

group("tests") {
  testonly = true
  deps = [ ":fidlc-test($host_toolchain)" ]
}

host_test_data("errcat_fidl_bad") {
  _dir = "fidl/bad"
  sources = rebase_path(read_file("$_dir/files.txt", "list lines"), ".", _dir)
  outputs = [ "$root_out_dir/fidlc-tests/bad/{{source_file_part}}" ]
}

host_test_data("errcat_fidl_good") {
  _dir = "fidl/good"
  sources = rebase_path(read_file("$_dir/files.txt", "list lines"), ".", _dir)
  outputs = [ "$root_out_dir/fidlc-tests/good/{{source_file_part}}" ]
}

host_test_data("errcat_md") {
  _dir = "//docs/reference/fidl/language/error-catalog"
  sources = rebase_path(read_file("$_dir/_files.txt", "list lines"), ".", _dir)
  outputs = [ "$root_out_dir/fidlc-tests/error-catalog/{{source_file_part}}" ]
}

host_test_data("errcat_other") {
  sources = [
    "//docs/error/_redirects.yaml",
    "//docs/reference/fidl/language/errcat.md",
    "errcat_good_tests.cc",
  ]
  outputs = [ "$root_out_dir/fidlc-tests/{{source_file_part}}" ]
}

test("fidlc-test") {
  sources = [
    "alias_tests.cc",
    "array_tests.cc",
    "attributes_tests.cc",
    "availability_interleaving_tests.cc",
    "bits_tests.cc",
    "canonical_names_tests.cc",
    "coded_types_generator_tests.cc",
    "compiled_transformer_tests.cc",
    "consts_tests.cc",
    "declaration_order_tests.cc",
    "direct_dependencies_tests.cc",
    "enums_tests.cc",
    "errcat_docs_tests.cc",
    "errcat_good_tests.cc",
    "errors_tests.cc",
    "fix_tests.cc",
    "flat_ast_tests.cc",
    "flexible_tests.cc",
    "formatter_tests.cc",
    "generated_name_tests.cc",
    "handle_tests.cc",
    "json_diagnostics_tests.cc",
    "json_findings_tests.cc",
    "library_tests.cc",
    "lint_findings_tests.cc",
    "lint_tests.cc",
    "method_tests.cc",
    "new_type_tests.cc",
    "ordinals_tests.cc",
    "overlay_tests.cc",
    "parsed_transformer_tests.cc",
    "parsing_tests.cc",
    "protocol_tests.cc",
    "recoverable_compilation_tests.cc",
    "recoverable_parsing_tests.cc",
    "recursion_detector_tests.cc",
    "reporter_tests.cc",
    "resource_tests.cc",
    "resourceness_tests.cc",
    "service_tests.cc",
    "source_map_generator_tests.cc",
    "source_signature_tests.cc",
    "span_tests.cc",
    "strictness_tests.cc",
    "string_array_tests.cc",
    "structs_tests.cc",
    "table_tests.cc",
    "test_library.cc",
    "token_list_tests.cc",
    "token_tests.cc",
    "transport_tests.cc",
    "types_tests.cc",
    "typeshape_tests.cc",
    "union_tests.cc",
    "using_tests.cc",
    "utils_tests.cc",
    "versioning_tests.cc",
    "versioning_types_tests.cc",
    "virtual_source_tests.cc",
  ]
  deps = [
    ":errcat_fidl_bad",
    ":errcat_fidl_good",
    ":errcat_md",
    ":errcat_other",
    "//third_party/boringssl",
    "//tools/fidl/fidlc:fidl",
    "//tools/fidl/fidlc:fidl-fix-cli",
    "//zircon/system/ulib/zxtest",
  ]
}
