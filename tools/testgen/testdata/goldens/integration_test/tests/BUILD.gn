# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/component/config.gni")
import("//build/components.gni")
import("//build/fidl/fidl.gni")
import("//build/rust/rustc_binary.gni")

group("tests") {
  testonly = true
  deps = [ ":example-component-test" ]
}

rustc_test("bin") {
  edition = "2021"
  source_root = "src/main.rs"
  sources = [ source_root ]
  name = "example_component_test"
  deps = [
    "../testing/fidl:test.examplecomponent_rust",
    "//src/lib/fidl/rust/fidl",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//src/testing/realm_proxy",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:tracing",
  ]
}

fuchsia_component("test-suite-component") {
  testonly = true
  component_name = "test-suite"
  manifest = "meta/test-suite.cml"
  deps = [ ":bin" ]
}

fuchsia_package("test-suite") {
  testonly = true
  deps = [ ":test-suite-component" ]
}

fuchsia_component("test-root") {
  testonly = true
  manifest = "meta/test-root.cml"
}

fuchsia_test_package("example-component-test") {
  test_components = [ ":test-root" ]
  subpackages = [
    "../testing/realm-factory",
    ":test-suite",
  ]
}
