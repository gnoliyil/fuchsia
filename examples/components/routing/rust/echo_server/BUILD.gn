# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# [START imports]
import("//build/components.gni")
import("//build/rust/rustc_binary.gni")

# [END imports]

# [START executable]
rustc_binary("bin") {
  output_name = "echo_server_rust"
  edition = "2021"

  deps = [
    "//examples/components/routing/fidl:echo_rust",
    "//src/lib/diagnostics/inspect/runtime/rust",
    "//src/lib/diagnostics/inspect/rust",
    "//src/lib/fuchsia",
    "//src/lib/fuchsia-component",
    "//third_party/rust_crates:anyhow",
    "//third_party/rust_crates:futures",
  ]

  sources = [ "src/main.rs" ]
}

# [END executable]

# [START component]
# Unpackaged component "#meta/echo_server.cm"
fuchsia_component("echo_server_cmp") {
  component_name = "echo_server"
  manifest = "meta/echo_server.cml"
  deps = [ ":bin" ]
}

# [END component]

# Subpackaged component "echo_server#meta/default.cm"
fuchsia_package_with_single_component("echo_server") {
  component_name = "default"
  manifest = "meta/echo_server.cml"
  deps = [ ":bin" ]
}
